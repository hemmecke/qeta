-------------------------------------------------------------------
---
--- FriCAS QEta
--- Copyright (C) 2020-2021  Ralf Hemmecke <ralf@hemmecke.org>
---
-------------------------------------------------------------------
-- This program is free software: you can redistribute it and/or modify
-- it under the terms of the GNU General Public License as published by
-- the Free Software Foundation, either version 3 of the License, or
-- (at your option) any later version.
--
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
-- GNU General Public License for more details.
--
-- You should have received a copy of the GNU General Public License
-- along with this program.  If not, see <http://www.gnu.org/licenses/>.
-------------------------------------------------------------------

OF==>OutputForm
display77(x) ==> display((x::OF)::Formatter(Format1D))
yof x ==> x :: Symbol :: OF
dbgPrint(x,y) ==> display77([yof ":> ", yof x, y::OF]$List(OF))
tracePrint(x,y) ==> display77(hconcat([yof"-- ",yof x,yof":=",y::OF]$List(OF)))

)if LiterateDoc
\documentclass{article}
\usepackage{qeta}
\externaldocument{qeta}

\lstset{literate=%
  {τ}{{$\tau$}}1%
  {η}{{$\eta$}}1%
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\begin{document}
\title{Using the QEta package to find Ramanujan-Kolberg identities}
\author{Ralf Hemmecke}
\date{29-Oct-2020}
\maketitle
\begin{abstract}
  The package \code{QEtaRamanujanKolberg} implements functions to help
  finding Ramanujan-Kolberg identities.
\end{abstract}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\tableofcontents

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Problem}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

We try to find all missing parts to find a Ramanujan-Kolberg relation
as defined in \cite[p.~227]{Radu:RamanujanKolberg:2015}.

Let $M$ be a positive integer and $r=(r_\delta)_{\delta|M}$ be a
sequence of integers indexed by the positive divisors of $M$. Define a
sequence $a(n)$ by
\begin{gather*}
  \sum_{n=0}^\infty a(n) q^n
  =
  \prod_{\delta|M} \prod_{n=1}^\infty (1-q^{\delta n})^{r_\delta}
  =
  \prod_{\delta|M} (q^\delta; q^\delta)_\infty^{r_\delta}
\end{gather*}
where
\begin{gather*}
(u; q)_\infty = \prod_{n=0}^\infty (1-uq^n)
\end{gather*}
is the $q$-Pochhammer symbol.

Given $N$, $m$, and $t \in \{0,\ldots,m-1\}$, we want to compute a
sequence $s=(s_\delta)_{\delta|N}$ of integers, a set
$\mathcal{O}_{r,m,t}\subseteq\{0,\ldots,m=1\}$, eta-quotients
$b_i \in M^\infty(N)$, and numbers $c_i\in \mathbb{Q}$ uch that
\begin{gather*}
q^\alpha \prod_{\delta|N} \prod_{n=1}^\infty (1-q^{\delta n})^{s_\delta}
\times
\prod_{t'\in \mathcal{O}(r,m,t)} \sum_{n=0}^\infty a(mn+t')q^n
=
\sum_{i=1}^k c_i b_i
\end{gather*}
where
\begin{gather*}
\alpha = \sum_{\delta|N} \frac{\delta s_\delta}{24}
         +
         \sum_{t'\in \mathcal{O}(r,m,t)}
             \frac{24 t' + \sum_{\delta|M} \delta r_\delta}{24m}
\end{gather*}
and $M^\infty(N)$ is the set of modular functions on $\Gamma_0(N)$
with a pole at most at infinity.

Each of the eta-quotients is given through a sequence
$r^{(i)}=\left(r^{(i)}_\delta\right)_{\delta|N}$ of integers in its
$q$-expansion at infinity.
\begin{gather*}
  b_i
  = q^{\beta_i} \prod_{\delta|N}
                      \prod_{n=1}^\infty (1-q^{\delta n})^{r^{(i)}_\delta}
  =
  q^{\beta_i} \prod_{\delta|N} (q^\delta, q^\delta)_\infty^{r^{(i)}_\delta}.
\end{gather*}
with
\begin{gather*}
  \beta_i = \sum_{\delta|N} \frac{\delta r^{(i)}_\delta}{24}
\end{gather*}


For mathematical background see \PathName{qeta.tex}.



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Implementation}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Helper macros}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Let us start with a few common macros.

These two technical macros are necessary to distinguish between Rep
and \%.
)endif

rep x ==> (x@%) pretend Rep
per x ==> (x@Rep) pretend %

)if LiterateDoc
Now some abbreviations for common domains.
)endif

P ==> PositiveInteger
N ==> NonNegativeInteger
Z ==> Integer
Q ==> Fraction Z
LZ ==> List Z
LLZ ==> List LZ

Pol ==> Polynomial
SUP ==> SparseUnivariatePolynomial
QETAAUX ==> QEtaAuxiliaryPackage;

asP x ==> x pretend P

T1 C  ==> QEtaTaylorSeries C
L1 C  ==> QEtaLaurentSeries C
A1 C ==> Finite0Series C
EQI C ==> EtaQuotientInfinity C
CA1 C ==> QEtaAlgebraCachedPower(C, A1 C) -- computation domain (cached)
CPol C ==> QEtaAlgebraCachedPower(C, Pol C) -- computation pols in (cached)
X1 C ==> QEtaExtendedAlgebra(C, CA1 C, CPol C)
toX1(C, a, b) ==> embed(a::CA1(C), b::CPol(C))$X1(C)

QXAB1 C ==> QEtaAlgebraBasis(C, X1 C)
QXTOPRED1 C ==> QEtaTopReduction(C, X1 C, QXAB1 C)
QXTOPCOMP1 C ==> QEtaComputation(C, X1 C, QXAB1 C, QXTOPRED1 C)
QXTOPSAMBA1 C ==> QEtaSambaPackage(C, X1 C, QXAB1 C, QXTOPCOMP1 C)

SPEC ==> QEtaSpecification
SPECS ==> List SPEC
QES4TI2 ==> QEtaQuotientSpecifications4ti2


)if LiterateDoc
%$
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Domain QEtaRamanujanKolbergIdentity}
\label{sec:QEtaRamanujanKolbergIdentity}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

We provide here a domain that prints nicely as an identity and has
enough access functions to extract various components of the identity
for easier manipulation.


)endif


)abbrev domain QETARKI QEtaRamanujanKolbergIdentity
QEtaRamanujanKolbergIdentity(C): Exports == Implementation where
  C: EntireRing -- basic coefficient domain
  L ==> List
  EX ==> Expression Z
  Exports ==> CoercibleTo OutputForm with
    -- constructor
    identity: (SPEC, SPEC, P, List N, SPECS, Pol C) -> %
      ++ identity(sspec, rspec, m, orb, rspecss, rel) returns a
      ++ structure that represents all information about the
      ++ q-expansion of $F_{s,r,m,t}$ at infinity, see Definition of
      ++ $F_{s,r,m,t}$ in qeta.tex,
      ++ Equation~\eqref{eq:F_s-r-m-t(tau)}. The relations is then
      ++ c*F=sum_{i}(rel(i)*etaQuotient(rspecs(i))) where F
      ++ corresponds to the modular function $F_{s,r,m,t}$ in
      ++ question. t is an element of orb.
      ++ At the same time this data structure serves as a container
      ++ for a generalized eta-quotient identity for
      ++ $F_{\bar{s},\bar{r},m,t}(\tau)$.

    -- accessor functions
    coSpecification: % -> SPEC
      ++ coExponent(x) returns the specification for the cofactor
      ++ (generalized) eta-quotient that must be muliplied to the
      ++ orbit product in order to obtain a modular function.
    definingSpecification: % -> SPEC
      ++ definingSpecification(x) returns the specification of
      ++ (generalized) eta-quotient that forms the generating series
      ++ of the coefficients a(n), i.e., \sum_{n=0}^\infty a(n)q^n =
      ++
    multiplier: % -> P
      ++ multiplier(x) returns factor m to select a certain
      ++ subsequence a(m*n+k) of the defining series (where k is one
      ++ number out of orbit(x).
    orbit: % -> List N
      ++ orbit(x) returns the orbit, i.e., only a product of the
      ++ generating series for $a(m*n+k)$ for all k in the orbit can
      ++ be turned into a modular function (for Gamma0) by multiplying
      ++ it with an eta-quotient.
    basisSpecifications: % -> SPECS
      ++ basisSpecifications(x) returns the specifications of the
      ++ (generalized) eta-quotients that form the combination of the
      ++ identity. The first of this basis is the special element t so
      ++ that the relation is actually formed by the module over C[t]
      ++ generated by 1 and rest basisSpecifications(x).
    identityPolynomial: % -> Pol C
      ++ identityPolynomial(x) gives the representation of the
      ++ identity as a polynomial (which equates to zero. This
      ++ polynomial is in terms of the variables F and Mi where F
      ++ corresponds to the orbit product together with a prefactor to
      ++ turn this orbit product into a modular function and the Mi
      ++ correspond to eta-quotients given by
      ++ etaQuotientMonoidExponentVectors(x).
    coefficient: % -> C
      ++ coefficient(x) is the coefficient that results from a
      ++ reduction process in case it cannot be inverted. For a
      ++ coefficient field, it will give 1.
    moduleCoefficients: % -> List SUP C
      ++ moduleCoefficients(x) gives the list of coefficients that
      ++ correspond to the (generalized) eta-quotients given by
      ++ basisSpecifications(x) and form the relation that that was
      ++ computed. Note that the first entry of basisSpecification(x)
      ++ must be replaced by 1 in order to make this relation a true
      ++ relation.

    alphaInfinity: % -> Z
      ++ alphaInfinity(x) returns the q exponent that corresponds to
      ++ the orbit product and the respective co-exponent in order to
      ++ make the whole produce a modular function. It is defined
      ++ already in the abstract of \cite{Radu:RamanujanKolberg:2015}.
    orbitProduct: % -> OutputForm
      ++ orbitProduct(x) returns the orbit product as an OutputForm.

-------------------------------------------------------------------

    -- The var-functions are helper functions for printing etaQuotients.
    -- They print the i-th variable.
    var: Symbol -> (Z, Z) -> OutputForm
    varsub: Symbol -> (Z, Z) -> OutputForm
    varEta: (Z, Z) -> OutputForm
    varPochhammer: (Z, Z) -> OutputForm

    varPower: (Z, Z, Z, (Z, Z) -> OutputForm) -> OutputForm
    monomial: (SPEC, (Z, Z) -> OutputForm) -> OutputForm
    monomialQuotient: (SPEC,  (Z, Z) -> OutputForm) -> OutputForm

    etaQuotient: (SPEC, (Z, Z) -> OutputForm) -> OutputForm
      ++ etaQuotient(rspec, v, e) returns the generalized eta-quotient
      ++ given by rspec where each part is
      ++ formatted by the function v.
    qQuotient: (SPEC, (Z, Z) -> OutputForm, Q) -> OutputForm
      ++ qQuotient(rspec, v, e) returns the generalized eta-quotient
      ++ given by rspec where each part is
      ++ formatted by the function v. The whole eta-quatient comes
      ++ multiplied with a q power with exponent e.
    qEtaQuotient: (SPEC, (Z, Z) -> OutputForm) -> OutputForm
      ++ qEtauotient(rspec, v) returns qQuotient(rspec, v, e) with
      ++ e=sigmaInfinity(rspec)/24.

    f:  (%, Z, (Z, Z) -> OutputForm, C, Q, SPEC) -> OutputForm

    etaRelationMonomial: (SUP C, SPEC, Z, (Z, Z) -> OF, Q) -> OF
      ++ etaRelationMonomial(ci, rspec, fmt, v, e) return an
      ++ eta-quoatient given by rspec and coefficient ci which is
      ++ multiplied by q^e with variables given through v and in a
      ++ form given by fmt.

    etaRelation: (List SUP C, SPECS, Z, (Z, Z) -> OF, C, Q, SPEC) -> OutputForm
      ++ etaRelation(cis, bas, fmt, v, c, e, sspec) returns the
      ++ eta-relation part multiplied by c*q^e*etaQuotient(sspec).

    pretty: (%, Z, (Z, Z) -> OF, C, Q, SPEC) -> OutputForm
      ++ pretty(x, fmt, v, c, e, sspec) outputs x as computed in
      ++ eta-quotients, but multiplied by c*q^e*etaQuotient(sspec).

    pretty: (%, Z, (Z, Z) -> OutputForm) -> OutputForm
      ++ pretty(x, fmt, v) outputs x as computed with respect to a
      ++ style given by the fmt parameter. If the lowest bit is set
      ++ in fmt then, the q factor is shown. If the bit 1 is
      ++ set, then the t parameter is replaces by the respective
      ++ eta-quotient, so that the coefficients will just be elements
      ++ of the coefficient domain.

    pretty: (%, Z) -> OutputForm
      ++ pretty(x, fmt) returns pretty(x, fmt, v) where v is a
      ++ variable indexing function chosen by bit 3 and 4 of fmt
      ++ according to the rules. If bit3=0, then variables are used.
      ++ If bit0=0, then the variable is "E" otherwise it is "u". If
      ++ bit4=0, then no subscripts are uses, bit4=1 means subscripts.
      ++ If bit3=1, then expressions are used. If bit0=0, then
      ++ v=varEta. If bit0=1, then v=varPochhammer.

-------------------------------------------------------------------

    -- Functions with X at their end give expressions or polynomial
    -- representations of the data.

    sumX: (BasicOperator, Symbol, P, N) -> EX
    orbitProductX: % -> EX
    monomialX: (SPEC, Symbol) -> Pol C
    monomialQuotientX: (SPEC, Symbol) -> Fraction Pol C
    etaQuotientX: (SPEC, Symbol) -> Fraction Pol C
    cofactorX: % -> Fraction Pol C
    qcofactorX:% -> Fraction Pol C

  Implementation ==> add
    Rep ==> Record(fsspec: SPEC,_
                   frspec: SPEC,_
                   fmultiplier: P,_
                   forbit: List N,_
                   fbasis: SPECS,_
                   fpol: Pol C,_
                   fcf: C,_
                   fcis: List SUP C)

    xx ==> rep x

    -- local for the implementation of "identity"
    pol2sup(pol: Pol C, sym: Symbol): SUP C ==
        z: SUP C := 0
        for m in reverse monomials pol repeat
            mon: SUP C := monomial(- leadingCoefficient m, degree(m, sym))
            z := mon + z
        z

    -- constructor
    identity(sspec: SPEC, rspec: SPEC, m: P, orb: List N, _
             rspecs: SPECS, pol: Pol C): % ==

        fsym: Symbol := "F"::Symbol
        msyms: List Symbol := indexedSymbols("M", #rspecs)$QAuxiliaryTools

        cf: C := ground coefficient(pol, fsym, 1)
        modrel: Pol C := coefficient(pol, fsym, 0)

        cis: List Pol C := [coefficient(modrel, msym, 1) for msym in rest msyms]
        c1: Pol C := coefficient(modrel, rest msyms, [0 for i in rest msyms])
        cis := cons(c1, cis) -- These are univariate polynomials in M1.

        tsym: Symbol := first msyms
        rel: List SUP C := [pol2sup(c, tsym) for c in cis]

        per [sspec, rspec, m, orb, rspecs, pol, cf, rel]

    -- accessor functions
    coSpecification(x: %): SPEC == xx.fsspec
    definingSpecification(x: %): SPEC == xx.frspec
    multiplier(x: %): P == xx.fmultiplier
    orbit(x: %): List N == xx.forbit
    basisSpecifications(x: %): SPECS == xx.fbasis
    identityPolynomial(x: %): Pol C == xx.fpol
    coefficient(x: %): C == xx.fcf
    moduleCoefficients(x: %): List SUP C == xx.fcis

    -- auxiliary stuff
    alphaInfinity(x: %): Z ==
        m: P := multiplier x
        orb: List N := orbit x
        rspec: SPEC := definingSpecification x
        alphaInfinity(coSpecification x, rspec, m, orb)$QEtaCofactorSpace

    -- local
    -- exponent without the cofactor
    alphaEtaInfinity(x: %): Q ==
        sbarhat: Q := sigmaInfinity coSpecification x
        alphaInfinity(x)::Q - (1/24)*sbarhat

    -- output stuff
    oeq(x, y) ==> ((concat(x,"(x)")$String)::Symbol::OF) = (y::OF)
    coerce(x: %): OutputForm ==
        cospec: OF := oeq("coSpecification", coSpecification x)
        defspec: OF := oeq("definingSpecification", definingSpecification x)
        m: OF := oeq("multiplier", multiplier x)
        orb: OF := oeq("orbit", orbit x)
        bas: OF :=  oeq("basis", basisSpecifications x)
        idpol: OF := oeq("identityPolynomial", identityPolynomial x)
        cf: OF := oeq("coefficient", coefficient x)
        alpha: OF := oeq("alpha", alphaInfinity x)
        vconcat [cospec, defspec, m, orb, bas, idpol, cf, alpha]

    -- local
    -- sum_{n=0}^\infty a(mn+t) q^n
    sum(a: OF, n: OF, m: P, t: N): OF ==
        out: OF := m::OF * n
        if not zero? t then out := out + t::OF
        q: OF := "q"::Symbol::OF
        out := prefix(a, [out]) -- builds a(m*n+t)
        sum(out*q^n, n=0::OF, "%Infinity"::Symbol::OF)

    -- \prod_{k\in orb} {sum_{n=0}^\infty a(mn+k) q^n
    orbitProduct(x: %): OF ==
        n: OF := "n"::Symbol::OF
        a: OF := "a"::Symbol::OF
        orb: List N := sort orbit x
        t := first orb
        out: OF := sum(a, n, multiplier x, t)
        for k in rest orb repeat out := out * sum(a, n, multiplier x, k)
        out

-------------------------------------------------------------------

    -- generalized case
    var(sy: Symbol): (Z, Z) -> OF == (d: Z, g: Z): OF +->
        s: String := concat(string sy, convert(d)@String)
        g = -1 => s::Symbol::OF
        concat([s, "__", convert(g)@String])::Symbol::OF

    varsub(sy: Symbol): (Z, Z) -> OF == (d: Z, g: Z): OF +->
        g = -1 => sub(sy::OF, d::OF)$OF
        sub(sy::OF, commaSeparate[d::OF,g::OF])$OF

    varEta(d: Z, g: Z): OF ==
        tau := "τ"::Symbol::OF
        eta := "η"::Symbol::OF
        g = -1 =>
            one? d => prefix(eta, [tau]$List(OF))$OF
            prefix(eta, [(d::OF)*tau])$OF
        prefix(sub(eta, commaSeparate[d::OF,g::OF])$OF, [tau])$OF

    varPochhammer(d: Z, g: Z): OF ==
        q := "q"::Symbol::OF
        g = -1 =>
            one? d => sub(paren([q, q]$List(OF)), "%Infinity"::Symbol::OF)
            l := semicolonSeparate[q^(d::OF), q^(d::OF)]$List(OF)
            sub(paren l, "%Infinity"::Symbol::OF)
        o := commaSeparate([q^(g::OF), q^((d-g)::OF)]$List(OF))
        o := semicolonSeparate[o, q^(d::OF)]
        sub(paren o, "%Infinity"::Symbol::OF)

    varPower(d: Z, g: Z, rd: Z, v: (Z, Z) -> OF): OF ==
        --assert(not zero? rd)
        one? rd => v(d, g)
        v(d, g)^(rd::OF)

    monomial(rspec: SPEC, v: (Z, Z) -> OF): OF ==
        r: LLZ := purePart rspec
        rtilde: LLZ := properGeneralizedPart rspec
        empty? rspec => (1$Z)::OF
        l: List OF := [varPower(x.1, x.2, x.3, v) for x in rtilde]
        for x in reverse r repeat l := cons(varPower(x.1, -1, x.2, v), l)
        reduce(_*, l)$List(OF)

    monomialQuotient(rspec: SPEC, v: (Z, Z) -> OF): OF ==
        rbar := concat(purePart rspec, properGeneralizedPart rspec)
        rbarpos := [x for x in rbar | last x > 0]
        rbarneg := [reverse! cons(-t, rest z)
                    for x in rbar | (z := reverse x; t := first z; t < 0)]
        mm: P := level rspec
        num: OF := monomial(generalizedEtaQuotientSpecification(mm, rbarpos), v)
        empty? rbarneg => num
        num / monomial(generalizedEtaQuotientSpecification(mm, rbarneg), v)

    etaQuotient(rspec: SPEC, v: (Z, Z) -> OF): OF ==
        monomialQuotient(rspec, v)

    qQuotient(rspec: SPEC, v: (Z, Z) -> OF, e: Q): OF ==
        eq := etaQuotient(rspec, v)
        zero? e => eq
        q: OF := "q"::Symbol::OF
        one? e => q * eq
        q^(e::OF) * eq

    qEtaQuotient(rspec: SPEC, v: (Z, Z) -> OF): OF ==
        qQuotient(rspec, v, (1/24)*sigmaInfinity(rspec))



-------------------------------------------------------------------
    -- local function
    withQFactor?(fmt: Z): Boolean == odd? fmt
    expand?(fmt: Z): Boolean == bit?(fmt, 1)
    modularFunctionIdentity?(fmt: Z): Boolean == not bit?(fmt, 2)
    asExpression?(fmt: Z): Boolean == bit?(fmt, 3)
    varsubscript?(fmt: Z): Boolean == bit?(fmt, 4)

    -- local
    plus(a: SPEC, b: SPEC): SPEC ==
        nn: P := asP lcm(level a, level b)
        h: XHashTable(Z, Z) := empty()
        for l in purePart a repeat h(nn*l.1) := l.2
        for l in purePart b repeat h(nn*l.1) := l.2 + elt(h, nn*l.1, 0)
        for l in properGeneralizedPart a repeat h(nn*l.1 + l.2) := l.3
        for l in properGeneralizedPart b repeat
            n: Z := nn*l.1 + l.2
            h.n := l.3 + elt(h, n, 0)
        rbar: LLZ := empty()
        for k in keys h repeat
            e: Z := h.k
            g: Z := positiveRemainder(k, nn)
            d: Z := numer((k-g)/nn) -- assert(denom=1)
            zero? g => rbar := cons([d, h.k], rbar) -- pure part
            cons([d, g, h.k], rbar) -- rtilde part
        generalizedEtaQuotientSpecification(nn, rbar)

    -- local
    times(z: Z, rspec: SPEC): SPEC ==
        rpure := [[x.1, z*x.2] for x in purePart rspec]
        rtilde := [[x.1, x.2, z*x.3] for x in properGeneralizedPart rspec]
        generalizedEtaQuotientSpecification(level rspec, concat(rpure,rtilde))

    -- local
    -- replace first element 1 (basis of the module)
    moduleBasis(x: %): SPECS ==
        bas: SPECS := basisSpecifications x
        mm: P := level first bas
        cons(generalizedEtaQuotientSpecification(mm, empty()), rest bas)

-------------------------------------------------------------------

    f(x: %, fmt: Z, v: (Z, Z) -> OF, c: C, e: Q, rspec: SPEC): OF ==
        e := e + alphaEtaInfinity x
        rspec := plus(rspec, coSpecification x)
        if withQFactor? fmt then e: Q := e + (1/24)*sigmaInfinity(rspec)
        cofact: OF := qQuotient(rspec, v, e)
        out: OF := orbitProduct x
        if not(zero? e and empty? rspec) then out := cofact * out
        cf: C := c * coefficient x
        one? cf => out
        one?(-cf) => - out
        cf::OF * out

-------------------------------------------------------------------

    etaRelationMonomial(c: SUP C, rspec: SPEC,
                        fmt: Z, v: (Z, Z) -> OF, e: Q): OF ==
        pol: OF := outputForm(c, "t"::Symbol::OF)
        if withQFactor? fmt then e := e + (1/24)*sigmaInfinity(rspec)
        eq: OF := qQuotient(rspec, v, e)
        one? c => eq
        empty? rspec => pol
        pol * eq

    etaRelation(cis: List SUP C, bspecs: SPECS,
                fmt: Z, v: (Z, Z) -> OF, c: C, e: Q, sspec: SPEC): OF ==
        l := [etaRelationMonomial(c*ci, plus(bspec, sspec), fmt, v, e)
              for ci in cis for bspec in bspecs | not zero? ci]
        empty? l => (0$Z)::OF
        reduce(_+, l)

-------------------------------------------------------------------

    -- fmt is divided in several bits
    -- bit 0: 0 = eta format,  1 = q format
    -- bit 1: 0 = format with t, 1 = expand t into eta-quotient
    pretty(x: %, fmt: Z, v: (Z, Z) -> OF, cf: C, e: Q, sspec: SPEC): OF ==
        cis: List SUP C := moduleCoefficients x
        bas: SPECS := moduleBasis x
        if expand? fmt then
            t: SPEC := first basisSpecifications x
            lots: List List Record(k: N, c: C) := [listOfTerms ci for ci in cis]
            --assert(#bas=#cis)
            --assert(#bas=#lots)
            cis := concat [[(rec.c)::SUP(C) for rec in lot] for lot in lots]
            bas := concat [[plus(times(rec.k, t), b) for rec in lot]
                           for lot in lots for b in bas]
        er: OF := etaRelation(cis, bas, fmt, v, cf, e, sspec)
        (f(x, fmt, v, cf, e, sspec) = er)$OF

    -- format is divided in several bits
    -- bit 0: 0 = eta format,  1 = q format
    -- bit 1: 0 = format with t, 1 = expand t into eta-quotient
    -- bit 2: 0 = modular function identity, 1 = only orbit product on LHS
    pretty(x: %, fmt: Z, v: (Z, Z) -> OF): OF ==
        modularFunctionIdentity? fmt =>
            pretty(x, fmt, v, 1, 0, etaQuotientSpecification(1,[]))
        invcf: Union(C, "failed") := 1$C exquo coefficient(x)
        icf: C := if invcf case "failed" then 1$C else invcf::C
        sspec: SPEC := times(-1, coSpecification x)
        pretty(x, fmt, v, icf, - alphaEtaInfinity x, sspec)

    -- format is divided in several bits
    -- bit 0: 0 = eta format,  1 = q format
    -- bit 1: 0 = format with t, 1 = expand t into eta-quotient
    -- bit 2: 0 = modular function identity, 1 = only orbit product on LHS
    -- bit 3: 0 = variables, 1 = expression
    -- bit 4: 0 = normal indexed variables, 1 = subscripted variables
    pretty(x: %, fmt: Z): OF ==
        v: (Z, Z) -> OF :=
            withQFactor? fmt =>
                asExpression? fmt => varPochhammer @ ((Z,Z)->OF)
                varsubscript? fmt => varsub("u"::Symbol)
                var("u"::Symbol)
            asExpression? fmt => varEta @ ((Z,Z)->OF)
            varsubscript? fmt => varsub("E"::Symbol)
            var("E"::Symbol)
        pretty(x, fmt, v)

-------------------------------------------------------------------
-- Return parts as expressions, polynomial, or rational functions.

    sumX(a: BasicOperator, n: Symbol, m: P, t: N): EX ==
        ex: EX := m * (n::EX)
        if not zero? t then ex := ex + t::EX
        ex := a(ex) -- builds a(m*n+t)
        infty: EX := "%Infinity"::Symbol::EX
        summation(ex, equation(n, segment(0, infty)))$EX

    orbitProductX(x: %): EX ==
        n: Symbol := "n"::Symbol
        a: BasicOperator := operator("a"::Symbol)
        orb: List N := sort orbit x
        ex: EX := sumX(a, n, multiplier x, first orb)
        for k in rest orb repeat ex := ex * sumX(a, n, multiplier x, k)
        ex

    -- local
    sub(v: Symbol, d: Z, g: Z): Symbol ==
        g = -1 => subscript(v, [d::OF]$List(OF))
        subscript(v, [d::OF, g::OF]$List(OF))

    -- local
    vPower(v: Symbol, d: Z, g: Z, e: N): Pol C == (sub(v, d, g)::Pol(C))^e

    monomialX(rspec: SPEC, v: Symbol): Pol C ==
        r: LLZ := purePart rspec
        rtilde: LLZ := properGeneralizedPart rspec
        empty? rspec => 1$Pol(C)
        l: List Pol C := [vPower(v, x.1, x.2, (x.3)::N) for x in rtilde]
        for x in reverse r repeat l := cons(vPower(v, x.1, -1, (x.2)::N), l)
        reduce(_*, l)

    monomialQuotientX(rspec: SPEC, v: Symbol): Fraction Pol C ==
        rbar := concat(purePart rspec, properGeneralizedPart rspec)
        rbarpos := [x for x in rbar | last x > 0]
        rbarneg := [reverse! cons(-t, rest z)
                    for x in rbar | (z := reverse x; t := first z; t < 0)]
        mm: P := level rspec
        rspecpos: SPEC := generalizedEtaQuotientSpecification(mm, rbarpos)
        rspecneg: SPEC := generalizedEtaQuotientSpecification(mm, rbarneg)
        monomialX(rspecpos, v) / monomialX(rspecneg, v)

    etaQuotientX(rspec: SPEC, v: Symbol): Fraction Pol C ==
        monomialQuotientX(rspec, v)

    cofactorX(x: %): Fraction Pol C ==
        eq: Fraction Pol C := etaQuotientX(coSpecification x, "E"::Symbol)
        e: Z := numer(24*alphaEtaInfinity(x))
        zero? e => eq
        xvar: Fraction Pol(C) := "x"::Symbol::Pol(C)::Fraction(Pol C)
        xvar^e * eq

    qcofactorX(x: %): Fraction Pol C ==
        eq: Fraction Pol C := etaQuotientX(coSpecification x, "u"::Symbol)
        alpha: Z := alphaInfinity x
        zero? alpha => eq
        q: Fraction Pol(C) := "q"::Symbol::Pol(C)::Fraction(Pol C)
        q^alpha * eq


)if LiterateDoc
%$
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Package QEtaRamanujanKolberg}
\label{sec:QEtaRamanujanKolberg}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%



)endif

)abbrev package QETARK QEtaRamanujanKolberg
++ QEtaRamanujanKolberg provides functions to compute
++ Ramanujan-Kolberg identities by an algorithm that was developed by
++ Radu in \cite{Radu:RamanujanKolberg:2015}
++ (`DOI=10.1016/j.jsc.2014.09.018
++ <https://doi.org/10.1016/j.jsc.2014.09.018>`_), see `RISC Report
++ 16-06
++ <https://www.risc.jku.at/publications/download/risc_5069/zzz3.pdf>`_.
++ See also at the top of the qetark.spad file where this package is
++ implemented.
QEtaRamanujanKolberg(C): Exports == Implementation where
  C: EuclideanDomain -- basic coefficient domain
  IDENTITY ==> QEtaRamanujanKolbergIdentity C
  Exports ==> with
    findIdentity: (SPEC, SPEC, P, N, SPECS) -> IDENTITY
      ++ findIdentity(sspec, rspec, m, k, bspecs) returns all data for
      ++ a Ramanujan-Kolberg identity.
    findIdentity: (SPEC, SPEC, P, N) -> IDENTITY
      ++ findIdentity(sspec, rspec, m, k) returns all data for a
      ++ Ramanujan-Kolberg identity.
    findIdentity: (P, SPEC, P, N, SPECS) -> IDENTITY
      ++ findIdentity(nn, rspec, m, k, bgens) returns all data for a
      ++ Ramanujan-Kolberg identity.
    findIdentity: (P, SPEC, P, N) -> IDENTITY
      ++ findIdentity(nn, rspec, m, k) returns all data for a
      ++ Ramanujan-Kolberg identity.
  Implementation ==> add
    findIdentity(sspec: SPEC, rspec: SPEC, m: P, t: N,_
                 bspecs: SPECS): IDENTITY ==
        import from QEtaModularGamma0Infinity(C)

        f: A1 C := modularEtaQuotientInfinity(sspec, rspec, m, t)

        -- For the following computation we attach a marker to the
        -- series.
        fsym: Symbol := "F"::Symbol
        xf: X1 C := toX1(C, f, fsym::Pol(C))

        specM0A1C spec ==> expansion(etaQuotient(spec)$EQI(C))::A1(C)
        egens: List A1(C) := [specM0A1C bspec for bspec in bspecs]

        -- We attach some identifiers also to those eta-quotients.
        msyms: List Symbol := indexedSymbols("M", #bspecs)$QAuxiliaryTools
        xgens := [toX1(C, x, sym::Pol(C)) for x in egens for sym in msyms]

        -- Eventualle, we apply the samba algorithm to obtain an
        -- order-complete basis.
        import from QXTOPRED1(C) -- necessary, because the next wouldn't compile
        xab: QXAB1(C) := samba(xgens.1, xgens)$QXTOPSAMBA1(C)

        -- We look of a representation of our original modular function f in
        -- the respective module of eta-quotients that is represented by
        -- the algebra-basis xab.
        xz: X1(C) := reduce(xf, xab)$QXTOPRED1(C)

        -- If xz is non-zero, then no relation has been found.
        orb: List N := orbit(rspec, m, t)$QEtaCofactorSpace
        not zero? xz => identity(sspec, rspec, m, orb, bspecs, 0$Pol(C))

        identity(sspec, rspec, m, orb, bspecs, second(xz)::Pol(C))

    findIdentity(sspec: SPEC, rspec: SPEC, m: P, t: N): IDENTITY ==
        -- We compute the basis for the modular eta-quotients of level nn.
        bspecs: SPECS := etaQuotientMonoidSpecifications(level sspec)$QES4TI2
        findIdentity(sspec, rspec, m, t, bspecs)

    findIdentity(nn: P, rspec: SPEC, m: P, t: N, bspecs: SPECS): IDENTITY ==
        sspec: SPEC := etaCofactorInfinity(nn, rspec, m, t)$QEtaModularGamma0
        findIdentity(sspec, rspec, m, t, bspecs)

    findIdentity(nn: P, rspec: SPEC, m: P, t: N): IDENTITY ==
        sspec: SPEC := etaCofactorInfinity(nn, rspec, m, t)$QEtaModularGamma0
        findIdentity(sspec, rspec, m, t)




)if LiterateDoc
\bibliography{qeta}
\end{document}
)endif
