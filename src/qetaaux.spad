-------------------------------------------------------------------
---
--- FriCAS QEta
--- Copyright (C) 2018-2021  Ralf Hemmecke <ralf@hemmecke.org>
---
-------------------------------------------------------------------
-- This program is free software: you can redistribute it and/or modify
-- it under the terms of the GNU General Public License as published by
-- the Free Software Foundation, either version 3 of the License, or
-- (at your option) any later version.
--
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
-- GNU General Public License for more details.
--
-- You should have received a copy of the GNU General Public License
-- along with this program.  If not, see <http://www.gnu.org/licenses/>.
-------------------------------------------------------------------

)if LiterateDoc
\documentclass{article}
\usepackage{qeta}
\externaldocument{qeta}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\begin{document}
\title{Auxiliary functions for modular functions}
\author{Ralf Hemmecke}
\date{26-Jan-2018}
\maketitle
\begin{abstract}
  The packages \code{QEtaAuxiliaryPackage} contains helper functions
  for transformations of eta-quotients.
\end{abstract}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\tableofcontents

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Indroduction}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Let $N$ be a positive natural number and $\Gamma_0(N)$ be as defined
in \eqref{eq:Gamma0}.

This file contains code to compute a (finite) monoid basis for
$E^\infty(N)$.

This file contains code to compute a (finite) monoid basis for
$GE^\infty(N)$ (the generalized eta-quotients that are modular for
$\Gamma_1(N)$).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Implementation}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Helper macros}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Let us start with a few common macros.

First of all for debugging.
)endif
OF==>OutputForm
display77(x) ==> display((x::OF)::Formatter(Format1D))
yof x ==> x :: Symbol :: OF
dbgPrint(x,y) ==> display77([yof ":> ", yof x, y::OF]$List(OF))
tracePrint(x,y) ==> display77(hconcat([yof"-- ",yof x,yof":=",y::OF]$List(OF)))
errorPrint(x,y) ==> tracePrint(x,y)

)if LiterateDoc
%$
These two technical macros are necessary to distinguish between Rep
and \%.
)endif

rep x ==> (x@%) pretend Rep
per x ==> (x@Rep) pretend %

)if LiterateDoc
Now some abbreviations for common domains.
)endif

P ==> PositiveInteger
N ==> NonNegativeInteger
Z ==> Integer
Q ==> Fraction Z
MZ ==> Matrix Z -- will be a square matrix
MQ ==> Matrix Q -- matrixEtaOrder
SL2Z ==> MZ -- represents SL_2(Z)
EXGCD ==> Record(coef1: Z, coef2: Z, generator: Z)
Rec ==> Record(red: SL2Z, triang: MZ)
Pol ==> SparseUnivariatePolynomial Z
LZ ==> List Z
LLZ ==> List LZ
INDICES ==> LLZ

INTF ==> IntegerNumberTheoryFunctions
DIVISORS m ==>  [asP d for d in divisors(m)$INTF]

asN x ==> x pretend N
asP x ==> x pretend P

)if LiterateDoc
%$
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Package QEtaAuxiliaryPackage: Check modularity of
  eta-quotients}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Here we mainly deal with the sets $R^*(N)$ and $R^\infty(N)$ from
\cite{Hemmecke+Radu:EtaRelations:2018}.

The function \code{matrixLigozat} computes the respective matrix $A_N$
whereas \code{modularGamma0} checks which of the conditions for an $r$
vector to correspond to a modular eta-quotient is violated.


)endif

-- Maybe this should become a domain CongruenceSubgroupGamma0(m).
)abbrev package QETAAUX QEtaAuxiliaryPackage
++ QEtaAuxiliaryPackage helps to do computations with
++ eta-quotients that are modular functions.
QEtaAuxiliaryPackage: with
    generalizedEtaFunctionIndices: P -> INDICES
      ++ generalizedEtaFunctionIndices(nn) returns all the indices of
      ++ a generalized eta-quotient of level nn (without exponents),
      ++ i.e., it return the list in \eqref{eq:sorted-indices} where
      ++ the 0 in the second argument is removed. To be precise, it
      ++ returns
      ++ [[d1],[d2],...,[dn],[d2,1],...,[d2,f2],...,[dn,1],...,[dn,fn]]
      ++ where di is the i-th positive divisor of nn and
      ++ fi=ceiling(di/2)-1.
    lengthOfGeneralizedEtaFunctionIndices: P -> N
      ++ lengthOfGeneralizedEtaFunctionIndices(nn) returns
      ++ #generalizedEtaQuotientSpecification(nn) without ever
      ++ computing the specification list explicitly.
    sigmaInfinity: (P, List Z) -> Z
      ++ sigmaInfinity(mm, r) returns \sum_{delta|mm} delta*r_delta where
      ++ it is assumed that the entries of r correspond to the divisors
      ++ of mm.
      ++ It corresponds to \eqref{eq:sumdelta} in qeta.tex.
    sigmaInfinity: (P, List List Z) -> Q
      ++ sigmaInfinity(mm, rtilde) returns
      ++ \sum_{delta|mm,0<g<delta/2}
      ++      delta*fractionalBernoulli2(g/delta)*r_{delta,g}
      ++ or rather
      ++ \sum_{l \in rtilde}(d*fractionalBernoulli2(g/d)*e | (d,g,e)=l).
      ++ We assume here that everly element of rtilde is of the form
      ++ [d,g,e] and describes the generalized eta-power \eta_{d,g}^e
    sigmaInfinity: (P, List Z, List List Z) -> Q
      ++ sigmaInfinity(mm, r, rtilde) returns
      ++ sigmaInfinity(mm, r) + 12*sigmaInfinity(mm, rtilde).
      ++ Compare with \eqref{eq:generalized-sigmaInfinity}.
    sigma0: (P, List Z) -> Z
      ++ sigma0(mm, r) returns \sum_{delta|mm} (mm/delta)*r_delta where
      ++ it is assumed that the entries of r correspond to the divisors
      ++ of mm.
      ++ It corresponds to \eqref{eq:sumdelta} in qeta.tex.
    sigma0: (P, List List Z) -> Z
      ++ sigma0(mm, rtilde) returns
      ++ \sum_{delta|mm, 0<g<delta/2} (mm/delta)*r_{delta,g}
      ++ or rather \sum_{l \in rtilde}((mm/d)*e | (d,g,e)=l).
      ++ We assume here that everly element of rtilde is of the form
      ++ [d,g,e] and describes the generalized eta-power \eta_{d,g}^e
    sigma0: (P, List Z, List List Z) -> Z
      ++ sigma0(mm, r, rtilde) returns sigma0(mm, r) + 2*sigma0(mm, rtilde).
      ++ Compare with \eqref{eq:generalized-sigma0}.
    fractionalPart: Q -> Q
      ++ fractionalPart(x) returns x - floor(x)::Fraction(Integer).
    fractionalBernoulli1: Q -> Q
      ++ fractionalBernoulli1(x) computes t - 1/2 where t is the
      ++ fractional part of x, i.e., t = x - floor(x)
    fractionalBernoulli2: Q -> Q
      ++ fractionalBernoulli2(x) computes t^2 - t + 1/6 where t is the
      ++ fractional part of x, i.e., t = x - floor(x)
    sawTooth: Q -> Q
      ++ sawTooth(x) is 0 if x is an integer and
      ++ fractionalBernoulli1(x) otherwise.
    dedekindSum: (Z, Z) -> Q
       ++ dedekindSum(p,q) returns for two relatively prime integers
       ++ p and q the Dedekind sum as defined by
       ++ dedekindSum(p,q)=sum(sawTooth(1/q)*sawTooth(p*i/q), i=1..q-1).
    generalizedDedekindSum: (P, Z, Z, Z, Z) -> Q
       ++ generalizedDedekindSum(nn,g,h,p,q) returns the generalized
       ++ Dedekind sum as defined in
       ++ \cite[p~673]{Yang:GeneralizedDedekindEtaFunctions} by
       ++ generalizedDedekindSum(nn,g,h,p,q) =
       ++ sum(sawTooth((g+i*nn)/(q*nn))*sawTooth((g'+p*i*nn)/(q*nn)),i=1..q-1).
       ++ Condition: one? gcd(p,q).
    aLigozat: (P, Z, Z) -> Z
      ++ aLigozat(m, c, delta) computes an entry of the matrix of
      ++ Ligozat corresponding to the index (c, delta) counted in
      ++ divisors.
    matrixLigozat: P -> MZ
      ++ matrixLigozat(m) returns the (n,n) matrix with entries a_m(di,dj)
      ++ given by Notation 3.2.6 in \cite{Ligozat:CourbesModulaires:1975}
      ++ where di and dj run over all positive divisors of m.
      ++ This matrix is A_N as described after Lemma 5.2 in
      ++ \cite{Hemmecke+Radu:EtaRelations:2018}.
    matrixAtkinLehner: (P, P) -> MZ
      ++ matrixAtkinLehner(m, t) with t a Hall divisor
      ++ (https://en.wikipedia.org/wiki/Hall_subgroup) of m returns
      ++ the 2x2 matrix with entries [[t, -a], [m, t*b]] such that
      ++ n=m/t and t*b + n*a = 1, see
      ++ \cite{Kohnen:WeierstrassPointsAtInfinity:2004}.
    modularGamma0: (P, List Z) -> Z
      ++ modularGamma0(nn, r) returns 0 if all conditions are
      ++ fulfilled. Otherwise it returns a positive number in the
      ++ range 1 to 4 that corresponds to the condition that is not
      ++ met. This corresponds to the conditions given for
      ++ R(N,i,j,k,l) on page 226 of \cite{Radu:RamanujanKolberg:2015}
      ++ and to the conditions \eqref{eq:sum=0},
      ++ \eqref{eq:sigmainfinity}, \eqref{eq:sigma0}, and
      ++ \eqref{eq:productsquare} in qeta.tex.
      ++ It is equivalent to check whether there is an extension v of r
      ++ such that matrixModularGamma0(nn)*v is 0.
    modularGamma0?: (P, List Z) -> Boolean
      ++ modularGamma0(nn, r) returns true iff the eta-quotient
      ++ corresponding to r is a modular function for Gamma_0(nn). It
      ++ is equivalent to zero?(modularGamma0(nn, r)).
    matrixModularGamma0: P -> MZ
      ++ matrixModularGamma0(nn) returns a matrix mat such
      ++ that if divs:=divisors(nn), n=#divisors(nn) and v is such
      ++ that mat*v=0 then v(1..n) is in R^*(nn), in other words, the
      ++ nullspace of mat "generates" R^*(nn). The first row of mat
      ++ corresponds sum(r.i, i=1..n). The second row of mat
      ++ corresponds to the sum(divs.i*r.i,i=1..n)+24*r(n+1). The
      ++ third row of mat corresponds to the
      ++ sum((nn/divs.i)*r.i,i=1..n)+24*r(n+2). The function computes
      ++ matrix B_N (equation (8) from
      ++ \cite{Hemmecke+Radu:EtaRelations:2018}
      ++ \url{http://www.risc.jku.at/publications/download/risc_5561/etarelations.pdf}
      ++ (DOI:10.1016/j.jsc.2018.10.001).
    matrixLigozatModularGamma0: (P, List Z) -> MZ
      ++ matrixLigozatModularGamma0(nn, idivs) returns a matrix mat which
      ++ is a vertical concatenation (a part of) matrixLigozat(nn)
      ++ whose rowindices correspond to idivs and
      ++ matrixModularGamma0(nn).
      ++ It computes matrix B_N equation (8) in
      ++ \cite{Hemmecke+Radu:EtaRelations:2018}
      ++ (DOI:10.1016/j.jsc.2018.10.001) put below the respective rows
      ++ of matrix A_N from the same article.
    upsilonExponent: (Z, Z, Z, Z) -> Z
      ++ upsilonExponent(a,b,c,d) returns z \in {0..23} such that
      ++ exp(\pi*i*z/12) is a multiplier in the transformation of an
      ++ eta-function by the matrix [[a,b],[c,d]] from SL_2(Z).
      ++ A definition can be found in \cite[Lemma~2.27]{Radu:PhD:2010}.
    upsilonExponent: MZ -> Z
      ++ upsilonExponent(m) for a matrix m from SL_2(Z) returns
      ++ upsilonExponent(m(1,1), m(1,2), m(2,1), m(2,2)).
    kappaSchoeneberg: (P, Z, Z, Z, Z, Z, Z) -> Q
      ++ kappaSchoeneberg(nn, g, h, a, b, c, d) returns a value x such
      ++ that \eta_{g,h}(mat*tau) = exp(2*%pi*%i*x) *
      ++ \eta_{g',h'}(tau) as given at the top of page 673 of
      ++ \cite{Yang:GeneralizedDedekindEtaFunctions:2004} where
      ++ mat=matrix[[a,b],[c,d]].
      ++ See also \eqref{eq:kappa_g-h-N-gamma-Schoeneberg}.
    kappaSchoeneberg: (P, Z, Z, MZ) -> Q
      ++ kappaSchoeneberg(nn, g, h, mat) returns a value x such
      ++ that \eta_{g,h}(mat*tau) = exp(2*%pi*%i*x) *
      ++ \eta_{g',h'}(tau) as given at the top of page 673 of
      ++ \cite{Yang:GeneralizedDedekindEtaFunctions:2004}.
      ++ See also \eqref{eq:kappa_g-h-N-gamma-Schoeneberg}.
    kappaYang: (P, Z, Z, Z, Z, Z, Z) -> Q
      ++ kappaYang(nn, g, h, a, b, c, d) returns a value x such that
      ++ E_{g,h}(mat*tau) = exp(2*%pi*%i*x) * E_{g',h'}(tau) as given
      ++ in \cite[Theorem~1]{Yang:GeneralizedDedekindEtaFunctions:2004}
      ++ where mat = matrix [[a,b],[c,d]].
      ++ Condition: not zero? c.
    kappaYang: (P, Z, Z, MZ) -> Q
      ++ kappaYang(nn, g, h, mat) returns a value x such that
      ++ E_{g,h}(mat*tau) = exp(2*%pi*%i*x) * E_{g',h'}(tau) as given
      ++ in \cite[Theorem~1]{Yang:GeneralizedDedekindEtaFunctions:2004}.
      ++ Condition: not zero? mat(2,1).
    numberOfGaps: (P, List P) -> N
      ++ numberOfGaps(n, l) computes
      ++ reduce(+, [floor(x/n) for x in l], 0).
    splitMatrix: (SL2Z, Z, P, N) -> Rec
      ++ splitMatrix(mat, delta, m, lambda) computes
      ++ splitMatrix(a, b, c, d) where the arguments are the matrix
      ++ entries of matrix [[delta, delta*lambda], [0, m]]*mat.
      ++ Input condition: determinant(mat)>0.
    splitMatrix: (SL2Z, Z) -> Rec
      ++ splitMatrix(mat, delta) computes splitMatrix(mat, delta, 1, 0).
      ++ Input condition: determinant(mat)>0.
    splitMatrix: (Z, Z, Z, Z) -> Rec
      ++ splitMatrix(a, b, c, d) returns a record rec that contains
      ++ two matrices rec.red=m1 and rec.triang=m2 such that mat=m1*m2
      ++ according to \cide[Lemma~4.8]{Radu:PhD:2010}, but in a variation
      ++ as described in qeta.tex, i.e., such that 0<=m2(1,2)<det(mat)/g
      ++ where g = gcd(a, c) and mat=[[a,b],[c,d]].
      ++ Input condition: a*d-b*c>0.
    rationalSquareRoot: Q -> Record(rat: Q, rootOf: P)
      ++ if z = rationalSquareRoot(x) then x = (z.rat)^2 * z.root
    minimalSquareRootOfUnity: P -> P
      ++ minimalSquareRootOfUnity(p) returns an integer n such that
      ++ in Z(x) (where x is a primitive n-th root of unity) there
      ++ exists an element y such that y^2=p.
      ++ p must be a radical integer, i.e., should not contain the
      ++ square of a prime.
    primePower: (P, P) -> N
      ++ primePower(d, p) returns e such that gcd(d/p^e, p)=1, i.e., the
      ++ highest power e such that p^e is a factor of d.

    matrixModularGamma1: (P, INDICES) -> MQ
      ++ matrixModularGamma1(nn, idxs) returns a matrix mat with 3
      ++ rows and n+2 colums (where n=#idxs such that mat*v=0 for an
      ++ (extended) exponent vector where the exponents correspond to
      ++ generalizedEtaFunctionIndices(nn). mat*v=0 encodes the
      ++ conditions \eqref{eq:generalized-weight},
      ++ \eqref{eq:generalized-sigmaInfinity}, and
      ++ \eqref{eq:generalized-sigma0}, but corresponding to the given
      ++ indices idxs.. See also modularGamm1 and modularGamma1?. The
      ++ first row encodes that the some of the exponents of the pure
      ++ part must be zero. The second row encodes that order at
      ++ infinity must be an integer. The third row encodes that the
      ++ order at 0 must be an integer. For the last two rows, the
      ++ "is-an-integer" property is encoded by a 1 in the (n+1)-th
      ++ and (n+2)-th column, respectively. All other entries are 0.

    matrixModularGamma1: P -> MQ
      ++ matrixModularGamma1(nn) returns a matrixModularGamma1(nn, idxs) for
      ++ idxs:=generalizedEtaFunctionIndices(nn).

    matrixEtaOrderElement: (P, Cusp, LZ) -> Q
      ++ z:=matrixEtaOrderElement(nn,cusp,idx). The parameter
      ++ corresponds to the index of a (generalized) eta-quotient,
      ++ i.e., it can be of the form idx=[delta,g] or of the form
      ++ idx=[delta] with g in the range 0..delta. Then z is the
      ++ coefficient of r_{delta,g} in
      ++ \cite[Thm.~4]{Robins:GeneralizedDedekindEtaProducts:1994},
      ++ except for the case nn=4, gcd(denom(cusp),nn)=2 (or
      ++ Gamma1(4)-equivalent) where we return half of this
      ++ coefficient. If idx=[delta] then
      ++ z=1/2*matrixEtaOrderElement(nn,cusp,[delta,0])/2. and thus
      ++ corresponds to the coefficient of $r_\delta$ in
      ++ \eqref{eq:order-rbar-non-adjusted} in
      ++ Theorem~\ref{thm:matrixEtaOrderRobins}. Note that Robins
      ++ refers to an expansion in the uniformizing variable
      ++ q^(gcd(c,nn)/nn) (where cusp=a/c). That is equal to
      ++ q^(1/width(nn,c)$CongruenceSubgroupGamma1) except for the
      ++ case nn=4 and gcd(nn,c)=2. In that case the width of the cusp
      ++ 1/2 is 1 and not 2=4/gcd(4.2). For this case, we deviate from
      ++ Robins' coefficients and adapt to an expansion in q rather
      ++ than an expansion in q^(1/2).

    matrixEtaOrderRow: (P, Cusp, INDICES) -> List Q
      ++ matrixEtaOrderRow(nn, cusp, idxs) returns
      ++ [matrixEtaOrderElement(nn,cusp,idx) for idx in idxs].

    matrixEtaOrderFull: (P, List Cusp, INDICES) -> MQ
      ++ matrixEtaOrder(nn,spitzen,idxs) returns a matrix that is
      ++ similar to matrixLigozat(nn). It allows the compution of the
      ++ order of generalized eta-quotients at the cusps of Gamma1(nn)
      ++ given by the parameter spitzen. The rows of the matrix are
      ++ sorted according to spitzen. The matrix columns correspond to
      ++ the given indices. It gives the coefficients of $r_\delta$
      ++ and $r_{\delta,g}$ in the sense of
      ++ Theorem~\ref{thm:matrixEtaOrderRobins}, in particular to the
      ++ coefficients in \eqref{eq:order-rbar-non-adjusted}.
      ++ It returns
      ++ matrix [matrixEtaOrderRow(nn,cusp,idxs) for cusp in spitzen].

    matrixEtaOrder: (P, List Cusp, INDICES) -> MQ
      ++ matrixEtaOrder(nn,spitzen,idxs) returns the matrix given by
      ++ [matrixEtaOrderRow(nn,cusp,idxs) for cusp in spitzen] with
      ++ duplicate rows removed.

    matrixEtaOrder: (P, List Cusp) -> MQ
      ++ matrixEtaOrder(nn, spitzen) returns
      ++ matrixEtaOrder(nn,spitzen,idxs) where
      ++ idxs:=generalizedEtaFunctionIndices(nn).

    matrixEtaOrder: P -> MQ
      ++ matrixEtaOrder(nn) returns matrixEtaOrder(nn, spitzen) where
      ++ spitzen := cusps(nn)$CongruenceSubgroupGamma1.

    matrixEtaOrderModularGamma1: (P, List Cusp, INDICES) -> MQ
      ++ matrixEtaOrderModularGamma1(nn,spitzen,idxs) returns the
      ++ vertical concatenation of matrixEtaOrder(nn,spitzen,idxs) and
      ++ matrixModularGamma1(nn,idxs) so that there will be #spitzen+3
      ++ rows and ncols(matrixModularGamma1 nn) columns.

    matrixEtaOrderModularGamma1: (P, List Cusp) -> MQ
      ++ matrixEtaOrderModularGamma1(nn, spitzen) returns
      ++ matrixEtaOrderModularGamma1(nn, spitzen, idxs) where
      ++ idxs:=generalizedEtaFunctionIndices(nn).


-------------------------------------------------------------------
-- Temporarily exported
-------------------------------------------------------------------
    jacobiUpperStar: (Z, Z) -> Z
      ++ See, for example, Definition 2.26 in the PhD thesis of Silviu Radu.
      ++ http://www3.risc.jku.at/publications/download/risc_5453/main.pdf
    jacobiLowerStar: (Z, Z) -> Z
      ++ See, for example, Definition 2.26 in the PhD thesis of Silviu Radu.
      ++ http://www3.risc.jku.at/publications/download/risc_5453/main.pdf
    coprimePart: (Z, Z) -> Z
       ++ coPrimePart(n, c) splits n into u*v such that gcd(u,c)=1
       ++ and returns u.
       ++ We assume that all input numbers are positive.

  == add
    fUnion ==> Union("nil", "sqfr", "irred", "prime")
    FL ==> List Record(flag: fUnion, factor: Z, exponent: N)
    DIVISIBLEBY?(k, z) ==> zero?(z rem k)

)if LiterateDoc
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsubsection{Local functions}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif

    sigmaAux(divs: List P, r: List Z): Z ==
        z: Z := 0
        for d in divs for rd in r repeat z := z + d*rd
        return z

    sigmaInfinity(mm: P, r: List Z): Z == sigmaAux(DIVISORS mm, r)
    sigma0(mm: P, r: List Z): Z == sigmaAux(reverse! DIVISORS mm, r)

)if LiterateDoc
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsubsection{Related to Eta-Quotients}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif

    -- Compute B_N and V_N as in \cite{Hemmecke+Radu:EtaRelations:2018}.
    -- aLigozat(m, di, dj) here corresponds to a_m(di, dj) in
    -- \cite[Notation 3.2.6]{Ligozat:CourbesModulaires:1975}.
    -- Note that we can use any integer c here, including c=0.
    aLigozat(m: P, c: Z, delta: Z): Z == -- di==c, dj==delta
        num: Z := m * gcd(c, delta)^2
        den: Z := gcd(c^2, m) * delta
        (num exquo den)::Z -- can prove that this cannot fail

    matrixLigozat(m: P): MZ ==
        divs: List Z := divisors(m)$INTF
        n: N := # divs
        mat: MZ := new(n, n, 0)
        for i in 1..n for di in divs repeat
            for j in 1..n for dj in divs repeat
                qsetelt!(mat, i, j, aLigozat(m, di, dj))
        mat

    matrixAtkinLehner(m: P, t: P): MZ ==
        -- t is a Hall divisor of m.
        -- https://en.wikipedia.org/wiki/Hall_subgroup
        --assert((m exquo t) case Z)
        n: Z := (m exquo t)::Z -- input condition is that t divides m
        --assert(one? gcd(n, t))
        exgcd: EXGCD := extendedEuclidean(n, t)
        matrix [[t, -exgcd.coef1], [m, t*exgcd.coef2]]

    -- These are the conditions (1) to (4) of
    -- \cite{Hemmecke+Radu:EtaRelations:2018} that have to be fulfilled
    -- by a vector r to lie in R^*(N).
    modularGamma0(m: P, r: List Z): Z ==
        divs: List Z := divisors(m)$INTF
        rdivs: List Z := reverse divs
        s: Z := 0
        a: Z := 0
        b: Z := 0
        c: Factored Z := 1
        for ri in r for d in divs for rd in rdivs repeat
            s := s + ri
            a := a + ri * d  -- sigma_\infty
            b := b + ri * rd -- sigma_0
            c := c * factor(d)^asN(abs ri)
        -- Check conditions 1, 2, 3, 4.
        not zero? s => 1
        not zero? positiveRemainder(a, 24) => 2
        not zero? positiveRemainder(b, 24) => 3
        for fe in factors c repeat if odd?(fe.exponent) then return 4
        return 0

    modularGamma0?(nn: P, r: List Z): Boolean ==
        zero? modularGamma0(nn, r)

    matrixModularGamma0(m: P): MZ ==
        divs: List Z := divisors(m)$INTF
        n: N := # divs
        d: Vector Z := vector divs
        fl: List Record(factor: Z, exponent: N) := factors factor m
        cols: P := asP(n + 2 + #fl)
        rows: P := asP(3 + #fl)
        mat: MZ := new(rows, cols, 0)
        -- sum(r(d.i), i=1..n)=0
        for j in 1..n repeat qsetelt!(mat, 1, j, 1)
        -- sum(d.i * r(d.i), i=1..n+1) = 0 (mod 24)
        for j in 1..n repeat qsetelt!(mat, 2, j, d.j)
        qsetelt!(mat, 2, n+1, 24) -- additional variable for (mod 24)
        -- sum(d.(n+2-i) * r(d.i), i=1..n+1) = 0 (mod 24)
        for j in 1..n repeat qsetelt!(mat, 3, j, d(n+1-j))
        qsetelt!(mat, 3, n+2, 24) -- additional variable for (mod 24)
        -- product is rational square
        for i in 1..#fl for fe in fl repeat
            k := n+2+i
            p: P := asP(fe.factor)
            for j in 2..n repeat -- for all divisors > 1
                qsetelt!(mat, i+3, j, primePower(asP(d.j), p))
            qsetelt!(mat, i+3, k, 2) -- additional variable for (mod 2)
        return mat

    matrixLigozatModularGamma0(m: P, idivs: List Z): MZ ==
        matmod0: MZ := matrixModularGamma0 m
        empty? idivs => return matmod0
        matLigozat: MZ := matrixLigozat m -- is a square matrix
        l: N := ncols matLigozat
        n: N := # idivs
        rows: N := nrows matmod0
        cols: N := ncols matmod0
        mat: MZ := new(rows + n, cols, 0)
        for i in idivs for k in 1..n repeat for j in 1..l repeat
            qsetelt!(mat, k, j, qelt(matLigozat, i, j))
        for i in 1..rows repeat for j in 1..cols repeat
            qsetelt!(mat, n+i, j, qelt(matmod0, i, j))
        return mat

)if LiterateDoc
%$
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsubsection{Related to Transformations of Eta-Quotients}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif

    jacobiLowerStar(c: Z, d: Z): Z == -- result either 1 or -1
        --assert(one? gcd(c,d))
        --aaawer(odd? d)
        -- See \cite[Def.~2.26]{Radu:PhD:2010}.
        -- \left(\frac{c}{d}\right)_* =
        -- \begin{cases}
        -- \left(\frac{c}{\lvert d \rvert}\right) (-1)^{m_c m_d},
        --   & \text{if $c\ne0$},\\
        -- \sign{d}, & \text{otherwise}
        -- \end{cases}
        -- where $m x=\frac{\sign{x}-1}{2}$.
        zero? c => sign(d) -- note that d ~= 0 in this case
        j: Z := jacobi(c, abs(d))$INTF
        if c>0 or d>0 then j else -j -- c=0 or d=0 does not happen

    jacobiUpperStar(c: Z, d: Z): Z == -- result either 1 or -1
        --assert(one? gcd(c,d))
        --assert(odd? d)
        -- See \cite[Def.~2.26]{Radu:PhD:2010}.
        -- \left(\frac{c}{d}\right)^* =
        -- \begin{cases}
        -- \left(\frac{c}{\lvert d \rvert}\right), & \text{if $c\ne0$},\\
        -- 1, & \text{otherwise}
        -- \end{cases}
        zero? c => 1
        jacobi(c, abs(d))$INTF

    upsilonExponent(a: Z, b: Z, c: Z, d: Z): Z ==
        u: Z := c*(a+d)-b*d*(c^2-1)-3*c
        v: Z := 3*(d-1)*(c-1)
        r(x) ==> positiveRemainder(x, 24)
        -- Note that we exp(2*\pi*i*n/24)=exp(\pi*i)=-1 for n=12.
        odd? c => if jacobiUpperStar(d, c) > 0 then r(u) else r(u+12)
        if jacobiLowerStar(c, d) > 0 then r(u-v) else r(u-v+12)

    upsilonExponent(mat: MZ): Z ==
        upsilonExponent(mat(1,1), mat(1,2), mat(2,1), mat(2,2))

    numberOfGaps(n: P, list: List P): N ==
        asN reduce(_+, [floor(x /$Q n) for x in list]$List(Z), 0)

    -- Split matrix as in Lemma~\ref{thm:matrix-splitting} with
    -- z = delta*lambda.
    splitMatrix(mat: SL2Z, delta: Z, m: P, lambda: N): Rec ==
        z: Z := delta*lambda
        w11: Z := delta*mat(1,1) + mat(2,1)*z
        w12: Z := delta*mat(1,2) + mat(2,2)*z
        w21: Z := mat(2,1)*m
        w22: Z := mat(2,2)*m
        splitMatrix(w11, w12, w21, w22)

    -- Split matrix as in Lemma~\ref{thm:matrix-splitting} with
    -- z = 0 and m = 1.
    splitMatrix(mat: SL2Z, delta: Z): Rec ==
        splitMatrix(mat(1,1) * delta, mat(1,2) * delta, mat(2,1), mat(2,2))
        -- Same as splitMatrix(mat, delta, 1, 0).

    -- Split matrix according to Section~\ref{sec:eta-transformation} in
    -- qeta.tex.
    splitMatrix(a: Z, b: Z, c: Z, d: Z): Rec ==
        exgcd: EXGCD := extendedEuclidean(a, c)
        g: Z := exgcd.generator
        d0: Z :=  exgcd.coef1 -- initial value for d1
        b0: Z := -exgcd.coef2 -- initial value for b1
        a1: Z := (a exquo g) :: Z
        c1: Z := (c exquo g) :: Z
        a2: Z := g
        d2: Z := a1*d-b*c1
        -- Now try to find s such that 0 <= b*d1 - d*b1 - s*d2 < d2.
        b2init: Z := b*d0 - d*b0
        b2: Z := positiveRemainder(b2init, d2)
        s: Z := ((b2init - b2) exquo d2) :: Z
        b1: Z := b0 + s * a1
        d1: Z := d0 + s * c1
        m1: MZ := matrix([[a1, b1], [c1, d1]]$LLZ)
        m2: MZ := matrix([[g, b2], [0, d2]]$LLZ)
        [m1, m2]$Rec

)if LiterateDoc
%$

We use \cite[Lemma~2.45]{Radu:PhD:2010} to compute the cusps of
$\Gamma_0(N)$ as the set of all $\frac{a}{c}$ such that $c|N$ and
$a\in X_c$.

)endif

    squarePart(fl: FL): Z ==
        -- if fl = factorList factor x and z=squarePart(fl) then
        -- z^2*r=expand(fl) for some squarefree r.
        z: Z := 1
        for x in fl repeat
            e: P := asP shift(x.exponent, -1)
            z := (x.factor)^e * z
        return z

    rationalSquareRoot(x: Q): Record(rat: Q, rootOf: P) ==
        import from FL
        fden: Factored Z := factor denom x
        fnum: Factored Z := factor numer x
        fl: FL := factorList fden
        for x in fl | odd?(x.exponent) repeat
            fnum := x.factor * fnum
            fden := x.factor * fden
        fl := factorList fnum
        r: Z := 1
        for x in fl | odd? (x.exponent) repeat r := x.factor * r
        return [squarePart(fl)/squarePart(factorList fden), asP r]

    square(x: Record(root: P, elem: Pol)): Vector Z ==
        pol: Pol := cyclotomic(x.root)$CyclotomicPolynomialPackage
        C ==> SimpleAlgebraicExtension(Z, Pol, pol)
        z: C := convert(x.elem)@C
        convert(z*z)@Vector(Z)

    minimalSquareRootOfUnity(x: P): P ==
        -- x is a radical integer (squarefree)
        -- See \cite[Section~IV.3]{Lang:AlgebraicNumberTheory:1986}.
        x = 2 => 8
        positiveRemainder(x, 4)=1 => x
        4*x

    primePower(d: P, p: P): N ==
        e: N := 0;
        x: Z := d
        while not one? gcd(x, p) repeat (x := (x exquo p)::Z; e:=e+1)
        return e

)if LiterateDoc
%$

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsubsection{Related to Generalized Eta-Quotients}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif

    fractionalPart(x: Q): Q == -- local function
        d: Z := denom x
        positiveRemainder(numer x, d)/d

    fractionalBernoulli1(x: Q): Q == fractionalPart x - 1/2
    fractionalBernoulli2(x: Q): Q ==
        t: Q := fractionalPart x
        t^2 - t + 1/6

    sawTooth(x: Q): Q ==
        one? denom x => 0
        fractionalBernoulli1 x
    dedekindSum(p: Z, q: Z): Q ==
        not one? gcd(p, q) => error "dedekindSum: input not coprime"
        ds: Q := 0
        for i in 1..abs(q)-1 repeat
            ds := ds + sawTooth(i/q)*sawTooth(p*i/q)
        return ds
    generalizedDedekindSum(nn: P, g: Z, h: Z, a: Z, c: Z): Q ==
        not one? gcd(a, c) => error "generalizedDedekindSum: input not coprime"
        gds: Q := 0
        gg: Z := a*g + c*h
        for i in 0..abs(c)-1 repeat
            cn := c * nn
            s1: Q := sawTooth((g+i*nn)/cn)
            s2: Q := sawTooth((gg+a*i*nn)/cn)
            gds := gds + s1*s2
        return gds

    generalizedEtaFunctionIndices(nn: P): LLZ ==
        nndivs: List P := DIVISORS nn
        indices: List List Z := empty()
        for d in nndivs repeat indices := cons([d], indices)
        for d in nndivs repeat
            for g in 1..ceiling(d/2)$Q-1 repeat indices := cons([d, g], indices)
        reverse! indices

    lengthOfGeneralizedEtaFunctionIndices(nn: P): N ==
        nndivs: List P := DIVISORS nn
        len: N := 0
        for d in nndivs repeat len := len + asN(ceiling(d/2)$Q)
        return len

    sigmaInfinity(mm: P, rtilde: List List Z): Q ==
        res: Q := 0
        for l in rtilde repeat --assert(#l=3)
            not DIVISIBLEBY?(l.1, mm) =>
                error "sigmaInfinity: delta is not a divisor"
            res := res + l.1 * l.3 * fractionalBernoulli2(l.2/l.1)
        res

    sigmaInfinity(mm: P, r: List Z, rtilde: List List Z): Q ==
        sigmaInfinity(mm, r)::Q + 12*sigmaInfinity(mm, rtilde)

    sigma0(mm: P, rtilde: List List Z): Z ==
        z: Z := 0
        for l in rtilde repeat --assert(#l=3)
            z := z + l.3 * ((mm exquo l.1)::Z)
        z

    sigma0(mm: P, r: List Z, rtilde: List List Z): Z ==
        sigma0(mm, r) + 2*sigma0(mm, rtilde)

    matrixModularGamma1(nn: P, idxs: INDICES): MQ ==
        spitzen: List Cusp := [infinity(), cusp(0,1)]
        rows: List List Q := [matrixEtaOrderRow(nn, c, idxs) for c in spitzen]
        s: List Q := [(#idx=1=>1;0) for idx in idxs]
        rows := cons(s, rows)
        n: N := #idxs
        mat: MQ := new(3, n+2, 0) -- 2 more for "is-an-integer" columns
        for i in 1..3 for row in rows repeat
            for j in 1..n for e in row repeat mat(i, j) := e
        qsetelt!(mat, 2, n+1, 1)
        qsetelt!(mat, 3, n+2, 1)
        return mat

    matrixModularGamma1(nn: P): MQ ==
        matrixModularGamma1(nn, generalizedEtaFunctionIndices nn)

    -- matrixEtaOrderElement(nn, cusp, delta, g) returns the coefficient of
    -- r_{delta.g} in
    -- \cite[Thm.~4]{Robins:GeneralizedDedekindEtaProducts:1994}. See
    -- also Theorem~\ref{thm:matrixEtaOrderRobins}. Since we always
    -- want an expansion in $q^{1/w_\gamma}$ where $w_\gamma$ is the
    -- width of the respective cusp for $\Gamma_1(nn)$, we adjust the
    -- following function for the special case, so that if $A$ is the
    -- result of \code{matrixEtaOrder}$(N)$, then $A \bar{r}$ gives
    -- the order of $g_{\bar{r}}(\gamma\tau)$ in terms of
    -- $q^{1/w_\gamma}$.
    matrixEtaOrderElement(nn: P, cusp: Cusp, idx: LZ): Q ==
        --assert(cusp>0) -- comes from cusps(nn)$CongruenceSubgroupGamma1
        empty? idx =>
            errorPrint("idx", idx)
            error "matrixEtaOrderElement: index not allowed"
        entryFactor: Q := 1
        delta: Z := idx.1
        g: Z := 0
        if #idx=1 then entryFactor := 1/2 else g := idx.2
        -- If cusp=a/c=lambda/(mu*epsilon), then we must have the
        -- condition: gcd(lambda,nn)=gcd(lambda,mu)=gcd(mu,nn)=1. So
        -- we try to find an equivalent cusp with this property.
        -- See function normalize in CongruenceSubgroupGamma1.
        delta <= 0 => error "matrixEtaOrderElement: delta not positive"
        g > delta => error "matrixEtaOrderElement: no such g allowed"
        c: Z := denom cusp
        l: Z := numer cusp -- \lambda
        -- Make cusp (0:1)=infinity a special case, i.e. treat it like
        -- (1:nn). See (11) of Robins.
        if infinity? cusp then
            c := nn
            l := 1
        e: Z := gcd(c, nn) -- \epsilon: divisor of nn (will not change)
        -- Note that nn/e = width(nn, c) (at least if (nn,c)~=(4,2)).
        -- Find a coprime (to nn) cofactor m such that m*e = c (mod nn).
        m: Z := (c exquo e)::Z
        n: Z := (nn exquo e)::Z
        while not one? gcd(m, nn) repeat m := m + n
        -- Find a coprime numerator l such that l/(m*e) is Gamma1(nn)
        -- equivalent to a/c.
        while not one? gcd(l, m) or not one? gcd(nn, l) repeat
            l := l + e
            if l >= nn then -- safety check -- should actually not happen
               errorPrint("[nn, numer cusp, denom cusp, c, m, e]",
                           [nn, numer cusp, denom cusp, c, m, e]$List(Z))
               error "matrixEtaOrder: lambda too big"

        h: Z := gcd(delta, e)
        z: Q := entryFactor * (nn*h^2/(2*delta*e)) * fractionalBernoulli2(l*g/h)
        nn = 4 and e = 2 => 1/2*z
        z

    matrixEtaOrderRow(nn: P, cusp: Cusp, idxs: INDICES): List Q ==
        [matrixEtaOrderElement(nn, cusp, idx) for idx in idxs]

    matrixEtaOrderFull(nn: P, spitzen: List Cusp, idxs: INDICES): MQ ==
        matrix [matrixEtaOrderRow(nn,cusp,idxs) for cusp in spitzen]

    matrixEtaOrder(nn: P, spitzen: List Cusp, idxs: INDICES): MQ ==
        ll := [matrixEtaOrderRow(nn,cusp,idxs) for cusp in spitzen]
        matrix removeDuplicates! ll

    matrixEtaOrder(nn: P, spitzen: List Cusp): MQ ==
        matrixEtaOrder(nn, spitzen, generalizedEtaFunctionIndices nn)

    matrixEtaOrder(nn: P): MQ ==
        matrixEtaOrder(nn, cusps(nn)$CongruenceSubgroupGamma1)

    matrixEtaOrderModularGamma1(nn: P, spitzen: List Cusp, idxs: INDICES): MQ ==
        matmod1 := matrixModularGamma1(nn, idxs)
        matord := matrixEtaOrder(nn, spitzen, idxs)
        m := nrows matord
        mat: MQ := new(m+3, ncols matmod1, 0)
        --assert(nrows(matmod1)=3)
        --assert(ncols matord + 2 = ncols matmod1)
        for i in 1..m repeat
            for j in 1..ncols matord repeat
                qsetelt!(mat, i, j, matord(i, j))
        for i in 1..3 repeat
            for j in 1..ncols matmod1 repeat
                qsetelt!(mat, m+i, j, matmod1(i, j))
        return mat

    matrixEtaOrderModularGamma1(nn: P, spitzen: List Cusp): MQ ==
        idxs:=generalizedEtaFunctionIndices(nn)
        matrixEtaOrderModularGamma1(nn, spitzen, idxs)

    kappaSchoeneberg(nn: P, g: Z, h: Z, a: Z, b: Z, c: Z, d: Z): Q ==
        zero? c => fractionalPart(b/(2*d)*fractionalBernoulli2(g/nn))
        gprime: Z := a*g + c*h
        x := a/(2*c)*fractionalBernoulli2(g/nn) _
             + d/(2*c)*fractionalBernoulli2(gprime/nn)
        gds := generalizedDedekindSum(nn, g, h, a, c)
        c < 0 => fractionalPart(x + gds)
        fractionalPart(x - gds)

    kappaYang(nn: P, g: Z, h: Z, a: Z, b: Z, c: Z, d: Z): Q ==
        r: Q := 1/2* ((g^2*a*b + 2*g*h*b*c + h^2*c*d)/nn^2 _
           - (g*b + h*(d-1))/nn) -- corresponds to delta/2 in Yang's paper
        u: Z := c*(a+d-3)+b*d*(1-c^2)
        odd? c => fractionalPart(u/12 + r)
        even? d => error "kappaYang: d is even"
        v: Z := a*c*(1-d^2)+d*(b-c+3)
        minusI: Q := -1/4
        fractionalPart(minusI + v/12 + r)

    kappaSchoeneberg(nn: P, g: Z, h: Z, mat: MZ): Q ==
        kappaSchoeneberg(nn, g, h, mat(1,1), mat(1,2), mat(2,1), mat(2,2))

    kappaYang(nn: P, g: Z, h: Z, mat: MZ): Q ==
        kappaYang(nn, g, h, mat(1,1), mat(1,2), mat(2,1), mat(2,2))

































)if LiterateDoc
%$

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Unused Package QAuxiliaryModularPackage: Annihilate
leading coefficients}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Add $p$-multiples of original generators in order to reduce the
order of the series. Return the respective (adjustment) polynomial.

)endif

)if UNUSED

--)abbrev package QAUXMOD QAuxiliaryModularPackage
QAuxiliaryModularPackage(L): Exports == Implementation where
  L: QEtaLaurentSeriesCategory Z
  ZX ==> Polynomial Z
  VZ ==> Vector Z
  term x ==> monomial(1, degree x)$ZX
  lc x ==> leadingCoefficient x
  Exports ==> with
    annihilateCoefficients: (P, List ZX, ZX -> L) -> ZX -> ZX
    annihilateLeadingCoefficients: (P, ZX -> L) -> ZX -> ZX
      ++ annihilateLeadingCoefficients(p, ev)(pol) adds a p-multiple
      ++ to any monomial of pol and returns a polynomial z such that
      ++ the evaluation order ev(z) >= order ev(pol) and the order
      ++ of ev(z) is as big as it can be made by varying each of the
      ++ monomials, i.e. we expect
      ++ order(ev z) = order(ev pol) + #monomials(pol) - 1.
      ++ The polynomial pol is assumed to be nonzero.
  Implementation ==> add
    annihilateCoefficients(p: P, mons: List ZX, ev: ZX -> L): ZX -> ZX ==
      (pol: ZX): ZX +->
        --assert(not zero? pol)
        ll: L := ev pol
        zero? ll => primitivePart pol
        ls: List L := [ev term x for x in mons]
        ords: List Z := [order(l, 1) for l in ls]
        o: Z := min ords
        nmons: N := # mons      -- number of variables
        for neqs in nmons..1 by -1 repeat
            mat: MZ := new(neqs, nmons, 0)
            v: VZ := new(neqs, 0)
            for i in 1..neqs repeat -- number of equations
                v.i := - coefficient(ll, o+i-1)
                for j in 1..nmons for l in ls repeat -- number of coefficients
                    mat(i, j) := p*coefficient(l, o+i-1)
            sol := diophantineSystem(mat, v)$SmithNormalForm(Z, VZ, VZ, MZ)
            if sol.particular case VZ then
                s := (sol.particular) :: VZ
                adjust := [p*c*term(x) for c in members s for x in mons]
                z := pol + reduce(_+, adjust, 0)
                ll := ev z
                c: Z := coefficient(ll, 0) quo p
                z := z - p*c*1$ZX
                zero? ev z => return primitivePart z
                -- divide by primes not in p
                c := content z
                primesp := [x.factor for x in factorList factor p]
                for x in primesp repeat
                    while zero? positiveRemainder(c, x) repeat
                        c := (c exquo x)::Z
                return (z exquo c)::ZX
        tracePrint("annihilateLeadingCoefficients", "nothing annihilated"::Symbol)
        return primitivePart pol

    annihilateLeadingCoefficients(p: P, ev: ZX -> L): ZX -> ZX ==
      (pol: ZX): ZX +->
        --assert(not zero? pol)
        mons: List ZX := monomials pol
        tracePrint("mons", [term(x) for x in mons])
        annihilateCoefficients(p, mons, ev)(pol)

)endif


)if LiterateDoc
%$
\bibliography{qeta}
\end{document}
)endif
