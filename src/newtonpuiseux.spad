-------------------------------------------------------------------
---
--- FriCAS QEta
--- Copyright (C) 2019-2021,  Ralf Hemmecke <ralf@hemmecke.org>
---
-------------------------------------------------------------------
-- This program is free software: you can redistribute it and/or modify
-- it under the terms of the GNU General Public License as published by
-- the Free Software Foundation, either version 3 of the License, or
-- (at your option) any later version.
--
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
-- GNU General Public License for more details.
--
-- You should have received a copy of the GNU General Public License
-- along with this program.  If not, see <http://www.gnu.org/licenses/>.
-------------------------------------------------------------------

)if LiterateDoc
\documentclass{article}
\usepackage{qeta}
\begin{document}
\title{Newton-Puiseux Method}
\author{Ralf Hemmecke}
\date{08-Aug-2019}
\maketitle
\begin{abstract}
  We implement the Newton-Puiseux method for generating Puiseux series
  solutions of a polynomial equation $f(x,y)=\sum_{k=0}^na_k(x)y^k=0$
  with $a_k\in K[x,x^{-1}]$ being Laurent polynomials in $x$.

  \url{https://homepages.math.uic.edu/~jan/mcs563s14/puiseux.pdf}

  \url{http://jaypantone.com/courses/winter16math118/lecture-notes/lecture-notes-25.pdf}

  \url{https://www.risc.jku.at/education/courses/ss2016/caag/09-puiseux.pdf}
\end{abstract}

\tableofcontents

\section{Overview}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Implementation}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif

OF==>OutputForm
display77(x) ==> display((x::OF)::Formatter(Format1D))
yof x ==> x :: Symbol :: OF
dbgPrint(x,y) ==> display77([yof ":> ", yof x, y::OF]$List(OF))
tracePrint(x,y) ==> display77(hconcat([yof"-- ",yof x,yof":=",y::OF]$List(OF)))

rep x ==> (x@%) pretend Rep
per x ==> (x@Rep) pretend %

P ==> PositiveInteger
N ==> NonNegativeInteger
Z ==> Integer
Q ==> Fraction Z
SY ==> Symbol
POLYK ==> Polynomial K
KX ==> SparseUnivariatePolynomial K -- K[x]
KXY ==> SparseUnivariatePolynomial KX -- KX[y]
KPX ==> PolynomialRing(K, Q) -- Puiseux polynomials
KPXY ==> SparseUnivariatePolynomial KPX -- KPX[y]
SlopePoints K ==> Record(slope: K, points: List Point K)
PRENP(K) ==> PreNewtonPuiseux K

asN x ==> x pretend N
asP x ==> x pretend P
asZ x ==> x pretend Z

)if LiterateDoc
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{QPowers}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif
)abbrev domain QPOWER QPower
QPower(v: SY): Join(QuotientFieldCategory Z, OpenMath, Canonical) == Q add
    Rep ==> Q
    xx ==> rep x
    coerce(x: %): OutputForm ==
        o: OutputForm := v::OutputForm
        if not one? xx then o := o ^ (xx::OutputForm)
        return o

)if LiterateDoc
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{NewtonPolygon}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif
)abbrev package NEWTONP NewtonPolygon
++ Author: Ralf Hemmecke
++ Description:
++ Implement Newton polygon computation
NewtonPolygon(K: Join(Field, OrderedSet)): Exports == Implementation where
  Exports ==> with

    pointsOfNewtonPolygon: List Point K -> List Point K
      ++ pointsOfNewtonPolygon(points) returns the vertices of the
      ++ Newton polygon corresponding to points.
    slopePointsOfNewtonPolygon: List Point K -> List SlopePoints K
      ++ slopePointsOfNewtonPolygon(points) returns the slopes
      ++ of the Newton polygon corresponding to p together with the
      ++ points forming this slope.

  Implementation ==> add

    normalize(points: List Point K): List Point K ==
        -- Check whether all points are 2-dimensional
        for point in points repeat
            if dimension point ~= 2 then
                error "not all points live in a plane"

        -- points are ascendingly ordered first by x-coordinate
        -- then by y-coordinate.
        points := sort(_<, points)$List(Point K)
        -- Take only the points with different x part.
        p1: Point K := first points
        points := rest points
        pts: List Point K := [p1]
        for p2 in points repeat
            if p2.1 > p1.1 then
                p1 := p2
                pts := cons(p2, pts)
        reverse! pts

    pointsOfNewtonPolygon(points: List Point K): List Point K ==
        points := normalize points
        -- points.1 and points(#points) are certainly vertices of the
        -- Newton polygon. We start with points.1, add new points, but
        -- while adding new points, old points are removed from the
        -- Newton polygon, if they lie above the edge with a previous
        -- point of the current Newton polygon.
        p1: Point K := first points;  points := rest points
        p2: Point K := first points;  points := rest points
        npPoints: List Point K := [p2, p1] -- Newton polygon points
        slopes: List K := [(p2.2  - p1.2) / (p2.1 - p1.1)]
        for np in points repeat -- newpoint
            pt: Point K := first npPoints
            slope: K := (np.2 - pt.2) / (np.1 - pt.1)
            while not empty? slopes and slope <= first slopes repeat
                -- remove old points
                slopes := rest slopes
                npPoints := rest npPoints
                pt := first npPoints
                slope := (np.2 - pt.2) / (np.1 - pt.1)
            slopes := cons(slope, slopes)
            npPoints := cons(np, npPoints)
        reverse! npPoints

    slopePointsOfNewtonPolygon(points: List Point K): List SlopePoints K ==
        points := normalize points
        -- points.1 and points(#points) are certainly vertices of the
        -- Newton polygon. We start with points.1, add new points, but
        -- while adding new points, old points are removed from the
        -- Newton polygon, if they lie above the edge with a previous
        -- point of the current Newton polygon.
        p1: Point K := first points;  points := rest points
        p2: Point K := first points;  points := rest points
        slope: K := (p2.2  - p1.2) * (1 / (p2.1 - p1.1))
        slopePointList: List SlopePoints K := [[slope, [p1, p2]]]
        for np in points repeat -- newpoint
            slopePoints: SlopePoints K := first slopePointList
            sPoints: List Point K := slopePoints.points
            -- p1 := sPoints.1 -- startpoint of last slope
            pt := sPoints.2 -- endpoint of last slope
            slope: K := (np.2 - pt.2) / (np.1 - pt.1)
            if slope = slopePoints.slope then
                slopePoints.points := cons(sPoints.1, cons(np, rest sPoints))
            else
                while not empty? slopePointList and
                      slope < first(slopePointList).slope repeat
                    -- remove useless slopes (and points)
                    pt := first(first(slopePointList).points) --
                    slopePointList := rest slopePointList
                    slope := (np.2 - pt.2) / (np.1 - pt.1)
                slopePointList := cons([slope, [pt, np]], slopePointList)
        reverse! slopePointList


)if LiterateDoc
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{PreNewtonPuiseux}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

PreNewtonPuiseux holds data that is later used for the creation of the
Puiseux series that are zeros of the given (bivariate) polynomial.

)endif

)abbrev domain PRENP PreNewtonPuiseux
++ Author: Ralf Hemmecke
++ Description:
++ A container that holds all data necessary to create the respective
++ Puiseux series in case maxidx = #arr.
PreNewtonPuiseux(K): Exports == Implementation where
  K: Field
  PUIMON ==> Record(k: Q, c: K)
  PUIREC ==> Record(mons: List PUIMON, curpol: KPXY)
  Exports ==> CoercibleTo OutputForm with
    new: (N, List PUIMON, KPXY) -> %
    _#: % -> N
    maxIndex: % -> N
    elt: (%, N) -> PUIREC
    setelt!: (%, N, PUIREC) -> PUIREC
    extend!: (%, PUIREC) -> PUIREC
  Implementation ==> add
    Rep ==> Record(maxidx: N, arr: PrimitiveArray PUIREC)
    import from Rep
    coerce(x: %): OutputForm == rep(x)::OutputForm
    emptyPUIREC: PUIREC := [empty()$List(PUIMON), 0$KPXY]
    new(n: N, monoms: List PUIMON, p: KPXY): % ==
       a: PrimitiveArray PUIREC := new(n, emptyPUIREC)
       i: N := 0
       for mon in monoms repeat
           a.i := [[mon], p]$PUIREC
           i := i+1
       per [i, a]
    #(x: %): N == #(rep(x).arr)
    maxIndex(x: %):N == rep(x).maxidx
    elt(x: %, i: N): PUIREC == rep(x).arr.(asZ(i)-1)
    setelt!(x: %, i: N, pr: PUIREC): PUIREC ==
        a: PrimitiveArray PUIREC := rep(x).arr
        a.(asZ(i) - 1) := pr
    extend!(x: %, pr: PUIREC): PUIREC ==
        a: PrimitiveArray PUIREC := rep(x).arr
        i: N := rep(x).maxidx
        rep(x).maxidx := i+1
        a.i := pr

)if LiterateDoc
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{NewtonPuiseux}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif

)abbrev package NEWTPUIS NewtonPuiseux
++ Author: Ralf Hemmecke
++ Description:
++ Implements the computation of a Puiseux series satisfying a bivariate
++ polynomial.
NewtonPuiseux(K, var, cen): Exports == Implementation where
  K: Join(AlgebraicallyClosedField, PolynomialFactorizationExplicit)
  var: Symbol
  cen: K
  UPX ==> UnivariatePuiseuxSeries(K, var, cen)
  PUIMON ==> Record(k: Q, c: K)
  PUIREC ==> Record(mons: List PUIMON, curpol: KPXY)

  Exports ==> with
    leadingPuiseuxMonomials: (KPXY, Q) -> List PUIMON
    leadingPuiseuxMonomials: KPXY -> List PUIMON
      ++ leadingPuiseuxMonomials(p) returns for each slope of the
      ++ Newton polygon of p the leading monomial of the Puiseux
      ++ series Y satisfying p(x, Y(x)) = 0 in terms of the defining
      ++ polynomial for the leading coefficient, i.e., one monomial
      ++ might correspond to several coefficients according to the
      ++ degree of the defining polynomial.
    leadingPuiseuxMonomials: KXY -> List PUIMON
      ++ leadingPuiseuxMonomials(p) returns for each slope of the
      ++ Newton polygon of p the leading monomial of the Puiseux
      ++ series Y satisfying p(x, Y(x)) = 0 in terms of the defining
      ++ polynomial for the leading coefficient, i.e., one monomial
      ++ might correspond to several coefficients according to the
      ++ degree of the defining polynomial.
    leadingPuiseuxMonomials: (POLYK, Symbol, Symbol) -> List PUIMON
      ++ leadingPuiseuxMonomials(p, x, y) returns for each slope of
      ++ the Newton polygon of p the leading monomial of the Puiseux
      ++ series Y satisfying p(x, Y(x)) = 0 in terms of the defining
      ++ polynomial for the leading coefficient, i.e., one monomial
      ++ might correspond to several coefficients according to the
      ++ degree of the defining polynomial.

    polynomial2Points: (POLYK, Symbol,Symbol) -> List Point Q
    polynomial2Points: KXY -> List Point Q
    polynomial2Points: KPXY -> List Point Q

    poly2KPXY: (POLYK, Symbol, Symbol) -> KPXY
    nextY: (KPXY, PUIMON) -> KPXY
    nextPuiseuxMonomials: PRENP K -> PRENP K
    coefficientRelation: (KPXY, List Point Q) -> KX
      ++ coefficientRelation(p, points) returns the relation that the
      ++ coefficient must fulfil in order to be a coefficient of the
      ++ puiseux series.

    puiseuxSolutions: KPXY -> List UPX
      ++ puiseuxSolutions(p) returns degree(p) Puiseux series solutions
      ++ s such that p(s)=0.

    coerce: KPX -> UPX
    elt: (KPXY, UPX) -> UPX
      ++ p(s) replaces the variable in the univariate polynomial ring
      ++ by the Puiseux series s.

  Implementation ==> add
    -- For hacking the fact that the implementation of AlgebraicNumber
    -- is actually not of category noZeroDivisors, we introduce the
    -- functions removeZeroCoefficients[X].
    removeZeroCoefficientsX(p: KPX): KPX ==
        zero? p => p
        m := leadingMonomial p
        z := removeZeroCoefficientsX reductum p
        c: K := leadingCoefficient m
        zero? c => z
        m + z

    removeZeroCoefficients(p: KPXY): KPXY ==
        zero? p => p
        m := leadingMonomial p
        z := removeZeroCoefficients reductum p
        c: KPX := leadingCoefficient m
        zero? removeZeroCoefficientsX c => z
        m + z

    coefficientRelation(p: KPXY, points: List Point Q): KX ==
        --assert(#point > 1)
        --assert(one? denom(pt.1) for all pt in points)
        coefficientPoly: KX := 0
        ymin: Z := numer(first(points).1)
        for point in rest points repeat ymin := min(ymin, numer point.1)
        for point in points repeat
            yex: Z := numer(point.1)
            cx: KPX := coefficient(p, asN(yex))
            c: K := coefficient(cx, point.2)
            if not zero? c then
                coefficientPoly := coefficientPoly + monomial(c, asN(yex-ymin))
        squareFreePart coefficientPoly

    toKPX(p: KX): KPX ==
        z: KPX := 0
        for m in reverse monomials p repeat -- from smallest to highest degree
            z := z + monomial(leadingCoefficient p, degree(p)::Q)
        return z

    toKPXY(p: KXY): KPXY ==
        z: KPXY := 0
        for m in reverse monomials p repeat -- from smallest to highest degree
            z := z + monomial(toKPX leadingCoefficient p, degree p)
        return z

    poly2KPXY(p: POLYK, x: Symbol, y: Symbol): KPXY ==
        -- sanity check
        sort variables p ~= sort [x, y] =>
            error "not a polynomial in the given variables"
        z: KPXY := 0
        for m in monomials p repeat
            mx: KPX := monomial(leadingCoefficient m, degree(m, x)::Q)$KPX
            z := z + monomial(mx, degree(m, y))$KPXY
        z

    -- We don't need exponents in the result that are smaller than e.
    biggerOrderThan?(e: Q): Q -> Boolean == (o: Q): Boolean +-> o > e
    leadingPuiseuxMonomialsOrder(p: KPXY, cond?: Q -> Boolean): List PUIMON ==
        import from NewtonPolygon(Q)
        points: List Point Q := polynomial2Points p
        slopePointList: List SlopePoints Q := slopePointsOfNewtonPolygon points
        mons: List PUIMON := empty()$List(PUIMON)
        for slopePoints in slopePointList | cond?(e:=-slopePoints.slope) repeat
            pnts := slopePoints.points
            cp: KX := coefficientRelation(p, pnts) -- assert(not zero? cp 0)
            roots: List K := rootsOf cp
            -- loop over the roots of the defining poly
            for c in roots repeat mons := cons([e, c]$PUIMON, mons)
        mons

    -- Here we assume that there is no branching anymore.
    --TODO: Code should be optimized, since we only need exponent > expo.
    leadingPuiseuxMonomial(p: KPXY, expo: Q): PUIMON ==
        import from NewtonPolygon(Q)
        points: List Point Q := polynomial2Points p
        slopePointList: List SlopePoints Q := slopePointsOfNewtonPolygon points
        for slopePoints in slopePointList | (e:=-slopePoints.slope)>expo repeat
            pnts := slopePoints.points
            cp: KX := coefficientRelation(p, pnts) -- assert(not zero? cp 0)
            return [e, rootOf cp]$PUIMON
        error "leadingPuiseuxMonomial: should never come here"
        --TODO: Can this "error" ever appear? Before we call this function,
        -- We check whether the last monomial is a root of the previous
        -- polynomial. the new p shouldn't have that monomial as root.

-- exported functions

    polynomial2Points(p: POLYK, x: Symbol, y: Symbol): List Point Q ==
        sort variables p ~= sort [x, y] =>
            error "not a polynomial in the given variables"
        [point [degree(m, y)::Q, degree(m, x)::Q] for m in monomials p]

    polynomial2Points(p: KXY): List Point Q ==
        points: List Point Q := empty()
        while not zero? p repeat
            ydeg := degree p
            cx: KX := leadingCoefficient p
            p := reductum p
            while not zero? cx repeat
                xdeg: Z := degree cx
                c: K := leadingCoefficient cx
                cx := reductum cx
                points := cons(point [ydeg::Q, xdeg::Q], points)
        return points

    polynomial2Points(p: KPXY): List Point Q ==
        points: List Point Q := empty()
        while not zero? p repeat
            ydeg := degree p
            cx: KPX := leadingCoefficient p
            p := reductum p
            while not zero? cx repeat
                xdeg: Q := degree cx
                c: K := leadingCoefficient cx
                cx := reductum cx
                points := cons(point [ydeg::Q, xdeg], points)
        return points

    leadingPuiseuxMonomials(p: KPXY, e: Q): List PUIMON ==
        leadingPuiseuxMonomialsOrder(p, biggerOrderThan? e)

    leadingPuiseuxMonomials(p: KPXY): List PUIMON ==
        leadingPuiseuxMonomialsOrder(p, (o: Q): Boolean +-> true)


    leadingPuiseuxMonomials(p: KXY): List PUIMON ==
        pp: KPXY := toKPXY p
        leadingPuiseuxMonomials(pp)

    leadingPuiseuxMonomials(p: POLYK, x: Symbol, y: Symbol): List PUIMON ==
        leadingPuiseuxMonomials poly2KPXY(p, x, y)

    nextPuiseuxMonomials(prenp: PRENP K): PRENP K ==
        maxidx: N := maxIndex prenp
        for i in 1..maxidx | (r := prenp.i; p := r.curpol; not zero? p) repeat
            puimons: List PUIMON := r.mons
            puimon := first puimons
            c: K := puimon.c
            e: Q := puimon.k

            -- First check whether a finite series is a solution.
            cxe: KPXY := monomial(monomial(c, e)$KPX, 0)$KPXY
            y := monomial(1, 1)$KPXY + cxe
            pnew := p y --substiture p(y+c*x^e) for the new polynomial
            -- Hack: If K is AlgebraicNumber then the new element c
            -- might have introduced zerodivisors.
            -- https://groups.google.com/forum/#!msg/fricas-devel/sowcAymy04s/xWBnEr1QCAAJ
            -- In order to avoid zero coefficients in polynomials from
            -- KPX, we explicitly remove such possible zero
            -- coefficients.
            pnew := removeZeroCoefficients pnew

            -- We only need monomials with bigger degree than e.
            monoms: List PUIMON := leadingPuiseuxMonomials(pnew, e)
            -- If it is empty we mark that we don't have to do anything
            -- in the next step, i.e., we set curpol:=0.

            -- Handle i-th record r=prenp.i
            if zero? p cxe then
                r.curpol := 0 -- modify in-place
              else
                empty? monoms => r.curpol := 0
                puimon: PUIMON := first monoms
                monoms := rest monoms
                r.mons := cons(puimon, puimons) -- update in-place
                r.curpol := pnew -- update in-place

            -- add new entries
            for puimon in monoms repeat
                extend!(prenp, [cons(puimon, puimons), pnew]$PUIREC)
        prenp

    puiseuxStream(p: KPXY, mon: PUIMON): Stream PUIMON == delay
        zero? p => empty()
        -- First check whether a finite series is a solution.
        e: Q := mon.k
        cxe: KPXY := monomial(monomial(mon.c, e)$KPX, 0)$KPXY
        zero? p cxe => empty()
        y := monomial(1, 1)$KPXY + cxe
        pnew := p y --substiture p(y+c*x^e) for the new polynomial
        pnew := removeZeroCoefficients pnew -- work over algebraic extension

        -- We only need monomials with bigger degree than e=monnew.k.
        monnew: PUIMON := leadingPuiseuxMonomial(pnew, e)
        cons(monnew, puiseuxStream(pnew, monnew))


    puiseux(puirec: PUIREC): UPX ==
        terms: List PUIMON := puirec.mons
        --assert(not empty? terms)
        den: N := asN lcm [denom(m.k) for m in terms]
        st: Stream PUIMON := puiseuxStream(puirec.curpol, first terms)
        for mon in terms repeat st := cons(mon, st)
        series(den, st)

    puiseuxSolutions(p: KPXY): List UPX ==
        d := degree p
        puimons: List PUIMON := leadingPuiseuxMonomials p
        m := new(d, puimons, p)$PRENP(K)
        -- repeat until the initial segments of all solutions can be
        -- distinguished.
        while d > maxIndex m repeat m := nextPuiseuxMonomials m
        -- Now construct Puiseux series from the PreNewtonPuiseux data.
        [puiseux(m.i) for i in 1..d]$List(UPX)

    coerce(p: KPX): UPX ==
        z: UPX := 0
        terms: List PUIMON := empty()
        den: Z := 1
        while not zero? p repeat
            t := leadingTerm p
            den := lcm(den, denom t.k)
            terms := cons(leadingTerm p, terms)
            p := reductum p
        st: Stream PUIMON := coerce terms
        series(asN den, st)

    elt(p: KPXY, y: UPX): UPX ==
        d := degree p
        lc: KPX := leadingCoefficient p
        p := reductum p
        z := lc::UPX
        while not zero? p repeat
            e := degree p
            lc: KPX := leadingCoefficient p
            p := reductum p
            z := z*y^(d-e) + (lc::UPX)
            d := e
        z * y^d


)if LiterateDoc
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Functional Laurent Inverse}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif

)abbrev package FLAUINV FunctionalLaurentInverse
++ Author: Ralf Hemmecke
++ Description:
++ For a Laurent series f(q) = q^(-n)(1 + a1*q + a2*q^2 + ...) find
++ all n Puiseux series q1(v), ..., qn(v) such that f(qi(v))=v for
++ all v in a close neighborhood of v_0.
++ Algorithm follows \cite{Paule+Radu:ProofWeierstrassGapTheorem:2019}
++ Section 6.
FunctionalLaurentInverse(K): Exports == Implementation where
  Z ==> Integer
  Q ==> Fraction Z
  K: Join(Algebra Q, PolynomialFactorizationExplicit)
  LX ==> QEtaLaurentSeries K
  TX ==> QEtaTaylorSeries K
  Exports ==> with
    functionalInverse: LX -> LX
      ++ functionalInverse(t) where t is non-zero and d = order(t) ~=
      ++ 0 returns a Laurent series w of order 1 such that t(w(q))=q^d
      ++ Note that this is the same as doing the following:
      ++ functionalInverse(t) == revert nthRoot(t, n)
  Implementation ==> add
    functionalInverse(l: LX): LX ==
        r: LX := nthRoot(l, order l) -- This gives a series of order =1.
        not one? order r => error "functionalInverse: order should be 1"
        q: TX := monomial(1, 1)$TX
        t: TX := q * qetaTaylorRep r
        t := revert(t)$TX
        laurent(0, t)$LX

)if oldCode
        --assert(not zero? l)
        --assert(one? leadingCoefficient l)
        -- below assume q = monomial(1,1)
        d: Z := order l
        t: TX := taylorRep l -- l = q^d * t
        r: TX := nthRoot(t, -d) -- order(t) = 0 = order(r)
        --assert(l = (q/r)^d)
        w := lagrange r
        --assert(order(w)=1)
        -- Note that by specification of the lagrange function we have
        -- w(q) = q * r(w(q)), i.e. r(w(q)) = w(q)/q.
        -- Therefore, l(w)=(w/r(w))^d = (w/(w/q))^d = q^d
        return laurent(0, w)
)endif

)if LiterateDoc
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Compute Determinant by Leibniz formula}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif

)if UNUSED

--)abbrev package TULS TruncatedUnivariateLaurentSeries
++ Author: Ralf Hemmecke
++ Description:
++
++ TruncatedUnivariateLaurentSeries is an implementation of Laurent
++ series with finite precision, it is a pair of a Laurent series and
++ the number of coefficients that are considered to be valid
++ (precision). Such a series is considered to be zero if all
++ "valid" coefficients are zero. Two series are equal if they are
++ equal up to the minimum of their respective validity index.
TruncatedUnivariateLaurentSeries(R) : Exports == Implementation where
  R: Ring
  QLS ==> QEtaLaurentSeries
  QTS ==> QEtaTaylorSeries
  Exports ==> Field with
    truncate: (QLS R, Z) -> %
      ++ truncate(l, n) yields a truncated Laurent series at order n.
    precision: % -> Z
      ++ precision(x) returns the number of valid coefficient in
      ++ coefficients(x).
    truncatedSeries: % -> QLS R
      ++ truncatedSeries(x) returns the series truncated at the order
      ++ given by the precision.
    laurentRep: % -> QLS R
      ++ laurentRep(X) returns the internally used Laurent series
      ++ representation of the x. That need not necessarily be a
      ++ finite series.
  Implementation ==> add
    Rep ==> Record(ser: QLS R, pr: Z) -- (series, precision)

    pZERO ==> -1
    pNONZEROCONSTANT ==> -2

    truncate(l: QLS R, n: Z): % == per [l, max(0, n - order(l, 0))]
    precision(x: %): Z == rep(x).pr
    laurentRep(x: %): QLS R == rep(x).ser
    truncatedSeries(x: %): QLS R ==
        l: QLS R := laurentRep x
        d: Z := degree l
        truncate(l, precision x - d)

    characteristic(): N == characteristic()$R
    hashUpdate!(s: HashState, x: %): HashState == error "unimplemented"
    coerce(x: %): OutputForm == truncatedSeries(x)::OutputForm
    -- We treat 0 and 1 in a special way.
    0: % == truncate(0$QLS(R), pZERO)
    1: % == truncate(1$QLS(R), pNONZEROCONSTANT)

    zero?(x: %): Boolean ==
        precision x = pZERO => true  -- x=0
        precision x = pNONZEROCONSTANT => false -- x=1
        cs: Stream R := coefficients taylorRep laurentRep x
        for i in 0.. precision x while not empty? cs repeat
            not zero? frst cs => return false
            cs := rest cs
        true

    one?(x: %): Boolean ==
        precision x = pZERO => false -- x=0
        precision x = pNONZEROCONSTANT and
            one? coefficient(laurentRep x, 0) => true  -- x=1
        not zero? order(laurentRep x, precision x) => false
        cs: Stream R := coefficients taylorRep laurentRep x
        empty? cs or not one? first cs => false
        cs := rst cs
        for i in 1 .. precision x while not empty? cs repeat
            not zero? frst cs => return false
            cs := rst cs
        true

    ((x: %) = (y: %)): Boolean ==
        EQ(x, y)$Lisp => true
        px := precision x;
        py := precision y;
        px = pZERO => zero? y
        py = pZERO => zero? x
        px = pNONZEROCONSTANT => one? y
        py = pNONZEROCONSTANT => one? 1
        order(laurentRep x, precision x) ~= order(laurentRep y, precision y) =>
            false
        zero?(x - y)

    PLUSMINUS(x, y, op, zeroxres) ==>
        (px: Z := precision x) = pZERO => zeroxres
        (py: Z := precision y) = pZERO => x
        lx: QLS(R) := laurentRep x
        ly: QLS(R) := laurentRep y
        dx: Z := degree lx
        dy: Z := degree ly
        px = pNONZEROCONSTANT =>
            py = pNONZEROCONSTANT =>
                lz := op(lx, ly)
                zero? coefficient(lz, 0) => 0
                per [lz, pNONZEROCONSTANT]
            dy <= 0 =>
                dy + py >= 0 => per [op(lx, ly), py]
                -- outside of precision
                y
            -- dy > 0
            per [op(lx, ly), dy + py]

        py = pNONZEROCONSTANT =>
            dx <= 0 =>
                dx + px >= 0 => per [op(lx, ly), px]
                -- outside of precision
                x
            -- dy > 0
            per [op(lx, ly), dx + px]
        -- here we have x and y non-constant series

        p := min(px + dx, py + dy) - min(dx, dy)
        per [op(lx, ly), p]

    ((x: %) + (y: %)): % == PLUSMINUS(x, y, (_+),  y)
    ((x: %) - (y: %)): % == PLUSMINUS(x, y, (_-), -y)

    (-(x: %)): % ==
        (px := precision x) = pZERO => x
        per [- laurentRep x, px]

    ((x: %) * (y: %)): % ==
        (px: Z := precision x) = pZERO or (py: Z := precision y) = pZERO => 0
        lx: QLS R := laurentRep x
        ly: QLS R := laurentRep y
        lz := lx * ly
        px = pNONZEROCONSTANT =>
            py = pNONZEROCONSTANT =>
                zero? coefficient(lz, 0) => 0
                per [lz, pNONZEROCONSTANT]
            per [lz, py]
        py = pNONZEROCONSTANT => per [lz, px]
        per [lz, min(px, py)]

    ((z: Z) * (y: %)): % == (z::Q) * y

    ((x: %) ^ (n: P)): % == per [laurentRep(x)^n, precision x]
    ((x: %) ^ (n: N)): % == per [laurentRep(x)^n, precision x]
    if R has Field then
        ((x: %) ^ (n: Z)): % == per [laurentRep(x)^n, precision x]
        inv(x: %): % ==
            (p := precision x) = pZERO => error "zero is not invertible"
            per [inv laurentRep x, precision x]

)endif


)if LiterateDoc
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Compute Determinant by Leibniz formula}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

)endif

)if UNUSED

--)abbrev package LEIBDET DeterminantByLeibnizFormula
++ Author: Ralf Hemmecke
++ Description:
++ Laurent series in FriCAS are declared to be of category Field, but
++ due to the non-existing zero test they do not form an "algorithmic"
++ field.
DeterminantByLeibnizFormula(R): Exports == Implementation where
  R: CommutativeRing -- We do not use "zero?" or "=".
  Exports ==> with
    determinantLeibniz: Matrix R -> R
      ++ determinantLeibniz(m) computes the determinant of m by
      ++ applying Leibniz' formula and recursively calling the same
      ++ algorithm on the submatrices.
  Implementation ==> add
    det(m: Matrix R, ndim: Z, ri: List Z, ci: List Z): R ==
        zero? ndim => 1
        j := first ci; ci := rest ci
        i := first ri; ri := rest ri
        one? ndim => qelt(m, i, j)
        ndim = 2 =>
            i2 := first ri; j2 := first ci
            m(i,j) * m(i2,j2) - m(i,j2) * m(i2,j)
        n: Z := ndim - 1
        d := m(i,j) * det(m, n, ri, ci)
        s: Z := -1; l: List Z := [i]
        while not empty? ri repeat
            i := first ri
            mij: R := m(i,j)
            ri := rest ri
            ri2: List Z := ri
            for k in l repeat ri2 := cons(k, ri2)
            d := d + s * mij * det(m, n, ri2, ci)
            s := -s
            l := cons(i, l)
        return d

    determinantLeibniz(m: Matrix R): R ==
        (ndim: Z := nrows m) ~= (ncols m) =>
            error "determinant: matrix must be square"
        ci: List Z := [i for i in 1..ndim] -- column indices
        ri: List Z := [i for i in 1..ndim] -- row indices
        det(m, ndim, ri, ci)

)endif

)if LiterateDoc
\bibliography{qeta}
\end{document}
)endif
