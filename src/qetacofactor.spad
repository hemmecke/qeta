-------------------------------------------------------------------
---
--- FriCAS QEta
--- Copyright (C) 2018, 2020  Ralf Hemmecke <ralf@hemmecke.org>
---
-------------------------------------------------------------------
-- This program is free software: you can redistribute it and/or modify
-- it under the terms of the GNU General Public License as published by
-- the Free Software Foundation, either version 3 of the License, or
-- (at your option) any later version.
--
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
-- GNU General Public License for more details.
--
-- You should have received a copy of the GNU General Public License
-- along with this program.  If not, see <http://www.gnu.org/licenses/>.
-------------------------------------------------------------------

OF==>OutputForm
dbgPrint(x,y) ==> print(([":> "::Symbol::OF, x::Symbol::OF, y::OF]$List(OF)::OF))
--dbgPrint(x,y) ==> display(([":> "::Symbol::OF, x::Symbol::OF, y::OF]$List(OF)::OF)::Formatter(Format1D))
xdbgPrint(x,y) ==> rhx:="42"

)if LiterateDoc
\documentclass{article}
\usepackage{qeta}
\usepackage{showlabels}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\begin{document}
\title{Expanding Dedekind Eta Functions into $q$-series}
\author{Ralf Hemmecke}
\date{26-Jan-2018}
\maketitle
\begin{abstract}
  The package \code{QEtaCoFactor} implements functions to help turning
  a sifted eta quotient into a modular function by finding an
  appropriate cofactor.
\end{abstract}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\tableofcontents

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Problem}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

We try to find all missing parts to find a Ramanujan-Kolberg relation
as defined in \cite[p.~227]{Radu:RamanujanKolberg:2015}.

Assume that we are given a series
\begin{gather*}
  \sum_{n=0}^\infty a(n) q^n
  =
  \prod_{\delta|M}\prod_{n=0}^\infty(1-q^{\delta n})^{r_\delta}
\end{gather*}
and want to express $\sum_{n=0}^\infty a(mn+t) q^n$ where $m$ and $M$
are a positive integers with $m|M$, $r\in R(M)$, and
$t\in\Set{0,\ldots,m-1}$ as a $\setQ$-linear combination of
eta-quotients and (maybe) an appropriate power of
$q^{1/24}=\unityPower{\frac{\tau}{24}}$.

For mathematical background see \PathName{qetafun.spad}.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Implementation}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Let us start with a few common macros.

These two technical macros are necessary to distinguish between Rep
and \%.
)endif

rep x ==> (x@%) pretend Rep
per x ==> (x@Rep) pretend %

)if LiterateDoc
Now some abbreviations for common domains.
)endif

P ==> PositiveInteger
N ==> NonNegativeInteger
Z ==> Integer

asN x ==> x pretend N
asP x ==> x pretend P

VZ ==> Vector Z
MZ ==> Matrix Z
Rec ==> Record(red: SL2Z, lg: N)
EXGCD ==> Record(coef1: Z, coef2: Z, generator: Z)
LP ==> List P
LZ ==> List Z
QAuxMEQ ==> QAuxiliaryModularEtaQuotientPackage

INTF ==> IntegerNumberTheoryFunctions
DIVISORS m ==>  [asP d for d in divisors(m)$INTF]

-- Note that in the following correspondence between programming variables
-- and notation in the mathematical description in qetafun.spad.
-- Variable   <--> mathematical notation
-- m               m
-- mm              M
-- nn              N
-- sigmaInfty      \sumdelta{r}
-- kappa(m)        \kappa

)abbrev package QETACOF QEtaCoFactor
QEtaCoFactor: Exports == Implementation where
  Exports ==> with
    conditionPrimeDivisors?: (P, P) -> Boolean
      ++ conditionExponentSum?(nn, m) returns true if every prime
      ++ divisor of m is also a divisor of nn. This corresponds to
      ++ (28) in \cite{Radu:RamanujanKolberg:2015} and to
      ++ \eqref{eq:p|m=>p|N} in qetafun.spad.

    conditionMmN?: (P, P, LZ, P) -> Boolean
      ++ conditionMmN(nn, mm, s, m) returns true if every divisor d of
      ++ mm (for which s_d is non-zero) is also a devisor of m*N. This
      ++ corresponds to condition (4.7) in \cite{Radu:PhD:2010} and in
      ++ equation \eqref{eq:delta|M=>delta|mN} in qetafun.spad.

    conditionSigma0?: (P, P, LZ, P) -> Boolean
      ++ conditionSigma0?(nn, mm, s, m) returns true if
      ++ (kappa(m)*m*nn^2/mm)*sigma0(nn, r) = 0 mod 24. This
      ++ corresponds to \eqref{eq:sv24} in qetafun.spad and to the
      ++ first condition on page~241 of
      ++ \cite{Radu:RamanujanKolberg:2015} (Definition 35).

    conditionSumExponents?: (P, P, LZ, P) -> Boolean
      ++ conditionSumExponents?(nn, mm, s, m) returns true if
      ++ the sum of the entries of s multiplied by kappa(m)*nn is
      ++ divisible by 8.
      ++ This corresponds to \eqref{eq:sum-s} in qetafun.spad
      ++ second condition on page~241 of
      ++ \cite{Radu:RamanujanKolberg:2015} (Definition 35).

    conditionSigmaInfinity?: (P, P, LZ, P, N) -> Boolean
      ++ conditionSigmaInfinity?(nn, mm, s, m, t) returns true if the
      ++ expression
      ++ (24*m)/gcd(kappa(m)*(-24*t-sigmaInfinity(mm,s)),24*m) is a
      ++ divisor of nn. This corresponds to \eqref{eq:w|N} in
      ++ qetafun.spad third condition on page~241 of
      ++ \cite{Radu:RamanujanKolberg:2015} (Definition 35).

    conditionEvenMultiplier?: (P, P, LZ, P) -> Boolean
      ++ conditionEvenMultiplier?(nn, mm, s, m) returns true if
      ++ various divisibility conditions are fulfilled.
      ++ This corresponds to \eqref{eq:even-m} in
      ++ qetafun.spad fourth condition on page~241 of
      ++ \cite{Radu:RamanujanKolberg:2015} (Definition 35).

    conditionCoEtaQuotientGamma0?: (P, P, LZ, P, N) -> Boolean
      ++ conditionCoEtaQuotientGamma0?(nn, mm, s, m, t) returns
      ++ true iff all the conditions for the parameters are fulfilled.
      ++ This checks whether (nn, mm, s, m, t) is in \Delta^* as
      ++ defined in
      ++ Definition~\ref{def:condition-co-eta-quotient-gamma0} in
      ++ qetafun.spad and in Definition 35 of
      ++ \cite{Radu:RamanujanKolberg:2015}

    minimalLevelCoEtaQuotientGamma0: (P, LZ, P, N) -> P
      ++ minimalLevelCoEtaQuotientGamma0(mm, s, m, t) returns the
      ++ smallest nn such that conditionCoEtaQuotientGamma0?(nn, mm,
      ++ s, m, t) is true, i.e. that (nn, mm, s, m, t) is an element
      ++ of \Delta^* as defined in
      ++ Definition~\ref{def:condition-co-eta-quotient-gamma0}.

    modularConditions?: (P, LZ, P, LZ, P, N) -> Boolean
      ++ modularConditions?(nn, r, mm, s, m, t) returns true iff all
      ++ the conditions of Theorem~\ref{thm:RaduConditions} are
      ++ fulfilled.
      ++ Compare with rStarConditions from QAuxiliaryModularEtaQuotientPackage.

    orbit: (Z, P, N) -> List N
      ++ orbit(sigmaInfty, m, t) computes the elements of
      ++ $\modularOrbit{s,m,t)$ as defined in qetafun.spad,
      ++ \cite[Def.~42]{Radu:RamanujanKolberg:2015} and
      ++ \cite[Lemma 4.35]{Radu:PhD:2010}.
      ++ We assume $sigmaInfty = \sum_{\delta|M} \delta s_\delta$.
    orbit: (P, List Z, P, N) -> List N
      ++ orbit(mm, s, m, t) computes the elements of
      ++ $\modularOrbit{s,m,t)$ as defined in qetafun.spad,
      ++ \cite[Def.~42]{Radu:RamanujanKolberg:2015} and
      ++ \cite[Lemma 4.35]{Radu:PhD:2010}.
      ++ orbit(s, m, t) returns the same as
      ++ orbit(sigmaInfinity(mm, s), m, t).
      ++ We assume $sigmaInfty = \sum_{\delta|M} \delta s_\delta$.
    chiExponent: (Z, P, List N) -> Z
      ++ chiExponent(sigmaInfty, m, orb) computes e such that
      ++ $\exp(2\pi i e / 24) = \chi_{s,m,t)$.
      ++ We assume $sigmaInfty = \sum_{\delta|mm} \delta s_\delta$.
      ++ See Lemma \ref{thm:chi-exponent}.
      ++ Furthermore, we assume that orb = orbit(sigmaInfty, m, t).
    etaCoFactorSpace: (P, P, List Z, P, N) -> Record(particular: Union(VZ, "failed"), basis: List VZ) -- List Z
      ++ etaCoFactorSpace(nn, mm, s, m, t) returns a vector v and the
      ++ basis of a space such that #v=#divisors(nn) and
      ++ modularConditions?(nn, members r, mm, s, m, t) is true for any
      ++ r = v + reduce(_+, [z.i * basis.i for i in 1..#basis])
      ++ and any sufficiently long list z of integers.
      ++ The function fails, if there is no such solution.
    sigmaInfinity: (P, List Z) -> Z
      ++ sigmaInfinity(mm, r) returns \sum_{delta|mm} delta*r_delta where
      ++ it is assumed that the entries of r correspond to the divisors
      ++ of mm.
      ++ It corresponds to \eqref{eq:sumdelta} in qetafun.spad.
    sigma0: (P, List Z) -> Z
      ++ sigma0(mm, r) returns \sum_{delta|mm} (mm/delta)*r_delta where
      ++ it is assumed that the entries of r correspond to the divisors
      ++ of mm.
      ++ It corresponds to \eqref{eq:sumdelta} in qetafun.spad.
    alphaInfinity: (P, LZ, P, LZ, P, N) -> Z
      ++ If modularConditions?(nn, r, mm, s, m, t) then
      ++ alphaInfinity(nn, r, mm, s, m, t) computes the order of the
      ++ q-expansion of $F_{r, s, m, t}$ as defined in qetafun.spad,
      ++ see~\eqref{eq:alphaInfinity}. It is the same as the \alpha as
      ++ defined in the abstract of \cite{Radu:RamanujanKolberg:2015},
      ++ DOI=10.1016/j.jsc.2017.02.001,
      ++ http://www.risc.jku.at/publications/download/risc_5338/DancingSambaRamanujan.pdf.


  Implementation ==> add
    fUnion ==> Union("nil", "sqfr", "irred", "prime")
    FL ==> List Record(flag: fUnion, factor: Z, exponent: N)
    DIVISIBLEBY?(k, z) ==> zero? positiveRemainder(z, k)

    -- auxiliary (local) functions

    -- kappa: P -> P
    --   ++ kappa(m) computes gcd(1-m^2,24).
    kappa(m: P): P == -- gcd(1-m^2, 24)
        ++ \cite[Def.~35]{Radu:RamanujanKolberg:2015}
        (even? m => 1; 8) * (DIVISIBLEBY?(3, m) => 1; 3)

    odotBar(a2: Z, sigmaInfty: Z, m: P, t: N): N ==
        --assert(one? positiveRemainder(a2, 24))
        -- \cite[Def.~41]{Radu:RamanujanKolberg:2015}
        z: Z := positiveRemainder(t*a2+(((a2-1) exquo 24)::Z)*sigmaInfty, m)
        asN z

    -- Return true iff \eqref{eq:Radu-productsquare} holds.
    checkSquare?(nn: P, mm: P, r: LZ, s: LZ, m: P, orbitSize: Z): Boolean ==
        nndivs: LP := DIVISORS nn
        mmdivs: LP := DIVISORS mm
        fl: FL := factorList factor nn
        -- By \eqref{eq:p|m=>p|N} and \eqref{eq:delta|M=>delta|mN}, it is
        -- enough to consider all prime divisors of N.
        for fe in fl repeat -- do for all primes dividing nn
            p: P := asP(fe.factor) -- extract prime factor
            -- Determine whether an even power of p divides
            -- \prod_{\delta|N}\delta^rdelta.
            e1: Z := 0
            for delta in rest nndivs for rdelta in rest r|odd? rdelta repeat
                e1 := e1 + primePower(delta, p)$QAuxMEQ
            e2: Z := 0
            if odd? orbitSize then
                -- check 2nd prod in eq:Radu-productsquare
                em: N := primePower(m, p)
                -- for all divisors of mm > 1
                if odd? first s then e2 := em -- case: delta=1
                for d in rest mmdivs for sdelta in rest s | odd? sdelta repeat
                    -- mod 2, only odd sdelta is relevant.
                    e2 := e2 + em + primePower(d, p)
            if odd?(e1 + e2) then return false
        return true

    -- exported functions

    -- The following function computes \modularOrbit{s, m, t} with the
    -- optimization given by Radu in \cite[Lemma 4.35]{Radu:PhD:2010},
    -- i.e., we do not need to check all entries of \setS_{24m}, but
    -- only those that are not equivalent mod w.
    orbit(sigmaInfty: Z, m: P, t: N): List N ==
        m24: Z := 24*m
        g := gcd(kappa(m)*(24*t + sigmaInfty), m24)
        w: P := asP((m24 exquo g)::Z)
        result: List N := empty()
        for a in 0..w-1 | one? gcd(a, w) repeat
            y := a
            while y < m24 and not one? gcd(y, m24) repeat y := y + w
            -- Note that m24=g*w and gcd(a + k*w, m24) =
            -- gcd(a+k*w,g*w) = 1 for k chosen to be the product of
            -- all prime divisors of g that do not divide a and do not
            -- divide w. In other words, the above while loop also
            -- terminates without the "y < m24" condition.
            e := odotBar(positiveRemainder(y^2, m24), sigmaInfty, m, t)
            if not member?(e, result) then result := cons(e, result)
        result

    -- local function
    sigmaAux(divs: List P, r: List Z): Z ==
        z: Z := 0
        for d in divs for rd in r repeat z := z + d*rd
        return z

    sigmaInfinity(nn: P, r: List Z): Z == sigmaAux(DIVISORS nn, r)
    sigma0(nn: P, r: List Z): Z == sigmaAux(reverse! DIVISORS nn, r)

    orbit(mm: P, s: List Z, m: P, t: N): List N ==
        orbit(sigmaInfinity(mm, s), m, t)

    chiExponent(sigmaInfty: Z, m: P, orb: List N): Z ==
        x: Z := 24*reduce(_+, orb, 0) + sigmaInfty * #orb
        positiveRemainder((1-m^2)*((x exquo m)::Z), 24)

    etaCoFactorSpace(nn: P, mm: P, s: List Z, m: P, t: N): _
      Record(particular: Union(VZ, "failed"), basis: List VZ) ==
        -- Setup matrix. Then solve inhomogeneous system.
        mat: MZ := rStarConditionMatrix(nn)$QAuxMEQ
        nr: N := nrows mat
        divs: List Z := divisors(mm)$IntegerNumberTheoryFunctions
        d: VZ := vector divs
        n: N := # divs
        w: Z := 0
        sigmaInfty: Z := 0
        sigma0: Z := 0
        for j in 1..n for sdelta in s repeat
            w := w + sdelta
            sigmaInfty := sigmaInfty + d.j * sdelta
            sigma0 := sigma0 + d(n+1-j) * sdelta
        -- Compute \modularOrbit{s, m,t}.
        orb: List N := orbit(sigmaInfty, m, t)
        orbitSize: Z := # orb
        nu: Z := chiExponent(sigmaInfty, m, orb)
        v: Vector Z := new(nr, 0)
        v.1 := - orbitSize * w                   -- \ref{eq:Radu-sum=0}
        v.2 := - nu - orbitSize * m * sigmaInfty -- \ref{eq:Radu-sigmainfinity}
        x: Z := orbitSize * m * nn * sigma0
        v.3 := - ((x exquo mm)::Z)               -- \ref{eq:Radu-sigma0}
        fl: FL := factorList factor nn
        -- Determine the cofactor according to (37) of
        -- \cite{Radu:RamanujanKolberg:2015} for each prime separately.
        -- Or see \ref{eq:Radu-productsquare} in qetafun.spad.
        if even? orbitSize then
            for i in 1..#fl for fe in fl repeat qsetelt!(v, i+3, 0)
        else
            for i in 1..#fl for fe in fl repeat
                p: P := asP(fe.factor) -- extract prime factor
                em: N := primePower(m, p)$QAuxMEQ
                -- for all divisors of mm > 1
                e: Z := if odd? first s then em else 0 -- case: delta=1
                for j in 2..n for sdelta in rest s | odd? sdelta repeat
                    -- Mod 2 only odd sdelta is relevant.
                    e := e + em + primePower(asP(d.j), p)$QAuxMEQ
                qsetelt!(v, i+3, positiveRemainder(e, 2))
        -- Now we have a matrix mat and a right-hand side v and need to
        -- solve mat*x=v for x.
        diophantineSystem(mat, v)$SmithNormalForm(Z, VZ, VZ, MZ)

    conditionPrimeDivisors?(nn: P, m: P): Boolean ==
        fm: Factored Z := factor m
        mfl: FL := factorList fm
        nnprimes := [f.factor for f in factorList factor nn]
        for f in mfl repeat
--            dbgPrint("eq:p|m=>p|N mfl", mfl)
--            dbgPrint("eq:p|m=>p|N nnprimes", nnprimes)
            not member?(f.factor, nnprimes) => return false
        return true

    conditionMmN?(nn: P, mm: P, s: LZ, m: P): Boolean ==
        mmdivs: LP := DIVISORS mm
--        dbgPrint("size of s: mmdivs", mmdivs)
--        dbgPrint("size of s: s", s)
        #mmdivs ~= #s =>  false

        divs: LP := DIVISORS(m*nn)
        for delta in mmdivs for sdelta in s | not zero? sdelta repeat
--            dbgPrint("eq:delta|M=>delta|mN", mmdivs)
--            dbgPrint("eq:delta|M=>delta|mN", divs)
            not member?(delta, divs) => return false
        return true

    conditionSigma0?(nn: P, mm: P, s: LZ, m: P): Boolean ==
        sv: Z := sigma0(mm, s)
        x: Z := kappa(m)*m*nn*nn*sv
--        dbgPrint("eq:sv24 s", s)
--        dbgPrint("eq:sv24 mmdivs", mmdivs)
--        dbgPrint("eq:sv24 sv", sv)
--        dbgPrint("eq:sv24 kappa", kappam)
--        dbgPrint("eq:sv24 x", x exquo mm)
        DIVISIBLEBY?(24, (x exquo mm) :: Z)

    conditionSumExponents?(nn: P, mm: P, s: LZ, m: P): Boolean ==
        ssum: Z := 0
        for sdelta in s repeat ssum := ssum + sdelta
--        dbgPrint("eq:sum-s ssum", ssum)
--        dbgPrint("eq:sum-s kappa", kappam)
--        dbgPrint("eq:sum-s nn", nn)
        DIVISIBLEBY?(8, kappa(m)*nn*ssum)

    conditionSigmaInfinity?(nn: P, mm: P, s: LZ, m: P, t: N): Boolean ==
        shat: Z := sigmaInfinity(mm, s)
        w: Z := ((24*m) exquo gcd(kappa(m)*(24*t + shat), 24*m)) :: Z
--        dbgPrint("eq:w|N nn", nn)
--        dbgPrint("eq:w|N mm", mm)
--        dbgPrint("eq:w|N mmdivs", mmdivs)
--        dbgPrint("eq:w|N s", s)
--        dbgPrint("eq:w|N shat", shat)
--        dbgPrint("eq:w|N m", m)
--        dbgPrint("eq:w|N t", t)
--        dbgPrint("eq:w|N kappam", kappam)
--        dbgPrint("eq:w|N w", w)
        DIVISIBLEBY?(w, nn)

    conditionEvenMultiplier?(nn: P, mm: P, s: LZ, m: P): Boolean ==
        odd? m => true
        u: Z := 1
        for delta in DIVISORS mm for sdelta in s | not zero? sdelta repeat
            adelta: P := asP abs(sdelta)
            u := u * delta^adelta
        e: Z := 0
        while even? u repeat
            e := e + 1
            u := shift(u, -1)

        b1: Boolean := DIVISIBLEBY?(4, kappa(m)*nn) and DIVISIBLEBY?(8, nn*e)
        b2: Boolean := even? e and DIVISIBLEBY?(8, nn*(u-1))
--        if not (b1 or b2) then
--            dbgPrint("eq:even-m m", m)
--            dbgPrint("eq:even-m kappa", kappam)
--            dbgPrint("eq:even-m nn", nn)
--            dbgPrint("eq:even-m e", e)
--            dbgPrint("eq:even-m e", u)
        return (b1 or b2)

    conditionCoEtaQuotientGamma0?(nn: P, mm: P, s: LZ, m: P, t: N): Boolean ==
        t < m -- stupidity check
         and conditionPrimeDivisors?(nn, m) --\eqref{eq:p|m=>p|N}
          and conditionMmN?(nn, mm, s, m) --\eqref{eq:delta|M=>delta|mN}
           and conditionSigma0?(nn, mm, s, m) --\eqref{eq:sv24}
            and conditionSumExponents?(nn, mm, s, m) --\eqref{eq:sum-s}
             and conditionSigmaInfinity?(nn, mm, s, m, t) --\eqref{eq:w|N}
              and conditionEvenMultiplier?(nn, mm, s, m) --\eqref{eq:even-m}

    minimalLevelCoEtaQuotientGamma0(mm: P, s: LZ, m: P, t: N): P ==
        nn: P := 1
        while not conditionCoEtaQuotientGamma0?(nn, mm, s, m, t) repeat
            nn := nn + 1
        return nn

    modularConditions?(nn: P, r: LZ, mm: P, s: LZ, m: P, t: N): Boolean ==
        not conditionCoEtaQuotientGamma0?(nn, mm, s, m, t) => false

        nndivs: LP := DIVISORS nn
        #nndivs ~= #r =>
            dbgPrint("size of r: nndivs", nndivs)
            dbgPrint("size of r: r", r)
            false

        mmdivs: LP := DIVISORS mm
        shat: Z := 0 -- sigmaInfty
        for delta in mmdivs for sdelta in s repeat shat := shat + delta * sdelta
        orb: List N := orbit(shat, m, t)
        orbitSize: Z := # orb

        --\eqref{eq:Radu-sum=0}
        not zero?(reduce(_+, r, 0) + orbitSize * reduce(_+, s, 0)) =>
            dbgPrint("eq:Radu-sum=0 r", r)
            dbgPrint("eq:Radu-sum=0 s", s)
            dbgPrint("eq:Radu-sum=0 orbitSize", orbitSize)
            false

        --\eqref{eq:Radu-sigmainfinity}
        rhat: Z := 0
        for delta in nndivs for rdelta in r repeat rhat := rhat + delta * rdelta
        nu: Z := chiExponent(shat, m, orb)
        z: Z := rhat + orbitSize * m * shat + nu
        not DIVISIBLEBY?(24, z) =>
            dbgPrint("eq:Radu-sigmainfinity nndivs", nndivs)
            dbgPrint("eq:Radu-sigmainfinity r", r)
            dbgPrint("eq:Radu-sigmainfinity mmdivs", mmdivs)
            dbgPrint("eq:Radu-sigmainfinity s", s)
            dbgPrint("eq:Radu-sigmainfinity orbitSize", orbitSize)
            dbgPrint("eq:Radu-sigmainfinity nu", nu)
            false

        --\eqref{eq:Radu-sigma0}
        rv: Z := 0
        for delta in reverse nndivs for rdelta in r repeat
            rv := rv + delta * rdelta
        sv: Z := 0
        for delta in reverse mmdivs for sdelta in s repeat
            sv := sv + delta * sdelta
        f: Z := ((m * nn) exquo mm)::Z -- modularInputConditions \implies f \in Z
        not DIVISIBLEBY?(24, rv + orbitSize * f * sv) =>
            dbgPrint("eq:Radu-sigma0 nndivs", nndivs)
            dbgPrint("eq:Radu-sigma0 r", r)
            dbgPrint("eq:Radu-sigma0 mmdivs", mmdivs)
            dbgPrint("eq:Radu-sigma0 s", s)
            dbgPrint("eq:Radu-sigma0 orbitSize", orbitSize)
            false

        --\eqref{eq:Radu-productsquare}
        not checkSquare?(nn, mm, r, s, m, orbitSize) =>
            dbgPrint("eq:Radu-productsquare nn", nn)
            dbgPrint("eq:Radu-productsquare mm", mm)
            dbgPrint("eq:Radu-productsquare r", r)
            dbgPrint("eq:Radu-productsquare s", s)
            dbgPrint("eq:Radu-productsquare orbitSize", orbitSize)
            false

        true

    alphaInfinity(nn: P, r: LZ, mm: P, s: LZ, m: P, t: N): Z ==
        Q ==> Fraction Z
        not modularConditions?(nn, r, mm, s, m, t) =>
            error "input does not correspond to a modular function"
        rhat: Z := sigmaInfinity(nn, r)
        shat: Z := sigmaInfinity(mm, s)
        orb: List N := orbit(shat, m, t)
        alpha: Q := rhat/24 + reduce(_+, [24*k+shat for k in orb], 0)/(24*m)
        not one? denom alpha => error "alpha not an integer"
        return numer alpha


)if LiterateDoc
\bibliography{qeta}
\end{document}
)endif
