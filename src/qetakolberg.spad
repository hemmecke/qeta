-------------------------------------------------------------------
---
--- FriCAS QEta
--- Copyright (C) 2018  Ralf Hemmecke <ralf@hemmecke.org>
---
-------------------------------------------------------------------
-- This program is free software: you can redistribute it and/or modify
-- it under the terms of the GNU General Public License as published by
-- the Free Software Foundation, either version 3 of the License, or
-- (at your option) any later version.
--
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
-- GNU General Public License for more details.
--
-- You should have received a copy of the GNU General Public License
-- along with this program.  If not, see <http://www.gnu.org/licenses/>.
-------------------------------------------------------------------

OF==>OutputForm
dbgPrint(x,y) ==> print(([":> "::Symbol::OF, x::Symbol::OF, y::OF]$List(OF)::OF))
--dbgPrint(x,y) ==> display(([":> "::Symbol::OF, x::Symbol::OF, y::OF]$List(OF)::OF)::LinearOutputFormat, 770)
xdbgPrint(x,y) ==> rhx:="42"

)if LiterateDoc
\documentclass{article}
\usepackage{qeta}
\usepackage{showlabels}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\begin{document}
\title{Expanding Dedekind Eta Functions into $q$-series}
\author{Ralf Hemmecke}
\date{26-Jan-2018}
\maketitle
\begin{abstract}

  Find a cofactor to turn a function related to a $q$-series into a
  modular function.

\end{abstract}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\tableofcontents

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Problem}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

We try to find all missing parts to find a Ramanujan-Kolberg relation
as defined in \cite[p.~227]{Radu:RamanujanKolberg:2015}.

Assume that we are given a series
\begin{gather*}
  \sum_{n=0}^\infty a(n) q^n
  =
  \prod_{\delta|M}\prod_{n=0}^\infty(1-q^{\delta n})^{r_\delta}
\end{gather*}
and want to express $\sum_{n=0}^\infty a(mn+t) q^n$ where $m$ and $M$
are a positive integers with $m|M$, $r\in R(M)$, and
$t\in\Set{0,\ldots,m-1}$ as a $\setQ$-linear combination of
eta-quotients and (maybe) an appropriate power of
$q^{1/24}=\unityPower{\frac{\tau}{24}}$.

For mathematical background see \PathName{qetafun.spad}.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Implementation}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Let us start with a few common macros.

These two technical macros are necessary to distinguish between Rep
and \%.
)endif

rep x ==> (x@%) pretend Rep
per x ==> (x@Rep) pretend %

)if LiterateDoc
Now some abbreviations for common domains.
)endif

P ==> PositiveInteger
N ==> NonNegativeInteger
Z ==> Integer
VZ ==> Vector Z
MZ ==> Matrix Z
Rec ==> Record(red: SL2Z, lg: N)
EXGCD ==> Record(coef1: Z, coef2: Z, generator: Z)

-- Note that in the following correspondence between programming variables
-- and notation in the mathematical description in qetafun.spad.
-- Variable   <--> mathematical notation
-- m               m
-- mm              M
-- nn              N
-- sigmaInfty      \sumdelta{r}
-- kappa(m)        \kappa

)abbrev package QETAKOL QEtaKolberg
QEtaKolberg: Exports == Implementation where
  Exports ==> with
    orbitRadu: (Z, P, N) -> List N
    orbit: (Z, P, N) -> List N
      ++ orbit(sigmaInfty, m, t) computes the elements of
      ++ $P_{s,m,t)$ as defined in qetafun.spad,
      ++ \cite[Def.~42]{Radu:RamanujanKolberg:2015} and
      ++ \cite[Lemma 4.35]{Radu:PhD:2010}.
      ++ We assume $sigmaInfty = \sum_{\delta|M} \delta s_\delta$.
    chiExponent: (Z, P, List N) -> Z
      ++ chiExponent(sigmaInfty, m, orb) computes e such that
      ++ $\exp(2\pi i e / 24) = \chi_{s,m,t)$.
      ++ We assume $sigmaInfty = \sum_{\delta|mm} \delta s_\delta$.
      ++ See Lemma \ref{thm:chi-exponent}.
      ++ Furthermore, we assume that orb = orbit(sigmaInfty, m, t).
    etaCoFactor: (P, P, P, N, List Z) -> Record(particular : Union(VZ, "failed"), basis : List VZ) -- List Z
      ++ etaCoFactor(m, mm, nn, t, s) returns a list r such that
      ++ #r=#divisors(nn) and (m, mm, nn, t, r, s) the conditions in
      ++ Theorem~\ref{thm:RaduConditions}.

)if TODO
--    splitMatrix: (MZ, Z, Z, N) -> Rec
      ++ splitMatrix(gamma, delta, m, lambda) returns a record rec that
      ++ contains two matrices rec.red=m1 and rec.triang=m2 and a value lg
      ++ such that for gamma = matrix [[a, b], [c, d]] and
      ++ W=matrix [[delta(a+ct), delta(b+dt)],[cm, dm]] it holds
      ++ W=m1*m2 according to \cite[Lemma~4.8]{Radu:PhD:2010}, but
      ++ in a variation as described in qetafun.spad, i.e., such that
      ++ m1 is given by eq:W'_delta-m-lambda, m2 by eq:W''_delta-m-lambda
      ++ and lg by eq:lambda_gamma.
      ++ Furthermore, 0<=lg<m and m2(1,2)=delta*lg
    lambdaGamma: (P, SL2Z, N) -> N
      ++ lambdaGamma(m, gamma, lambda) returns the transformed lambda
      ++ according to gamma. See eq:lambda_gamma in qetafun.spad.
    sigmaInfinity: (N, List Z) -> Z
      ++ sigmaInfinity(mm, r) returns \sum_{delta|mm} delta*r_delta where
      ++ it is assumed that the entries of r correspond to the divisors
      ++ of mm.
      ++ It corresponds to \eqref{eq:sumdelta} in qetafun.spad.
    sigmaZero: (N, List Z) -> Z
      ++ sigmaZero(mm, r) returns \sum_{delta|mm} (mm/delta)*r_delta where
      ++ it is assumed that the entries of r correspond to the divisors
      ++ of mm.
)endif

  Implementation ==> add

    -- auxiliary (local) functions

    -- kappa: P -> P
    --   ++ kappa(m) computes gcd(1-m^2,24).
    kappa(m: P): P == -- gcd(1-m^2, 24)
        ++ \cite[Def.~35]{Radu:RamanujanKolberg:2015}
        (even? m => 1; 8) * (zero? positiveRemainder(m, 3) => 1; 3)

    odotBar(a2: Z, sigmaInfty: Z, m: P, t: N): N ==
        --assert(one? positiveRemainder(a2, 24))
        -- \cite[Def.~41]{Radu:RamanujanKolberg:2015}
        z: Z := positiveRemainder(t*a2+(((a2-1) exquo 24)::Z)*sigmaInfty, m)
        qcoerce(z)@N

    orbitRadu(sigmaInfty: Z, m: P, t: N): List N ==
        g := gcd(kappa(m)*(24*t + sigmaInfty), 24*m)
        dbgPrint("g", g)
        w: P := qcoerce((24*m exquo g)::Z)@P
        dbgPrint("w", w)
        sw: List Z := empty()
        for a in 0..w-1 | one? gcd(a, w) repeat
            a2 := positiveRemainder(a^2, w)
            dbgPrint("a", a)
            dbgPrint("a2", a2)
            if not member?(a2, sw) then sw := cons(a2, sw)
        dbgPrint("sw", sw)
        s: List Z := empty()
        -- There exists k \in \setN such that a^2+k*w = 1 mod 24
        --choose a representative (mod w) that is divisible by 24
        for a2 in sw repeat
            u: Z := a2
            dbgPrint("u00000", u)
            while not one? positiveRemainder(u, 24) repeat
                u := u + w
                dbgPrint("u", u)
            s := cons(u, s)
        sort [odotBar(a2, sigmaInfty, m, t) for a2 in s]

    chiExponent(sigmaInfty: Z, m: P, orb: List N): Z ==
        x: Z := 24*reduce(_+, orb, 0) + sigmaInfty * #orb
        positiveRemainder((1-m^2)*((x exquo m)::Z), 24)

    -- primePower(d, p) returns e such that gcd(d/p^e, p)=1.
    primePower(d: P, p: P): N ==
        e: N := 0;
        x: Z := d
        while not one? gcd(x, p) repeat (x := (x exquo p)::Z; e:=e+1)
        return e

    etaCoFactor(m: P, mm: P, nn: P, t: N, s: List Z): _
      Record(particular : Union(VZ, "failed"), basis : List VZ) ==
        -- Setup matrix. Then solve inhomogeneous system.
        QEQMEVS ==> QEtaQuotientMonoidExponentVectorsStar
        mat: MZ := rStarConditionMatrix(nn)$QEQMEVS
        nr: N := nrows mat
        divs: List Z := divisors(mm)$IntegerNumberTheoryFunctions
        d: VZ := vector divs
        dbgPrint("d", d)
        n: N := # divs
        dbgPrint("n", n)
        w: Z := 0
        sigmaInfty: Z := 0
        sigma0: Z := 0
        for j in 1..n for sdelta in s repeat
            w := w + sdelta
            sigmaInfty := sigmaInfty + d.j * sdelta
            sigma0 := sigma0 + d(n+1-j) * sdelta
        -- Compute P_{m,r}(t).
        orb: List N := orbit(sigmaInfty, m, t)
        dbgPrint("orb", orb)
        orbitSize: Z := # orb
        nu: Z := chiExponent(sigmaInfty, m, orb)
        dbgPrint("nu", nu)

        v: Vector Z := new(nr, 0)
        v.1 := - orbitSize * w                   -- \ref{eq:Radu-sum=0}
        dbgPrint("v1", v.1)
        v.2 := - nu - orbitSize * m * sigmaInfty -- \ref{eq:Radu-sigmainfinity}
        dbgPrint("v2", v.2)
        x: Z := orbitSize * m * nn * sigma0
        v.3 := - ((x exquo mm)::Z)               -- \ref{eq:Radu-sigma0}
        dbgPrint("v3", v.3)
        fl: List Record(factor: Z, exponent: N) := factors factor nn
        -- Determine the cofactor according to (37) of
        -- \cite{Radu:RamanujanKolberg:2015} for each prime separately.
        -- Or see \ref{eq:Radu-productsquare} in qetafun.spad.
        if even? orbitSize then
            for i in 1..#fl for fe in fl repeat qsetelt!(v, i+3, 0)
        else
            for i in 1..#fl for fe in fl repeat
                dbgPrint("i", i)
                p: P := qcoerce(fe.factor)@P -- extract prime factor
                dbgPrint("p", p)
                em: N := primePower(m, p)
                -- for all divisors of mm > 1
                e: Z := if odd? first s then em else 0 -- case: delta=1
                for j in 2..n for sdelta in rest s | odd? sdelta repeat
                    -- Mod 2 only odd sdelta is relevant.
                    e := em + primePower(qcoerce(d.j)@P, p)
                dbgPrint("i+3", i+3)
                dbgPrint("e", e)
                qsetelt!(v, i+3, e)
                dbgPrint("v", v)
        -- Now we have a matrix mat and a right-hand side v and need to
        -- solve mat*x=v for x.
        dbgPrint("v", v)
        dbgPrint("mat", mat)
        diophantineSystem(mat, v)$SmithNormalForm(Z, VZ, VZ, MZ)

)if TODO
     splitMatrix(gamma: MZ, delta: Z, m: Z, t: Z): Rec ==
        LLZ ==> List List Z
        --assert(delta>0)
        --assert(m>0)
        --assert(one? determinat(gamma))
        --assert(zero? positiveRemainder(c*m, delta))

        a: Z := gamma(1,1);   b: Z := gamma(1,2)
        c: Z := gamma(2,1);   d: Z := gamma(2,2)

        act: Z := a + c*t;    bdt := b + d*t

        -- matrix W
        A: Z := delta * act;  B: Z := delta * bdt
        C: Z := c*m;          D: Z := d*m

        exgcd: EXGCD := extendedEuclidean(act, C)
        g: Z := exgcd.generator
        --assert(one? g) -- sanity check
        if not one? g then error "splitMatrix: gcd is not 1"

        a1: Z := act
        c1: Z := (C exquo delta) :: Z -- cm/delta

        d1i: Z    :=  exgcd.coef1 -- initial value for d1
        bstari: Z := -exgcd.coef2 -- initial value for bstar

        -- Now try to find s such that 0 <= bdt*d1 - D*b1 - s*d2 < d2.
        tgammai: Z := bdt*d1i - D*bstari -- initial value
        tgamma: Z := positiveRemainder(tgammai, m) -- = t_\gamma
        s: Z := ((tgammai - tgamma) exquo m) :: Z
        b1: Z := delta * (bstari + act * s)
        d1: Z := d1i + delta * s * c1

        m1: MZ := matrix([[a1, b1], [c1, d1]]$LLZ)
        m2: MZ := matrix([[delta, delta * tgamma], [0, m]]$LLZ)
        [m1, m2, tgamma]$Rec

    lambdaGamma(m: P, gamma: SL2Z, lambda: N): N == 1




)endif

    -- exported functions

    -- The following function computes P_{m,r,t} with the optimization
    -- given by Radu in \cite[Lemma 4.35]{Radu:PhD:2010}, i.e., we do not
    -- need to check all entries of \setS_{24m}, but only those that are
    -- not equivalent mod w.
    orbit(sigmaInfty: Z, m: P, t: N): List N ==
        m24: Z := 24*m
        g := gcd(kappa(m)*(24*t + sigmaInfty), m24)
        w: P := qcoerce((m24 exquo g)::Z)@P
        result: List N := empty()
        for a in 0..w-1 | one? gcd(a, w) repeat
            y := a
            while y < m24 and not one? gcd(y, m24) repeat y := y + w
            -- Not the following "if y < m24 then" condition is unnecessary.
            -- Note that gcd(a + k*w, m24)=gcd(a+k*w,g*w)=1 for
            -- k chosen to be the product of all prime divisors of g
            -- that do not divide i and do not divide w.
            e := odotBar(positiveRemainder(y^2, m24), sigmaInfty, m, t)
            if not member?(e, result) then result := cons(e, result)
        result


)if LiterateDoc
\bibliography{qeta}
\end{document}
)endif

)if false


    relationRamanujanKolberg(level: P, m: P, t: N, r: RVector):
--        Record(s: RVector, t: List(N), rhs: MonoidRing(C, MRVector))
        List N
      ==
        empty()

    -- See \cite[Thm.~45]{Radu:RamanujanKolberg:2015}.
    -- m = m, t=t, M=ilevel, N=level
    coFactorConditions(level: P, ilevel: P, m: P, t: Z, r: List Z): MZ ==
        --assert(#r=#divisors(ilevel))
        --assert(zero? postiveRemainder(level, ilevel))
        -- DeltaStar-Conditions
        -- \cite[Def.~34]{Radu:RamanujanKolberg:2015} must be fulfilled.
        p := orbit(level, sigmainfty, m, t, r)
        orbitSize := #p
        divs: List P := [qcoerce(d)@P for d in divisors(level)]
        k: P := kappa m
        c1 := orbitSize * reduce(_+, r)
        c2 := chiExponent(m, r, orb)
)endif
