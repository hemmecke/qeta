-- -*- coding: utf-8 -*-
-- $$\newcommand{\qPochhammer}[3][\infty]{\left( #2;#3 \right)_{#1}}$$
-- Via jupytext this file can be shown as a jupyter notebook.

)read /home/hemmecke/g/qeta/input/jfricas-test-support.input )quiet

-- # Problem 12289 of American Mathematical Monthly

-- Problem by G.E. Andrews (USA) and M. Merca (Romania)
-- Prove
-- \begin{gather*}
-- \sum_{n=0}^\infty 2\cos\left(\frac{(2n+1)\pi}{3}\right) q^{n(n+1)/2}
-- = \qPochhammer{q}{q} \qPochhammer{q,q^5}{q^6}
-- \end{gather*}
-- when $\lvert q \rvert < 1$.

-- # Proof

-- We denote the lefthand side by $S$ and split this sum into 3 parts.
-- \begin{align*}
-- S
-- &=
-- \underbrace{\sum_{n=0}^\infty q^{3n(3n+1)/2}}_{=: s(q^3)}
-- -
-- 2 \sum_{n=0}^\infty q^{(3n+1)(3n+2)/2}
-- +
-- \underbrace{\sum_{n=0}^\infty q^{(3n+2)(3n+3)/2}}_{=: t(q^3)}
-- \end{align*}
-- where
-- \begin{gather*}
-- s(q) = \sum_{n=0}^\infty q^{n(3n+1)/2},
-- \qquad
-- t(q) = \sum_{n=0}^\infty q^{(3n+2)(n+1)/2}.
-- \end{gather*}
--
-- It is easy to check that the middle sum is equal to $-2q\,s(q^3)$.

-- \begin{align*}
-- t(q)
-- &=
-- \sum_{n=0}^\infty q^{(3n+2)(n+1)/2}
-- = \sum_{n=0}^\infty q^{(3n+2)(n+1)/2}
-- = \sum_{n=1}^\infty q^{n(3n-1)/2}
-- = \sum_{n=-\infty}^{-1} q^{(-n)(3(-n)-1)/2}
-- = \sum_{n=-\infty}^{-1} q^{n(3(n+1)/2}.
-- \end{align*}
-- So we have
-- \begin{align*}
-- s(q^3) + t(q^3)
-- &=
-- \sum_{n=-\infty}^\infty q^{3n(3n+1)/2}.
-- \end{align*}

-- The Jacobi triple product is given by
-- \begin{align*}
-- \sum_{n=-\infty}^\infty q^{n(n+1)/2} z^n
-- & = \qPochhammer{q}{q} \qPochhammer{-\frac{1}{z}}{q} \qPochhammer{-zq}{q}
-- \end{align*}
-- (see last but one formula at https://en.wikipedia.org/wiki/Jacobi_triple_product#Properties).
--
-- Replace $q$ by $q^9$ and $z$ by $q^{-3}$, then
-- \begin{align*}
-- \sum_{n=-\infty}^\infty (q^9)^{n(n+1)/2} q^{-3n}
-- &=
-- \sum_{n=-\infty}^\infty q^{9 n(n+1)/2 - 3n}
-- = \sum_{n=-\infty}^\infty q^{(9 n^2 + 9n - 6n)/2}
-- = \sum_{n=-\infty}^\infty q^{3n(3n+1)/2}
-- \\
-- &= s(q^3) + t(q^3)
-- \\
-- &= \qPochhammer{q^9}{q^9}
--    \qPochhammer{-\frac{1}{q^{-3}}}{q^9}
--    \qPochhammer{-q^{-3}q^9}{q^9}
--  = \qPochhammer{q^9}{q^9}
--    \qPochhammer{-q^3}{q^9}
--    \qPochhammer{-q^6}{q^9}
-- \\
-- &= \qPochhammer{q^9}{q^9}
--    \qPochhammer{-q^3}{q^9}
--    \qPochhammer{-q^6}{q^9}
-- \\
-- &= \qPochhammer{q^9}{q^9}
--    \frac{\qPochhammer{q^6}{q^{18}}}{\qPochhammer{q^3}{q^9}}
--    \frac{\qPochhammer{q^{12}}{q^{18}}}{\qPochhammer{q^6}{q^9}}
-- \\
-- &= \frac{\qPochhammer{q^9}{q^9}
--          \qPochhammer{q^6,q^{12}}{q^{18}}}
--         {\qPochhammer{q^3,q^6}{q^9}}
-- \end{align*}
--
-- Note that for $a=1$ or $a=2$ we have:
-- \begin{align*}
-- \qPochhammer{-q^a}{q^3}
-- &= \prod_{n=0}^\infty (1+q^{3n+a})
-- = \prod_{n=0}^\infty \frac{(1-q^{6n+2a})}{(1-q^{3n+a})}
-- = \frac{\qPochhammer{q^{2a}}{q^6}}{\qPochhammer{q^a}{q^3}}.
-- \end{align*}
--

-- Let us investigate $s(q)$.
--
-- \begin{align*}
-- 2 s(q)
-- &= 2 \sum_{n=0}^\infty q^{3n(n+1)/2}
-- = \sum_{n=1}^\infty q^{3(-n)((-n+1)/2} + \sum_{n=0}^\infty q^{3n(n+1)/2}
-- = \sum_{n=-\infty}^{-1} q^{3n(n+1)/2} + \sum_{n=0}^\infty q^{3n(n+1)/2}
-- \\
-- &= \sum_{n=-\infty}^\infty q^{3n(n+1)/2}
-- = q^{-3/8} \sum_{n=-\infty}^\infty q^{3(n^2 + n)/2 +3/8}
-- = q^{-3/8} \sum_{n=-\infty}^\infty (q^{3/2})^{(n+1/2)^2}
-- \\
-- &= q^{-3/8} \theta_2(q^{3/2})
-- \end{align*}

-- According to https://en.wikipedia.org/wiki/Theta_function#Relations_to_Dedekind_eta_function
-- we can write
-- \begin{align*}
-- 2 s(q)
-- &= q^{-3/8} \theta_2(q^{3/2})
-- = 2 q^{-3/8} \frac{\eta(6\tau)^2}{\eta(3\tau)}
-- = 2 \frac{\qPochhammer{q^6}{q^6}^2}{\qPochhammer{q^3}{q^3}}
-- \end{align*}
-- where $q=\exp(2\pi i \tau)$.

-- It is now only left to show that
-- \begin{align*}
-- S
-- &= s(q^3) + t(q^3) - 2 q s(q^3)
-- \\
-- &=
-- \frac{\qPochhammer{q^9}{q^9}
--          \qPochhammer{q^6,q^{12}}{q^{18}}}
--         {\qPochhammer{q^3,q^6}{q^9}}
-- - 2 q \frac{\qPochhammer{q^{18}}{q^{18}}^2}{\qPochhammer{q^9}{q^9}}
-- \\
-- &= \qPochhammer{q}{q} \qPochhammer{q,q^5}{q^6}
-- \end{align*}

-- or equivalently (by dividing the equation by the righthand side)
-- \begin{align*}
-- \frac{\qPochhammer{q^9}{q^9}
--          \qPochhammer{q^6,q^{12}}{q^{18}}}
--         {\qPochhammer{q}{q}
--          \qPochhammer{q,q^5}{q^6}
--          \qPochhammer{q^3,q^6}{q^9}}
-- - 2 q \frac{\qPochhammer{q^{18}}{q^{18}}^2}
--          {\qPochhammer{q}{q}
--           \qPochhammer{q^9}{q^9}
--          \qPochhammer{q,q^5}{q^6}}
-- &= 1
-- \end{align*}

-- Note that
-- \begin{gather*}
-- \qPochhammer{q,q^5}{q^6}
-- = \frac{\qPochhammer{q}{q}     \qPochhammer{q^6}{q^6}}
--        {\qPochhammer{q^2}{q^2} \qPochhammer{q^3}{q^3}}
-- \end{gather*}
-- [[1,2],[2,−1],[3,−1],[6,1]]

)set stream calc 80
--eulerExpansion specEQI(Z) eqSPEC [[1,2],[2,-1],[3,-1],[6,1]]
eulerExpansion specEQI(Z) geqSPEC [[1,1],[6,1,1]]

-- # Init

-- +
-------------------------------------------------------------------
--setup
-------------------------------------------------------------------

-- +
)set mess type off
)set mess time off
)set stream calc 2

C ==> Q
qIdentity ==> 1
expand ==> 2
nonModular ==> 4
asExpression ==> 8
withSubscript ==> 16

)r convenience )quiet
-- -

)set mess type on
)set mess time on

-- +
-------------------------------------------------------------------
--endsetup
-------------------------------------------------------------------
-- -

)set output algebra on
)set output formatted off

-- # Check the modular identity

-- +
-------------------------------------------------------------------
--test:verify12289
-------------------------------------------------------------------
-- -


-- The left-hand side of the problem equation.

rspec := geqSPEC [[1,1],[6,1,1]]

-- The two specifications for the generating functions
-- of $a(3n)$ and $a(3n+1)$.

rspec30 := geqSPEC [[1,-1],[2,1],[3,2],[6,-1]]
rspec31 := geqSPEC [[3,-1],[6,2]]

-- We form the respective eta-quotients of the
-- equation among (modular) eta-quotients that we
-- have derived above and that remains to verify.

rspec0 := dilate(rspec30,3)/rspec
rspec1 := dilate(rspec31,3)/rspec
modularGamma1? rspec0
modularGamma1? rspec1
printSPEC rspec0
printSPEC rspec1

-- Note that above, we have actually derived the following
-- specification instead of `rspec30` (which is derived below),
-- but they lead to the same series expansion.

spec0 := geqSPEC [[9,1],[9,3,-1],[18,6,1]]
spec := dilate(rspec30,3)/spec0
assertTrue(modularGamma1?(spec))
assertEquals(order(expansion specEQI(Z)(spec) - 1$L1(Z), 100), 100)

-- ## Must check at most 6 coefficient at expansion at infinity

nn := 18
spitzen := cusps(nn)$GAMMA1
y0 := specYM1GEQ rspec0;
y1 := specYM1GEQ rspec1;
g0 := qetaGradesEstimate y0
g1 := qetaGradesEstimate y1
d0 := reduce(+, [x for x in g0 | x>0])
d1 := reduce(+, [x for x in g1 | x>0])
l1 := (specM1A1(Z)(rspec0) - 2*specM1A1(Z)(rspec1) - 1$A1(Z))::L1(Z)
assertEquals(order(l1, d0+d1+1), d0+d1+1)

-- ## Compute series expansion at every cusp

-- First determine a coefficient field that can hold
-- any of the expansion coeffcients.

xiord := lcm(minimalRootOfUnity y0, minimalRootOfUnity y1)
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);

-- Obviously the eta-quotients have pole not only at
-- infinity.

qetaGrades specM1AnCX rspec0
qetaGrades specM1AnCX rspec1

)set stream calc 3
a0 := specM1AnCX(rspec0)
a1 := specM1AnCX(rspec1)
aa := a0 - 2*a1 - 1 $ An(CX)
assertTrue(zero?(aa))

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # Towards finding a proof for Problem 12289

-- +
-------------------------------------------------------------------
--test:problem12289-3disection
-------------------------------------------------------------------
-- -


-- We start by entering the right-hand side and look
-- at the $q$-series expansion.

rspec := geqSPEC [[1,1],[6,1,1]];
)set stream calc 100
s := eulerExpansion specEQI(C) rspec

-- Deceived by the $\frac{1}{3}$ in the problem specification,
-- we try a 3-disection of the series.

s0 := choose(3,0,s)
s1 := choose(3,1,s)
s2 := choose(3,2,s)

-- Interesting, all coefficients for exponents congruent to
-- 2 modulo 3 vanish and the coefficients for exponents congruent
-- to 1 modulo 3 seem to be -2.

-- Let us use QEta to do the 3-disection.

-- In level 6, the left-hand side generalized eta-product,
-- can be written as a proper eta-quotient.
-- We do a 1-disection of the generalized eta-quotient
-- and find a proper eta-quotient for it.

rspec := geqSPEC [[1,1],[6,1,1]];
nn := minLevel1(rspec, 3, 2)
idxs := etaFunctionIndices nn
id := findID1(nn, idxs, rspec, 1, 0, idxs);
-- pretty(id, qIdentity + asExpression + expand)
rhs printSPEC rspec = rhs printSPEC inv coSpecification id
rspec := inv coSpecification id
assertEquals(rspec, eqSPEC [[1, 2], [2, - 1], [3, - 1], [6, 1]])

-- ## (3n+2)-disection &mdash; Vanishing of coefficient for exponent congruent 2 mod 3

-- The following is no surprise since there is no integer
-- $n$ with $\frac{n(n+1)}{2} \equiv 2 \pmod{3}$.

id2 := findID0(nn, rspec, 3, 2);
pretty(id2, qIdentity + asExpression + expand + nonModular)
assertEquals(identityPolynomial id2, F)

-- ## (3n+1)-disection &mdash; Explain the $-2$ coefficients

id1 := findID0(nn, rspec, 3, 1);
pretty(id1, asExpression + expand + nonModular + qIdentity)
assertEquals(identityPolynomial id1, F+2)

-- ## 3n-disection

-- We can do the 3-disection in $\Gamma_0(6)$.

id1 := findID0(nn, rspec, 3, 0);
pretty(id1, asExpression + expand + nonModular + qIdentity)
assertEquals(identityPolynomial id1, F-1)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
