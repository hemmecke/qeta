-- -*- coding: utf-8 -*-
-- Via jupytext this file can be shown as a jupyter notebook.

)read /home/hemmecke/g/qeta/input/jfricas-test-support.input

-- # Problem formulation


-- This worksheet contains some formulas and computations from
-- the following article.
--
--     @InCollection{Frye+Garvan:AutomaticProofOfThetaFunctionIdentities:2019,
--       author =       {Jie Frye and Frank Garvan},
--       title =        {Automatic Proof of Theta-Function Identities},
--       booktitle =    {Elliptic Integrals, Elliptic Functions and Modular
--                       Forms in Quantum Field Theory},
--       publisher =    {Springer International Publishing},
--       address =      {Cham},
--       crossref =     {Bluemlein+Schneider+Paule:EllipticIntegrals:2019},
--       isbn =         {978-3-030-04480-0},
--       doi =          {10.1007/978-3-030-04480-0_10},
--       url =          {https://link.springer.com/chapter/10.1007/978-3-030-04480-0_10},
--       pages =        {195--258}
--     }

-- # Init

-- +
-------------------------------------------------------------------
--setup
-------------------------------------------------------------------

-- +
)set mess type off
)set mess time off
C ==> Q

GEP ==> generalizedEtaPower $ EQI(C)
GEQ ==> generalizedEtaQuotient $ EQI(C)
EQ ==> etaQuotient $ EQI(C)
EE ==> eulerExpansion $ EQI(C)
EX ==> expansion $ EQI(C)

QF ==> QFunctions(Q, L1 Q)
QFX ==> QFunctions(CX, L1 CX)
jf ==> jacobiFunction $ QF
ef ==> eulerFunction $ QF
gep ==> generalizedEulerProduct $ QF
one(n: Z): Q == 1

ygeq(nn,rbar) ==> (generalizedEtaQuotient $ YGEQ) generalizedEtaQuotientSpecification(nn,rbar)

L ==> L1 C
TX ==> T1 CX
LX ==> L1 CX
PX ==> QEtaPuiseuxSeries CX

LZ ==> List Z
LLZ ==> List LZ
GEQSPLIT ==> Record(pure: LZ, properGeneralized: LLZ)

PUIEX ==> puiseuxExpansion $ QESX(C, CX)
LAUEX ==> laurentExpansion $ QESX(C, CX)
LAUEXs ==> laurentExpansions $ QESX(C, CX)
LAUEX1 ==> laurentExpansionsGamma1 $ QESX(C, CX)

oo ==> infinity()$Cusp

--)set mess type on
--)set mess time on

-- +
-------------------------------------------------------------------
--endsetup
-------------------------------------------------------------------

-- +
-------------------------------------------------------------------
--test:eq8rel
-------------------------------------------------------------------
-- -

-- # Eta-Quotient relation in level 8

-- We have taken the following relation from the website
-- https://www.risc.jku.at/people/hemmecke/eta/etarelations/etarels8.input
-- and check whether it holds for all cusps of $\Gamma_1(8)$.
-- $$E_4^{12} - E_1^4 E_2^2 E_4^2 E_8^4 - 4 E_2^4 E_8^8=0$$
-- In order to check we turn the equation into an identity of
-- modular functions of $\Gamma_1(8)$ by dividing the whole
-- equation by $E_4^12$, i.e. we check
-- $$1 - E_1^4 E_2^2 E_4^{-10} E_8^4 - 4 E_2^4 E_4^{-12} E_8^8 = 0$$.

rbar1 := [];
rbar2 := [[1,4],[2,2],[4,-10],[8,4]];
rbar3 := [[2,4],[4,-12],[8,8]];

nn := mm := 8;
y1 := ygeq(mm, rbar1); xiord1 := minimalRootOfUnity(y1)::P;
y2 := ygeq(mm, rbar2); xiord2 := minimalRootOfUnity(y2)::P;
y3 := ygeq(mm, rbar3); xiord3 := minimalRootOfUnity(y3)::P;
xiord := lcm[xiord1,xiord2,xiord3]
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps(y1)

a1 := LAUEX1(y1)::An(CX);
a2 := LAUEX1(y2)::An(CX);
a3 := LAUEX1(y3)::An(CX);
assertTrue(zero?(a1 - a2 - 4*a3))

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # Rogers-Ramanujan functions

-- \begin{align}
-- G(q)
-- &=
-- \sum_{n=0}^\infty \frac{q^{n^2}}{(q;q)_n}
-- =
-- \prod_{n=0}^\infty \frac{1}{(1-q^{5n+1})(1-q^{5n+4})}
-- \\
-- H(q)
-- &=
-- \sum_{n=0}^\infty \frac{q^{n(n+1)}}{(q;q)_n}
-- =
-- \prod_{n=0}^\infty \frac{1}{(1-q^{5n+2})(1-q^{5n+3})}
-- \end{align}

-- +
-------------------------------------------------------------------
--test:Rogers-Ramanujan-1-2
-------------------------------------------------------------------
-- -

-- ## Ramanujan identity (1.2)

-- \begin{align}
-- H(q) G(q)^{11}-q^2 G(q) H(q)^{11}
-- &=
-- 1 + 11 G(q)^6 H(q)^6
-- \tag{1.2}
-- \end{align}


-- Check Pochhammer relations with eta-quotient and Jacobi function
-- computation.

z1 := expansion(GEQ(5,[1,2],[-11,-1]));
z2 := gep(5, [1,4])^(-11)*gep(5,[2,3])^(-1);
assertEquals(order(z1 - z2, 200), 200)


jinvG:=jf(one,5,1);
jinvH:=jf(one,5,2);
pochGH := EE EQ(5, [0,12]);
jjp := jinvG^(-11)*jinvH^(-1)*pochGH;
assertEquals(order(z1 - jjp,200),200)


-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------


-- +
-------------------------------------------------------------------
--test:Rogers-Ramanujan-1-3
-------------------------------------------------------------------
-- -

-- ## Ramanujan identity (1.3)

-- \begin{align}
-- H(q) G(q^11)-q^2 G(q) H(q^{11})
-- &=
-- 1
-- \tag{1.3}
-- \end{align}

G := inv jf(one,5,1) * ef 5; -- (q  ,q^4;q^5)
H := inv jf(one,5,2) * ef 5; -- (q^2,q^3;q^5)
l1 := multiplyExponents(G, 11) * H;
l2 := monomial(1,2)$L * G * multiplyExponents(H,11);
assertEquals(order(l1 - l2 - 1, 100), 100)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------


-- +
-------------------------------------------------------------------
--test:Rogers-Ramanujan-1-5
-------------------------------------------------------------------
-- -

-- ## Ramanujan identity (1.5)

-- \begin{align}
-- \frac{1}{\eta_{5;1}(\tau)^{11} \eta_{5;2}(\tau)}
-- -
-- \frac{1}{\eta_{5;1}(\tau) \eta_{5;2}(\tau)^{11}}
-- &=
-- 1 + 11 \frac{\eta(5\tau)^6}{\eta(\tau)^6}
-- \tag{1.5}
-- \end{align}
--
-- We check this identity at all cusps of $\Gamma_1(5)$.

rbar1 := [[5,1,-11], [5,2, -1]]; assertTrue(modularGamma1?(5,rbar1));
rbar2 := [[5,1, -1], [5,2,-11]]; assertTrue(modularGamma1?(5,rbar2));
rbar3 := [[1,-6],[5,6]];         assertTrue(modularGamma1?(5,rbar3));

nn := mm := 5;
y1 := ygeq(mm, rbar1); xiord1 := minimalRootOfUnity(y1)::P;
y2 := ygeq(mm, rbar2); xiord2 := minimalRootOfUnity(y2)::P;
y3 := ygeq(mm, rbar3); xiord3 := minimalRootOfUnity(y3)::P;
xiord := lcm[xiord1,xiord2,xiord3]
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps(y1)

a1 := LAUEX1(y1)::An(CX);
a2 := LAUEX1(y2)::An(CX);
a3 := LAUEX1(y3)::An(CX);
assertTrue(zero?(a1 - a2 - (1$An(CX) + 11 * a3)))

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------


-- +
-------------------------------------------------------------------
--test:Rogers-Ramanujan-1-6
-------------------------------------------------------------------
-- -

-- ## Ramanujan identity (1.6)

-- \begin{align}
-- \frac{1}{\eta_{5;1}(11\tau) \eta_{5;2}(\tau)}
-- -
-- \frac{1}{\eta_{5;1}(\tau) \eta_{5;2}(11\tau)}
-- &=
-- 1
-- \tag{1.6}
-- \end{align}

rbareta51 := [[5,1,1]]; assertTrue(not modularGamma1?(5,rbareta51));
rbareta52 := [[5,2,1]]; assertTrue(not modularGamma1?(5,rbareta52));
nn := mm := 5;
yeta51 := ygeq(mm, rbareta51); xiord1 := minimalRootOfUnity(yeta51)::P;
yeta52 := ygeq(mm, rbareta52); xiord2 := minimalRootOfUnity(yeta52)::P;
xiord := lcm[xiord1,xiord2]
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps(nn)$GAMMA1

p1 := PUIEX(yeta51(oo)); -- eta_{5,1} -- Puiseux expansion
p2 := PUIEX(yeta52(oo)); -- eta_{5,2}
px1 := multiplyExponents(inv p1, 11) * inv p2;
px2 := inv p1 * multiplyExponents(inv p2,11);
assertEquals(order(px1-px2-1, 50),50)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------


-- +
-------------------------------------------------------------------
--test:Rogers-Ramanujan-1-6-11
-------------------------------------------------------------------
-- -

-- ## Ramanujan identity (1.6) in level $11\cdot 5$

-- \begin{align}
-- \frac{1}{\eta_{55;11}(\tau) \eta_{5;2}(\tau)}
-- -
-- \frac{1}{\eta_{5;1}(\tau) \eta_{55;22}(\tau)}
-- &=
-- 1
-- \tag{1.6}
-- \end{align}

-- +
rbar1 := [[55,11,-1],[5,2,-1]]; assertTrue(modularGamma1?(55,rbar1))
rbar2 := [[5,1,-1],[55,22,-1]]; assertTrue(modularGamma1?(55,rbar2))

nn := mm := 55
y1 := ygeq(mm, rbar1); xiord1 := minimalRootOfUnity(y1)::P;
y2 := ygeq(mm, rbar2); xiord2 := minimalRootOfUnity(y2)::P;
xiord := lcm[xiord1,xiord2]
-- -

EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps(y2)

a1 := LAUEX1(y1)::An(CX);
a2 := LAUEX1(y2)::An(CX);
assertTrue(zero?(a1-a2-1$An(CX)))

pgrds := [-order PUIEX(y1 c) * WIDTH1(nn, c) for c in spitzen]
lgrds := qetaGrades a1
assertEquals(pgrds, lgrds)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------


-- +
-------------------------------------------------------------------
--test:Rogers-Ramanujan-1-7
-------------------------------------------------------------------
-- -

-- # Rogers-Ramanujan analogue in level 13

-- \begin{align}
-- \frac{1}{\eta_{13;1,3,4}(\tau)^{3} \eta_{13;2,5,6}(\tau)}
-- -
-- \frac{1}{\eta_{13;1,3,4}(\tau) \eta_{13;2,5,6}(\tau)^3}
-- &=
-- 1 + 3 \frac{\eta(13\tau)^2}{\eta(\tau)^2}
-- \tag{1.7}
-- \end{align}
--
-- We check this identity at all cusps of $\Gamma_1(13)$.

-- +
rbar1 := [[13,1,-3],[13,3,-3],[13,4,-3],[13,2,-1],[13,5,-1],[13,6,-1]];
rbar2 := [[13,1,-1],[13,3,-1],[13,4,-1],[13,2,-3],[13,5,-3],[13,6,-3]];
rbar3 := [[1,-2],[13,2]];
assertTrue(modularGamma1?(13,rbar1));
assertTrue(modularGamma1?(13,rbar2));
assertTrue(modularGamma1?(13,rbar3));

nn := mm := 13;
y1 := ygeq(mm, rbar1); xiord1 := minimalRootOfUnity(y1)::P;
y2 := ygeq(mm, rbar2); xiord2 := minimalRootOfUnity(y2)::P;
y3 := ygeq(mm, rbar3); xiord3 := minimalRootOfUnity(y3)::P;
xiord := lcm[xiord1,xiord2,xiord3]
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps(y1)
-- -

a1 := LAUEX1(y1)::An(CX);
a2 := LAUEX1(y2)::An(CX);
a3 := LAUEX1(y3)::An(CX);
assertTrue(zero?(a1 - a2 - (1$An(CX) + 3 * a3)))

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------

-- +
-------------------------------------------------------------------
--test:Rogers-Ramanujan-1-8
-------------------------------------------------------------------
-- -

-- # Rogers-Ramanujan analogue in level $3\cdot 13$

-- \begin{align}
-- \frac{1}{\eta_{39;3,9,12}(\tau) \eta_{13;2,5,6}(\tau)}
-- -
-- \frac{1}{\eta_{13;1,3,4}(\tau) \eta_{39;6,15,18}(\tau)}
-- &=
-- 1 + 3 \frac{\eta(13\tau)^2}{\eta(\tau)^2}
-- \tag{1.8}
-- \end{align}
--
-- We check this identity at all cusps of $\Gamma_1(39)$.

rbar1 := [[39,3,-1],[39,9,-1],[39,12,-1],[13,2,-1],[13,5,-1],[13,6,-1]];
rbar2 := [[13,1,-1],[13,3,-1],[13,4,-1],[39,6,-1],[39,15,-1],[39,18,-1]];
rbar3 := [[1,-2],[13,2]];
assertTrue(modularGamma1?(39,rbar1))
assertTrue(modularGamma1?(39,rbar2))
assertTrue(modularGamma1?(39,rbar3))

nn := mm := 39;
y1 := ygeq(mm, rbar1); xiord1 := minimalRootOfUnity(y1)::P;
y2 := ygeq(mm, rbar2); xiord2 := minimalRootOfUnity(y2)::P;
y3 := ygeq(mm, rbar3); xiord3 := minimalRootOfUnity(y3)::P;
xiord := lcm[xiord1,xiord2,xiord3]
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps(y1)

a1 := LAUEX1(y1)::An(CX);
a2 := LAUEX1(y2)::An(CX);
a3 := LAUEX1(y3)::An(CX);
assertTrue(zero?(a1 - a2 - 1$An(CX)))

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------

-- +
-------------------------------------------------------------------
--test:Ramanujan-2-19
-------------------------------------------------------------------
-- -

-- # Ramanujan identity (2.19)

-- \begin{align}
-- \frac{\eta(\tau)^2}{\eta(25\tau)}
-- &=
-- \frac{\eta_{25;10}(\tau)}{\eta_{25,5}(\tau)}
-- -
-- 1
-- -
-- \frac{\eta_{25;5}(\tau)}{\eta_{25,10}(\tau)}
-- \tag{2.19}
-- \end{align}
--
-- We check this identity at all cusps of $\Gamma_1(25)$.

nn := mm := 25;
rbar1 := [[1,1],[25,-1]];
rbar2 := [[25,5,-1],[25,10, 1]];
rbar3 := [[25,5, 1],[25,10,-1]];
assertTrue(modularGamma1?(mm,rbar1))
assertTrue(modularGamma1?(mm,rbar2))
assertTrue(modularGamma1?(mm,rbar3))

y1 := ygeq(mm, rbar1); xiord1 := minimalRootOfUnity(y1)::P;
y2 := ygeq(mm, rbar2); xiord2 := minimalRootOfUnity(y2)::P;
y3 := ygeq(mm, rbar3); xiord3 := minimalRootOfUnity(y3)::P;
xiord := lcm[xiord1,xiord2,xiord3]
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps(y1)

a1 := LAUEX1(y1)::An(CX);
a2 := LAUEX1(y2)::An(CX);
a3 := LAUEX1(y3)::An(CX);
assertTrue(zero?(a1 - a2 + a3 + 1$An(CX)))

for c in spitzen repeat
  p1 := PUIEX y1 c;
  p2 := PUIEX y2 c;
  p3 := PUIEX y3 c;
  px := p1 - p2 + p3 + 1
  assertEquals(order(px,10),10)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
