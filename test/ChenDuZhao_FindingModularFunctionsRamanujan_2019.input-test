-- -*- coding: utf-8 -*-
-- Via jupytext this file can be shown as a jupyter notebook.

)read /home/hemmecke/g/qeta/input/jfricas-test-support.input )quiet

-- # Problem formulation


-- This worksheet contains some formulas and computations from
-- the following article.
--
--     @Article{ChenDuZhao_FindingModularFunctionsRamanujan_2019,
--       author =       {Chen, William Y. C. and Du, Julia Q. D. and Zhao,
--                       Jack C. D.},
--       title =        {Finding Modular Functions for {R}amanujan-Type
--                       Identities},
--       journal =      {Annals of Combinatorics},
--       year =         2019,
--       volume =       23,
--       number =       3,
--       pages =        {613--657},
--       abstract =     {This paper is concerned with a class of partition
--                       functions $a(n)$ introduced by Radu and defined in
--                       terms of eta-quotients. By utilizing the
--                       transformation laws of Newman, Schoeneberg and
--                       Robins, and Radu’s algorithms, we present an
--                       algorithm to find Ramanujan-type identities for
--                       $a(mn+t)$. While this algorithm is not guaranteed to
--                       succeed, it applies to many cases. For example, we
--                       deduce a witness identity for $p(11n+6)$ with
--                       integer coefficients. Our algorithm also leads to
--                       Ramanujan-type identities for the overpartition
--                       functions $\overline{p}(5n+2)$ and
--                       $\overline{p}(5n+3)$ and Andrews–Paule’s broken
--                       2-diamond partition functions $\triangle_2(25n+14)$
--                       and $\triangle_2(25n+24)$. It can also be extended
--                       to derive Ramanujan-type identities on a more
--                       general class of partition functions. For example,
--                       it yields the Ramanujan-type identities on Andrews’
--                       singular overpartition functions
--                       $\overline{Q}_{3,1}(9n+3)$ and
--                       $\overline{Q}_{3,1}(9n+6)$ due to Shen, the
--                       2-dissection formulas of Ramanujan, and the
--                       8-dissection formulas due to Hirschhorn.},
--       issn =         {0219-3094},
--       doi =          {10.1007/s00026-019-00457-4},
--       url =          {https://doi.org/10.1007/s00026-019-00457-4}
--     }
--

-- # Init

-- +
-------------------------------------------------------------------
--setup
-------------------------------------------------------------------

-- +
)set mess type off
)set mess time off
C ==> Q

PEP ==> etaPower $ EQI(C)
GEP ==> generalizedEtaPower $ EQI(C)
EQ ==> etaQuotient $ EQI(C)
EE ==> eulerExpansion $ EQI(C)
EX ==> expansion $ EQI(C)

QF ==> QFunctions(Q, L1 Q)
QFX ==> QFunctions(CX, L1 CX)
jf ==> jacobiFunction $ QF
ef ==> eulerFunction $ QF
gep ==> generalizedEulerProduct $ QF

L ==> L1 C
TX ==> T1 CX
LX ==> L1 CX
PX ==> QEtaPuiseuxSeries CX

puiex ==> puiseuxExpansion $ QESX(C, CX)

oo ==> infinity()$Cusp

qIdentity ==> 1
expand ==> 2
nonModular ==> 4
asExpression ==> 8
withSubscript ==> 16

)read convenience.input )quiet
)set stream calc 2
-- -

)set mess type on
)set mess time on

-- +
-------------------------------------------------------------------
--endsetup
-------------------------------------------------------------------
-- -

)set output algebra on
)set output formatted off

-- # $p(5n)$

-- +
-------------------------------------------------------------------
--test:p5n
-------------------------------------------------------------------
-- -


-- See Formula (1.4) of
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.
-- \begin{align}
-- \sum_{n=0}^\infty p(5n)
-- &=
-- \frac{(q^5;q^5)_\infty}{(q;q)_\infty^2 (q,q^4;q^5)_\infty^8}
-- -
-- 3 q \frac{(q^5;q^5)_\infty^6 (q,q^4;q^5)_\infty^2}{(q;q)_\infty^7}
-- \tag{1.4}
-- \end{align}

-- We can easily verify this identity up to order 100.

m := 5; t := 0;
rspec := geqSPEC [[1,-1]];
nn := minLevel1(rspec, m, t)

-- As can be seen that is done in level 5.

-- Now we check the relation above up to order 100.

ps := partitionSeries(1)$QFunctions(Q, L1(Q))
f := p5n0 := choose(m, t, ps)$L1(C);
rspec1 := geqSPEC [[1,-2],[5,1],[5,1,-8]];
rspec2 := geqSPEC [[1,-7],[5,6],[5,1,2]];
f1 := eulerExpansion specEQI(C) rspec1
f2 := eulerExpansion specEQI(C) rspec2
g := f1 - monomial(3, 1)$L1(C)*f2;
assertEquals(order(f - g,100), 100)

-- ## Find cofactor

-- Let us do a first step into the direction of finding
-- the above identity. In order to do this, we have to
-- multiply the left-hand side of (1.4) by a generalized
-- eta-quotient so that the product will be a modular
-- function for $\Gamma_1(5)$ with poles only at infinity.
--
-- This cofactor can be computed as follows.

idxs := [[1],[5],[5, 1]]
sspec := cofactI1(nn,rspec,m,t,idxs)

-- Interestingly, it is just the invers of the eta-quotient in
-- the second summand of the right-hand side. So we will show:
-- \begin{align}
-- \frac
-- {(q;q)_\infty^7}
-- {q (q^5;q^5)^6_\infty (q,q^4;q^5)_\infty^2}
-- \sum_{n=0}^\infty p(5n)
-- &=
-- \frac
-- {(q;q)_\infty^5}
-- {q (q^5;q^5)_\infty^5 (q,q^4;q^5)_\infty^{10}}
-- -
-- 3
-- \end{align}
-- or rather
-- \begin{align}
-- \frac
-- {\eta(\tau)^7}
-- {\eta(5 \tau)^6 \eta_{5,1}(\tau)^2}
-- \sum_{n=0}^\infty p(5n)
-- &=
-- \frac
-- {\eta(\tau)^5}
-- {\eta(5 \tau)^5 \eta_{5,1}(\tau)^{10}}
-- -
-- 3.
-- \end{align}

assertTrue(modularGamma1?(sspec,rspec,m,t))

-- Is the cofactor and the basis modular for $\Gamma_1(N)$.

yf := specYM1GEQ(sspec,rspec,m,t);
rspect := geqSPEC [[1,5], [5,-5], [5,1,-10]]
yt := specYM1GEQ(rspect);
xiord := minimalRootOfUnity(yf)::P
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps(yf)

-- The variables `af` and `at` correspond to the lefthand side
-- and the (generalized) eta-quotient on the righthand side,
-- expanded at all cusps
-- where $\xi$ is a 120-th primitive root of unity.

af := expandAnCX yf; qetaGrades af
at := expandAnCX yt; qetaGrades at

)set stream calc 3
af
at

-- We check the identity at all cusps of
-- $\Gamma_1(5)$.

assertTrue(zero?(af-at+3*1$An(CX)))

-- ## Find the relation

-- Potentially, we can use the generalized eta-functions
-- $\eta_1(\tau) := \eta(\tau)$,
-- $\eta_5(\tau) := \eta(5\tau)$,
-- $\eta_{5,1}(\tau)$, and
-- $\eta_{5,2}(\tau)$.

generalizedEtaFunctionIndices(nn)

idxs := [[1],[5],[5, 1]]
mspecs := ncmSPECS1(nn, idxs)
ygens := [specYM1GEQ x for x in mspecs];
egens := [expandAnCX x for x in ygens];
[qetaGrades x for x in egens]


msyms := indexedSymbols("M", #egens)
k := position(oo, spitzen)
xgens := [toX1(C, series(x,k), msym) for msym in msyms for x in egens];
[qetaGrade x for x in xgens]
xab := samba(xgens.1, xgens)$QXTOPSAMBA1(C)

xf := toX1(C, series(af,k), 'F)
xr := reduce(xf,xab)$QXTOPRED1(C)
pol := second(xr)::Pol(C)
ilcpol := -inv(coefficient(pol,'F, 1))::C

mpol := coefficient(ilcpol*pol, 'F, 0)
assertEquals(mpol, M1-3)

-- ## Find the relation by just one function

-- There is also a package that puts the above steps together.

m := 5; t := 0;
rspec := geqSPEC [[1,-1]];
nn := minLevel1(rspec, m, t)
idxs := [[1],[5],[5, 1]];

id := findID1(nn, idxs, rspec, m, t, idxs)
pretty(id, qIdentity + asExpression + expand + withSubscript)

assertEquals(coSpecification id, sspec)
idpol := identityPolynomial(id)
ilcpol := -inv(coefficient(idpol,'F, 1))::C
assertEquals(ilcpol*coefficient(idpol,'F,0), M1-3)

idxs := [[1],[5],[5, 1]];
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript + asExpression)
idxs := [[1],[5],[5, 2]];
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)

idxs := [[1],[5],[5, 1]];
id := findID1(nn, idxs, rspec, m, 1, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)
idxs := [[1],[5],[5, 1]];
id := findID1(nn, idxs, rspec, m, 2, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)
id := findID1(nn, idxs, rspec, m, 4, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # $p(11n+6)$

-- +
-------------------------------------------------------------------
--test:p11n6
-------------------------------------------------------------------
-- -

-- See \cite[Thm.~1.1]{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.
-- Theorem 1.1 Equations (1.7)-(1.9)

m := 11; t := 6;
rspec := eqSPEC [[1,-1]]
nn := minLevel1(rspec, m, t)

-- Create the series from the above article in two different ways
-- and compare the first 100 coefficients.

ps := partitionSeries(1)$QFunctions(Q, L1(Q));
p11n6 := choose(11,6,ps);
z01 := eulerExpansion(PEP(1,24)*PEP(11,-23)*GEP(11,1,-28)*GEP(11,2,-16)*GEP(11,3,-12)*GEP(11,4,-4));
sspecz0 := geqSPEC [[1,24],[11,-23],[11,1,-28],[11,2,-16],[11,3,-12],[11,4,-4]];
z00 := eulerExpansion specEQI(C) sspecz0;
assertEquals(order(z01-z00, 100), 100)
z0 := monomial(1,-20)$L1(C)* z00;

rspecz := geqSPEC [[1,1],[11,-1],[11,1,-3],[11,2,-2]];
z := EX specEQI(C) rspecz;
zexp := EX(PEP(1,1)*PEP(11,-1)*GEP(11,1,-3)*GEP(11,2,-2))
assertEquals(order(z - zexp, 100), 100)

rspece := geqSPEC [[1,3],[11,-3],[11,1,-5],[11,2,-5],[11,3,-4],[11,4,-1]];
e := EX specEQI(C) rspece;
eexp := EX(PEP(1,3)*PEP(11,-3)*GEP(11,1,-5)*GEP(11,2,-5)*GEP(11,3,-4)*GEP(11,4,-1));
assertEquals(order(e - eexp, 100), 100)

-- Take the relation from the above article and check it with
-- the above series.

-- +
ts := [z^10, z^8*e, z^9, z^7*e, z^8, z^6*e, z^7, z^5*e, z^6, z^4*e,_
       z^5, z^3*e, z^4, z^2*e, z^3, z*e, z^2, z, 1$L];
cs := [1,11,30,-44,-90,44,72,-44,4,99,-12,-132,-41,88,68,-22,-39,7,1];
terms := [c*t for c in cs for t in ts];

zz := z0*p11n6 - 11*reduce(_+, terms, 0$L);
assertEquals(removeDuplicates[coefficient(zz,i) for i in -20..50],[0])
assertEquals(order(zz, 100), 100)
-- -

-- ## Check modularity for cofactor

assertTrue(modularGamma1?(sspecz0,rspec,m,t))

-- ## Check Puiseux = Laurent expansion

-- Check that Puiseux expansion and Laurent
-- expansion have the same order.

x := specYM1GEQ(sspecz0,rspec,m,t);
spitzen := cusps x
xiord := minimalRootOfUnity(x)::P
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi)
)set stream calc 2
PGRD(nn,x,c) ==> - WIDTH1(nn, c)*order puiex(x(c))
pgrds := [PGRD(nn, x, cusp) for cusp in spitzen]
ax := expandAnCX x;
assertEquals(pgrds, qetaGrades ax)
yz0 := specYGEQ sspecz0;

yz := specYGEQ rspecz;
pgrds := [PGRD(nn, yz, cusp) for cusp in spitzen]
assertEquals(pgrds, qetaGrades yz)

ye := specYGEQ rspece;
pgrds := [PGRD(nn, ye, cusp) for cusp in spitzen]
assertEquals(pgrds, qetaGrades ye)

-- ## Check modularity of basis

assertTrue(modularGamma1? rspecz)
assertTrue(modularGamma1? rspece)

-- ## Find cofactor

-- If we exclued the index [11,5] from the indices, then we
-- find the same cofactor as in
-- \cite[Thm.~1.1]{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.

idxs := [[1],[11],[11,1],[11,2],[11,3],[11,4]]
assertEquals(concat(idxs, [11,5]), generalizedEtaFunctionIndices nn)

sspecz0
sspec := cofactI1(nn,rspec,m,t,idxs)
assertEquals(sspecz0,sspec)

assertTrue(modularGamma1?(sspec,rspec,m,t))
yf := generalizedEtaQuotient(sspec,rspec,m,t);
xiord := minimalRootOfUnity(yf)::P
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps yf
af := expandAnCX yf; qetaGrades af

-- Obviously, our cofactor gives the same modular
-- function as the cofactor $z_0$ from
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.

)set stream calc 1
assertTrue(zero?(af-ax))

-- ## Find the relation

-- Check whether the computation of exponent vectors and specifications
-- agrees.

mspecs := ncmSPECS1 nn;
ygens := [specYM1GEQ x for x in mspecs];
egens := [expandAnCX x for x in ygens];
[qetaGrades x for x in egens]

msyms := indexedSymbols("M", #egens)
k := position(oo, spitzen)
xgens := [toX1(C, series(x,k), msym) for msym in msyms for x in egens];
[qetaGrade x for x in xgens]
xab := samba(xgens.1, xgens)$QXTOPSAMBA1(C)

xf := toX1(C, series(af,k), 'F)
xr := reduce(xf, xab)$QXTOPRED1(C)
pol := second(xr)::Pol(C);
ilcpol := -inv(coefficient(pol,'F, 1))::C;

mpol := coefficient(ilcpol*pol, 'F, 0)
assertEquals(_
  mpol,_
  11*(M1^10 + 40*M1^9 + 11*M1^8*M5 + 225*M1^8 + 44*M1^7*M5_
      + 552*M1^7 + 44*M1^6*M5 + 718*M1^6-88*M1^5*M5_
      + 516*M1^5 - 231*M1^4*M5 + 169*M1^4 - 220*M1^3*M5_
      - 16*M1^3 - 110*M1^2*M5 - 24*M1^2 - 22*M1*M5_
      - 3*M1+1))

-- ## Find the relation by just one function

idxs := [[1],[11],[11,1],[11,2],[11,3],[11,4]];
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)

-- +
pol := 11*(11*(M1^8-4*M1^7+4*M1^6-4*M1^5+9*M1^4-12*M1^3+8*M1^2-2*M1)*M5_
           + M1^10+19*M1^9-35*M1^8-16*M1^7+92*M1^6-155*M1^5_
           + 190*M1^4-152*M1^3+71*M1^2-15*M1+1) - F;

pol := 11*((11*M1^8-44*M1^7+44*M1^6-44*M1^5+99*M1^4-132*M1^3+88*M1^2-22*M1)*M5_
           +M1^10+19*M1^9-46*M1^8+28*M1^7+48*M1^6-111*M1^5+91*M1^4_
           -20*M1^3-17*M1^2+7*M1+1)-F
assertEquals(-11*identityPolynomial id, pol)


-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # $\overline{p}(5n+2)$

-- +
-------------------------------------------------------------------
--test:pp5n2
-------------------------------------------------------------------
-- -

-- $$
-- \sum_{n=0}^\infty \overline{p}(n) q^n
-- =
-- \frac{(q^2;q^2)_\infty}{(q;q)_\infty^ 2}
-- $$
--
-- See example in
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}
-- after Theorem 2.1 and compare with Theorem 3.1 on page 20.

m := 5; t:= 2
rspec := eqSPEC [[1,-2],[2,1]]
nn := minLevel1(rspec,m,t)

idxs := generalizedEtaFunctionIndices nn
ecss := etaCofactorSpace1System(nn,rspec,m,t,idxs)$QEtaCofactorSpace

-- ## Manual initial cofactor vector

-- Although we will do an automatic computation below, we show
-- here, another relatively minimal (initial) vector for the
-- cofactor.

mat := ecss.comatrix
v := ecss.rhs
mat * vector [0,0,18,-17,0,0,0,0,0,-8,  1,0,13,4,9]

ecs := etaCofactorSpace1(nn,rspec,m,t,idxs)$QEtaCofactorSpace

spitzen := CUSPS1 10
v0 := ecs.particular;
b := ecs.basis;
vv := v0 - 23*b.9 + 2*b.8 + 4*b.7


-- The vector vv does, however, not correspond to a generalized
-- eta-quotient with poles only at infinity.

le := members vv
idxs := ecs.indices
sspecvv := generalizedEtaQuotientSpecification(nn, idxs, le)$QEtaSpecification
qetaGrades(sspecvv, rspec, m, t)$QEtaModularGamma1Package

-- ## Automatic computation of cofactor

-- +
sspec := cofactI1(nn,rspec,m,t,idxs)

modularGamma1?(sspec, rspec, m, t)
-- -

idxs := cons([nn], rest properGeneralizedEtaFunctionIndices nn)
sspec := cofactI1(nn,rspec,m,t,idxs)
modularGamma1?(sspec, rspec, m, t)

y := specYM1GEQ(sspec, rspec, m, t);
xiord := minimalRootOfUnity(y)::P
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
)set stream calc 2

-- The respective series for the generating series of
-- $\overline{p}(5n+2)$ after multiplying with the cofactor
-- is a modular function for $\Gamma_1(10)$ with only a pole
-- of order 3 at infinity.

af := expandAnCX y
assertEquals(qetaGrades af,[0,0,0,0,0,0,0,3])

-- ## Find the relation for $\overline{p}(5n+2)$

mspecs := ncmSPECS1 nn;
ygens := [specYM1GEQ x for x in mspecs];
egens := [expandAnCX x for x in ygens];
[qetaGrades x for x in egens]

msyms := indexedSymbols("M", #egens)
k := position(oo, spitzen)
xgens := [toX1(C, series(x,k), msym) for msym in msyms for x in egens];
[qetaGrade x for x in xgens]
xab := samba(xgens.1, xgens)$QXTOPSAMBA1(C)

-- The reduction by the algebra basis leads to another
-- relation than given in Theorem 3.1 of
-- \cite[p. 20]{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.

xf := toX1(C, series(af,k), 'F);
xr := reduce(xf, xab)$QXTOPRED1(C);
pol := second(xr)::Pol(C);
ilcpol := -inv(coefficient(pol,'F, 1))::C;
mpol := coefficient(ilcpol*pol, 'F, 0)
assertEquals(mpol, 4*(M1^3 + 7*M1^2 + 8*M1 + 4))

-- We can, however, get exactly the same relation if we use the
-- third element of our list.

xab := samba(xgens.3, xgens)$QXTOPSAMBA1(C);
xf := toX1(C, series(af,k), 'F);
xr := reduce(xf, xab)$QXTOPRED1(C);
pol := second(xr)::Pol(C);
ilcpol := -inv(coefficient(pol,'F, 1))::C;
mpol := coefficient(ilcpol*pol, 'F, 0)
assertEquals(mpol, 4*(M3^3 + M3^2 - 8*M3 + 8))

-- ## Comparison with computation from article

-- We have found a relation that looks exactly like (3.6) from
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}, but
-- with different $y$ and different $z$.

sspec,mspecs.3

-- In \cite[Thm. 3.1]{ChenDuZhao_FindingModularFunctionsRamanujan_2019}
-- the generalized eta-quotient $\eta_{10,5}(\tau)$ is used.
-- We replace it by $\eta(5\tau)^2/\eta(10\tau)^2$ and therefore get
-- the following specification in our notation.

sspeccdz := geqSPEC [[1,11],[2,-7],[5,-1],[10,-2],[5,1,12],[10,1,-14],[10,4,-8]]
assertTrue(modularGamma1?(sspeccdz, rspec, m, t))

-- The respective series for the generating series of
-- $\overline{p}(5n+2)$ after multiplying with the cofactor
-- is a modular function for $\Gamma_1(10)$ with only a pole
-- of order 3 at infinity.

afcdz := expandAnCX specYM1GEQ(sspeccdz, rspec, m, t);
assertEquals(qetaGrades afcdz,[0,0,0,0,0,0,0,3])

-- Although the initial cofactor is different, we get the
-- same expansion as q-series (at all cusps).

assertTrue(not (sspec=sspeccdz))
assertTrue(zero?(af - afcdz))

-- So the quotient of the two cofactors must be
-- a generalized eta-quotient that is equal to 1.

spec1 := sspec/sspeccdz
a1 := specM1AnCX spec1
assertTrue(zero?(a1 - 1$An(CX)))

-- Let us investigate the difference to the $z$ of
-- \cite[Thm. 3.1]{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.

zspec := geqSPEC [[1,1],[5,1],[10,-2],[5,1,-2],[10,1,-1]]
assertTrue(modularGamma1? zspec)

-- Indeed, our generalized eta-quotient (given by mspecs.1)
-- and the one from the article above have the same
-- $q$-series expansion.

spec1 := mspecs.3/zspec
a1 := specM1AnCX spec1
assertTrue(zero?(a1 - 1$An(CX)))

-- ## Find a relation by just one function

-- By our implementation, we are able to find a relation with
-- only one generalized eta-function.

id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)
assertEquals(32*identityPolynomial id, F-4*(M1^3+M1^2-8*M1+8))

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # $\overline{Q}_{3,1}$ &mdash; (3,1)-singular overpartition

-- +
-------------------------------------------------------------------
--test:singularoverpartition-3-1-9-3
-------------------------------------------------------------------
-- -

-- See \cite[Thm. 10.3]{ChenDuZhao_FindingModularFunctionsRamanujan_2019}
-- as well as (10.3) for the definition of $\overline{Q}_{3,1}$.
-- $$
-- \sum_{n=0}^\infty \overline{Q}_{k,i}(n) q^n
-- =
-- \frac{(q^k;q^k)_\infty (q^{2i},q^{2k-2i};q^{2k})_\infty}
--       {(q;q)_\infty (q^i, q^{k-i};q^k)_\infty}
-- $$

m := 9; t := 3
rspec := geqSPEC [[1,-1],[3,1],[3,1,-1],[6,2,1]] -- Q_{3,1}
nn := minLevel1(rspec,m,t)
idxs := generalizedEtaFunctionIndices(nn)
sspec := cofactI1(nn,rspec,m,t)
modularGamma1?(sspec, rspec, m, t)$QEtaCofactorConditions
y := specYM1GEQ(sspec, rspec, m, t);
xiord := minimalRootOfUnity(y)::P
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps y
af := expandAnCX y
assertEquals(qetaGrades af, [0,0,0,1])
assertEquals(qetaGrades y, [0,0,0,1])

-- The cofactor in
-- \cite[Thm. 10.3]{ChenDuZhao_FindingModularFunctionsRamanujan_2019}
-- is a pure eta-quotient.

sspeccdz := geqSPEC [[1,14], [2,-5], [3,-6],[6,-3]]

-- We can find it ourselves by restricting the search space.

idxs := [[1], [2], [3], [6]]
sspec := cofactI1(nn,rspec,m,t,idxs)
assertEquals(sspeccdz, sspec)

af := expandAnCX y

-- ## Find the relation for $\overline{Q}_{3,1}(9n+3)$

mspecs := ncmSPECS1 nn
ygens := [specYM1GEQ x for x in mspecs];
egens := [expandAnCX x for x in ygens];
[qetaGrades x for x in egens]

-- The basis elements only differ in a constant.
-- The choice that corresponds to the $z$ from Theorem 10.3 is
-- our second basis element.

msyms := indexedSymbols("M", #egens)
k := position(oo, spitzen)
xgens := [toX1(C, series(x,k), msym) for msym in msyms for x in egens];
[qetaGrade x for x in xgens]
xab := samba(xgens.2, xgens)$QXTOPSAMBA1(C)

-- The reduction by the algebra basis leads exactly to the
-- relation given in Theorem 10.3 of
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.

xf := toX1(C, series(af,k), 'F);
xr := reduce(xf, xab)$QXTOPRED1(C);
pol := second(xr)::Pol(C);
ilcpol := -inv(coefficient(pol,'F, 1))::C;
mpol := coefficient(ilcpol*pol, 'F, 0)
assertEquals(mpol, 6*(M2 + 16))

-- ## Find the relation for $\overline{Q}_{3,1}(9n+3)$ in one step

idxs := [[1],[2],[3],[6]]
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)

pol := 6*(M1+15) - F;
assertEquals(-90*identityPolynomial id, pol)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # $\overline{Q}_{3,1}$ &mdash; (3,1)-singular overpartition

-- +
-------------------------------------------------------------------
--test:singularoverpartition-3-1-9-6
-------------------------------------------------------------------
-- -

-- See \cite[Thm. 10.3]{ChenDuZhao_FindingModularFunctionsRamanujan_2019}
-- as well as (10.3) for the definition of $\overline{Q}_{3,1}$.
-- $$
-- \sum_{n=0}^\infty \overline{Q}_{k,i}(n) q^n
-- =
-- \frac{(q^k;q^k)_\infty (q^{2i},q^{2k-2i};q^{2k})_\infty}
--       {(q;q)_\infty (q^i, q^{k-i};q^k)_\infty}
-- $$
--

m := 9; t := 6
rspec := geqSPEC [[1,-1],[3,1],[3,1,-1],[6,2,1]] -- Q_{3,1}

-- That is equal to https://faculty.math.illinois.edu/~reznick/2690367.pdf
-- where it is given by (q^3;q^3)*(-q;q)/(-q^3;q^3).
-- We can derive a representation in pure eta-quotients automatically.

aidxs := generalizedEtaFunctionIndices(6)
idxs := aidxs(1..4)
id := findID1(6, idxs, rspec, 1, 0, idxs);
-- pretty(id, qIdentity)
rspec2 := inv coSpecification id
rspec2Expected := geqSPEC [[1, - 2], [2, 1], [3, 2], [6, - 1]]
assertEquals(rspec2, rspec2Expected)

-- Now we can try to prove divisibility by Radu's original method
-- for (pure) eta-quorients.

nn := minLevel0(rspec2,m,t)
ORBIT(rspec2,m,t)
id := findID0(nn, rspec2, m, 3);
pretty(id, qIdentity + nonModular + expand)

-- In the following we show the same thing with the extension due to
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.

nn := minLevel1(rspec,m,t)
idxs := generalizedEtaFunctionIndices(nn)
sspec := cofactI1(nn,rspec,m,t)
modularGamma1?(sspec, rspec, m, t)$QEtaCofactorConditions
y := specYM1GEQ(sspec, rspec, m, t);
xiord := minimalRootOfUnity(y)::P
EXTENDEDCOEFFICIENTRING(C, xiord, CX, xi);
spitzen := cusps(y)
af := expandAnCX y
assertEquals(qetaGrades af, [0,0,0,1])
assertEquals(qetaGrades y, [0,0,0,1])

-- The cofactor in
-- \cite[Thm. 10.3]{ChenDuZhao_FindingModularFunctionsRamanujan_2019}
-- is a pure eta-quotient.

sspeccdz := geqSPEC [[1,13], [2,-4], [3,-3],[6,-6]]

-- We can find it ourselves by restricting the search space.

idxs := [[1], [2], [3], [6]]
sspec := cofactI1(nn,rspec,m,t,idxs)
assertEquals(sspeccdz, sspec)

af := expandAnCX y

-- ## Find the relation for $\overline{Q}_{3,1}(9n+6)$

mspecs := ncmSPECS1 nn;
ygens := [specYM1GEQ x for x in mspecs];
egens := [expandAnCX x for x in ygens];
[qetaGrades x for x in egens]

-- The basis elements only differ in a constant.
-- The choice that corresponds to the $z$ from Theorem 10.3 is
-- our second basis element.

msyms := indexedSymbols("M", #egens)
k := position(oo, spitzen)
xgens := [toX1(C, series(x,k), msym) for msym in msyms for x in egens];
[qetaGrade x for x in xgens]
xab := samba(xgens.2, xgens)$QXTOPSAMBA1(C)

-- The reduction by the algebra basis leads exactly to the
-- relation given in Theorem 10.3 of
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.

xf := toX1(C, series(af,k), 'F);
xr := reduce(xf, xab)$QXTOPRED1(C);
pol := second(xr)::Pol(C);
ilcpol := -inv(coefficient(pol,'F, 1))::C;
mpol := coefficient(ilcpol*pol, 'F, 0)
assertEquals(mpol, 24*(M2 + 4))

-- ## Find the relation for $\overline{Q}_{3,1}(9n+6)$ in one step

idxs := [[1],[2],[3],[6]]
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)

pol := 24*(M1+3)- F;
assertEquals(-72*identityPolynomial id, pol)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # $\Delta_2(25n+14)$

-- +
-------------------------------------------------------------------
--test:Delta2-25-14
-------------------------------------------------------------------
-- -

-- Investigate the identities of Theorem 9.1 of
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.

-- Let
-- \begin{align*}
-- \sum_{n=0}^\infty \Delta_k(n) q^n
-- &=
-- \frac{
--       (q^2;q^2)_\infty (q^{2k+1};q^{2k+1})_\infty
--      }{
--       (q;q)_\infty^5 (q^{4k+2};q^{4k+2})_\infty
--      }.
-- \end{align*}
-- In \cite{AndrewsPaule_MacMahonsPartitionAnalysisXI_2007}
-- it is conjectured that
-- $$
-- \Delta_2(25n+14) \equiv 0 \pmod{5}.
-- $$
-- We show that by a witness identity.

m := 25; t := 14
rspec := geqSPEC [[1,-3],[2,1],[5,1],[10,-1]]
nn := minLevel1(rspec,m,t)
idxs := [[1],[2],[5],[10],[10,1],[10,2]]
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)

-- We find that when we multiply the polynomial by
-- the inverse of the leading coefficient of F and
-- then subtract F, every coefficient is an integer
-- and is divisible by 5.

ipol := identityPolynomial id;
assertEquals(# monomials ipol, 59)
c := inv coefficient(id)
mons := monomials(c * ipol - F);
l := removeDuplicates [denom m for m in mons];
assertEquals(l, [1])
assertEquals(gcd [numer m for m in mons],5)

-- The `findIdentity` function takes the first
-- basis specification. By using the second, we
-- arrive at a relation with smaller denominator
-- and also smaller coefficients.

mspecs := monoidSpecifications id
bas := [specEQI(C) mspecs.i for i in 1..#mspecs]

-- +
--id1 := findID1(sspec, rspec, m, t, [mspecs.1]);
--pol1 := 6192449487634432 * identityPolynomial id1
-- -

-- We get a relation with slightly smaller
-- coefficients, if we take the second basis element.

sspec := coSpecification id
id2 := findID1(sspec, rspec, m, t, [mspecs.2]);
c := -(1/5)*inv coefficient(id2)
pol2 := c * identityPolynomial id2

-- ## Redo computation from the article

-- In \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}
-- another cofactor is used. For
-- \begin{gather}
-- \frac{(q;q)_\infty^{92}
--       (q^5;q^5)_\infty^{14}
--       (q,q^4;q^5)_\infty^{52}
--       (q^4,q^6;q^{10})_\infty^4
--      }{q^{57}
--       (q^2;q^2)_\infty^{58}
--       (q^{10};q^{10})_\infty^{46}
--       (q,q^9;q^{10})_\infty^{109}
--       (q^5;q^{10})_\infty^{10}
--      }
-- \sum_{n=0}^\infty \Delta_2(25n+14)q^n
-- \end{gather}
-- they find a 58-term representation
-- as a sum of powers of
-- \begin{gather}
-- z =
-- \frac{(q;q)_\infty
--       (q^5;q^5)_\infty
--      }{q
--       (q,q^4;q^5)_\infty^2
--       (q^{10};q^{10})_\infty^2
--       (q,q^9;q^{10})_\infty
--      }
-- \end{gather}
-- with coefficients divisible by 5.
--
-- With the same parameters as on page 34 of
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019},
-- we get exactly the right-hand side presented on page 35.

m := 25; t := 14
rspec := geqSPEC [[1,-3],[2,1],[5,1],[10,-1]];
nn := minLevel1(rspec,m,t)
sspec := geqSPEC [[1,92],[2,-58],[5,14],[10,-46],_
                  [5,1,52],[10,1,-109],[10,4,4],[10,5,-5]];
mspecs := [geqSPEC [[1,1],[5,1],[10,-2],[5,1,-2],[10,1,-1]]];
specEQI(C) mspecs.1
id := findID1(sspec, rspec, m, t, mspecs);
pretty(id, qIdentity + nonModular + withSubscript)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # $\Delta_2(25n+24)$

-- +
-------------------------------------------------------------------
--test:Delta2-25-24
-------------------------------------------------------------------
-- -

-- Investigate the identities of Theorem 9.1 of
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.
--
-- Let
-- \begin{align*}
-- \sum_{n=0}^\infty \Delta_k(n) q^n
-- &=
-- \frac{
--       (q^2;q^2)_\infty (q^{2k+1};q^{2k+1})_\infty
--      }{
--       (q;q)_\infty^5 (q^{4k+2};q^{4k+2})_\infty
--      }.
-- \end{align*}
-- In \cite{AndrewsPaule_MacMahonsPartitionAnalysisXI_2007}
-- it is conjectured that
-- $$
-- \Delta_2(25n+24) \equiv 0 \pmod{5}.
-- $$
-- We show that by a witness identity.

m := 25; t := 24
rspec := geqSPEC [[1,-3],[2,1],[5,1],[10,-1]]
nn := minLevel1(rspec,m,t)
idxs := [[1],[2],[5],[10],[5,1],[10,1]]
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)
assertEquals(# monomials identityPolynomial id, 59)

-- Let
-- \begin{align*}
-- \sum_{n=0}^\infty a(n) q^n
-- &=
-- \frac{
--       (q;q)_\infty^2 (q^2;q^2)_\infty
--      }{
--       (q^{10};q^{10})_\infty
--      }.
-- \end{align*}
-- Then, because of $(1-q^n)^5\equiv 1-q^{5n} \pmod{5}$,
-- we have
-- $\Delta_2(n)\equiv a(n) \pmod{5}$ for all natural $n$.
--
-- In \cite{AndrewsPaule_MacMahonsPartitionAnalysisXI_2007}
-- it is conjectured that
-- $$
-- \Delta_2(25n+24) \equiv 0 \pmod{5}.
-- $$
--
-- A relation for the series $a(n)$ is much smaller than
-- the one for $\Delta_2(n)$.

-- +
m := 25; t := 24
--rspec := geqSPEC [[1,-3],[2,1],[5,1],[10,-1]]
rspec := geqSPEC [[1,2],[2,1],[10,-1]]
nn := minLevel1(rspec,m,t)
idxs := [[1],[2],[5],[10],[10,1]]
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)

pol := 5*((2*M1+1)*M3 - (4*M1+1)*M1) - F;
assertEquals(5*identityPolynomial id, pol)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # $\Delta_2(25n+24)$

-- +
-------------------------------------------------------------------
--test:theorem-10-5-1
-------------------------------------------------------------------
-- -

-- Investigate the identities of Theorem 9.1 of
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.
--
-- Let
-- \begin{align*}
-- \sum_{n=0}^\infty \Delta_k(n) q^n
-- &=
-- \frac{
--       (q^2;q^2)_\infty (q^{2k+1};q^{2k+1})_\infty
--      }{
--       (q;q)_\infty^5 (q^{4k+2};q^{4k+2})_\infty
--      }.
-- \end{align*}
-- In \cite{AndrewsPaule_MacMahonsPartitionAnalysisXI_2007}
-- it is conjectured that
-- $$
-- \Delta_2(25n+24) \equiv 0 \pmod{5}.
-- $$
-- We show that by a witness identity.

m := 25; t := 24
rspec := geqSPEC [[1,-3],[2,1],[5,1],[10,-1]]
nn := minLevel1(rspec,m,t)
idxs := [[1],[2],[5],[10],[5,1],[10,1]]
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)
assertEquals(# monomials identityPolynomial id, 59)

-- Let
-- \begin{align*}
-- \sum_{n=0}^\infty a(n) q^n
-- &=
-- \frac{
--       (q;q)_\infty^2 (q^2;q^2)_\infty
--      }{
--       (q^{10};q^{10})_\infty
--      }.
-- \end{align*}
-- Then, because of $(1-q^n)^5\equiv 1-q^{5n} \pmod{5}$,
-- we have
-- $\Delta_2(n)\equiv a(n) \pmod{5}$ for all natural $n$.
--
-- In \cite{AndrewsPaule_MacMahonsPartitionAnalysisXI_2007}
-- it is conjectured that
-- $$
-- \Delta_2(25n+24) -\equiv 0 \pmod{5}.
-- $$

-- A relation for the series $a(n)$ is much smaller than
-- the one for $\Delta_2(n)$.

m := 25; t := 24
--rspec := geqSPEC [[1,-3],[2,1],[5,1],[10,-1]]
rspec := geqSPEC [[1,2],[2,1],[10,-1]]
nn := minLevel1(rspec,m,t)
idxs := [[1],[2],[5],[10],[10,1]]
id := findID1(nn, idxs, rspec, m, t, idxs);
pretty(id, qIdentity + expand + nonModular + withSubscript)

pol := 5*((2*M1+1)*M3 - (4*M1+1)*M1) - F;
assertEquals(5*identityPolynomial id, pol)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # Theorem 10.4

-- +
-------------------------------------------------------------------
--test:formula-10-10
-------------------------------------------------------------------
-- -
-- See Formula (10.10) of
-- \cite{ChenDuZhao_FindingModularFunctionsRamanujan_2019}.
--
-- \begin{align}
-- R(q)
-- &=
-- \frac{(q^2,q^{3};q^{5})_\infty}
--      {(q,q^{4};q^{5})_\infty}
-- \\
-- &=
-- \frac{(q^8,q^{12};q^{20})_\infty^2}
--      {(q^6,q^{14};q^{20})_\infty (q^{10},q^{10};q^{20})_\infty}
-- +
-- q \frac{(q^2,q^{18};q^{20})_\infty (q^8,q^{12};q^{20})_\infty}
--      {(q^4,q^{16};q^{20})_\infty (q^{10},q^{10};q^{20})_\infty}
-- \tag{10.10}
-- \end{align}

rspec := geqSPEC [[5,1,-1],[5,2,1]]
modularGamma1? rspec

nn := minLevel1(rspec,2,0)
idxs := rest properGeneralizedEtaFunctionIndices nn


id := findID1(10, idxs, rspec, 2, 0, idxs);
pretty(id, qIdentity + expand + nonModular + asExpression)
sspec2n0 := coSpecification id;
assertEquals(sspec2n0, geqSPEC[[10,3,1],[10,4,-2],[10,5,1]])

id := findID1(10, idxs, rspec, 2, 1, idxs);
pretty(id, qIdentity + expand + nonModular + asExpression)
sspec2n1 := coSpecification id;
assertEquals(sspec2n1, geqSPEC[[10,1,-1],[10,2,1],[10,4,-1],[10,5,1]])

-- And now for $R(q)^{-1}$.

id := findID1(10, idxs, inv rspec, 2, 0, idxs);
pretty(id, qIdentity + expand + nonModular + asExpression)
sspec2n0 := coSpecification id;
assertEquals(sspec2n0, geqSPEC[[10,1,1],[10,2,-2], [10,5,1]])

id := findID1(10, idxs, inv rspec, 2, 1, idxs);
pretty(id, qIdentity + expand + nonModular + asExpression)
sspec2n1 := coSpecification id;
assertEquals(sspec2n1, geqSPEC[[10,2,-1],[10,3,-1],[10,4,1],[10,5,1]])

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
