-- -*- coding: utf-8 -*-
-- Via jupytext this file can be shown as a jupyter notebook.

)read /home/hemmecke/g/qeta/input/jfricas-test-support.input

-- # Problem formulation

-- Let $p(n)$ denote the number of partitions of the natural number $n$. Show that for every natural number $k$ the expression $p(11k+6)$ is divisible by 11.
--
-- The solution is described in the article (http://www.sciencedirect.com/science/article/pii/S0747717117300147):
--
--     @Article{Hemmecke:DancingSambaRamanujan:2018,
--       author =       {Ralf Hemmecke},
--       title =        {Dancing Samba with {R}amanujan Partition
--                       Congruences},
--       journal =      {Journal of Symbolic Computation,
--       year =         2018,
--       JournalUrl =   {http://www.sciencedirect.com/science/journal/07477171},
--       URL =          {http://www.sciencedirect.com/science/article/pii/S0747717117300147},
--       DOI =          {10.1016/j.jsc.2017.02.001},
--       volume =       {84},
--       pages =        {14--24},
--       ISSN =         {0747-7171},
--       keywords =     {Partition identities, Number theoretic algorithm,
--                       Subalgebra basis},
--       abstract =     {The article presents an algorithm to compute a
--                       $C[t]$-module basis $G$ for a given subalgebra $A$
--                       over a polynomial ring $R=C[x]$ with a Euclidean
--                       domain $C$ as the domain of coefficients and $t$ a
--                       given element of $A$. The reduction modulo $G$
--                       allows a subalgebra membership test. The algorithm
--                       also works for more general rings $R$, in particular
--                       for a ring $R\subset C((q))$ with the property that
--                       $f\in R$ is zero if and only if the order of $f$ is
--                       positive. As an application, we algorithmically
--                       derive an explicit identity (in terms of quotients
--                       of Dedekind $\eta$-functions and Klein's
--                       $j$-invariant) that shows that $p(11n+6)$ is
--                       divisible by 11 for every natural number $n$ where
--                       $p(n)$ denotes the number of partitions of $n$.}
--      }
--
-- in particlular, what we here call `algebraBasis` corresponds to the
-- `samba` algorithm from the article.
--
-- The article is also available as <a
-- href="http://www.risc.jku.at/publications/download/risc_5338/DancingSambaRamanujan.pdf">RISC
-- report 16-06</a>.

-- ## Explanation of the session
--
-- Let X be an algebra that can be used in with the algorithm `samba`,
-- but which has a polynomial attached to record the operations that
-- have been done with the original element. This somehow simulates a
-- similar idea as attaching a unit matrix to a matrix and then
-- computing the row echelon form of the matrix. The unit matrix turns
-- into the transformation matrix.
--
-- Since we want to show divisibility by 11 of $p(11k+6)$ for every
-- $k$ where $p$ is the partition function, the main computation is
-- done with a coefficient ring $C=\mathbb{Z}_{(11)}$ being integers
-- localized at 11, i.e. rational numbers that have no denominator
-- divisible by 11.

-- +
-------------------------------------------------------------------
--setup
-------------------------------------------------------------------
-- -

C ==> IntegerLocalizedAtPrime 11
L ==> A1 C
X ==> X1 C

-- The following function simply maps the integer coefficients of the
-- series into the localized coefficient domain or into the rational
-- number domain Q.

abfmap(A, B, f, x) ==> map(f, x)$QEtaLaurentSeriesFunctions2(A, B)
abffmap(A, B, f, x) ==> abfmap(A, B, f, x::L1(A))::A1(B)
abmap(A, B, x) ==> abffmap(A, B, (c: A): B +-> c::B, x)
zcmap(x) ==> abmap(Z, C, x)
cqmap(x) ==> abmap(C, Q, x)
zqmap(x) ==> abmap(Z, Q, x)

level := 22;

PolC ==> Pol C; LC ==> A1 C
PolZ ==> Pol Z; LZ ==> A1 Z
PolQ ==> Pol Q; LQ ==> A1 Q
ETAZ ==> EtaQuotient Z

smaller11(x: PolC, y: PolC):Boolean == _
  exponent leadingCoefficient x < exponent leadingCoefficient y
smallerT(x: PolC, y: PolC):Boolean == degree(x, 'T) < degree(y, 'T)


-- +
-------------------------------------------------------------------
--endsetup
-------------------------------------------------------------------
-- -

-- # Relations in terms of the $M_i$'s

-- +
-------------------------------------------------------------------
--test:Hemmecke
-------------------------------------------------------------------
-- -


-- The generators of the $\eta$-quotients of level 22 are given by
-- these exponents for the $\eta$ functions (with arguments being the
-- divisors of 22). The variable `rgens` corresponds to the generators
-- of $R^\infty(22)$.

rgens := etaQuotientMonoidExponentVectors level;
rgensExpected: List List Z := [_
    [- 1, 1, 11, - 11], [- 4, 8, 4, - 8], [7, - 3, 3, - 7], _
    [- 2, 6, 6, - 10], [0, 4, 8, - 12], [2, 2, 10, - 14], [4, 0, 12, - 16]];

assertEquals(rgens, rgensExpected)

divs := divisors(level)$IntegerNumberTheoryFunctions;
etaquotgensZ := [etaQuotient(divs, r)$ETAZ for r in rgens]
for x in etaquotgensZ repeat assertTrue(one? denom eulerExponent x)

eqgensZ := [expansion x for x in etaquotgensZ];
rfeqgensZ := [_
  (3*q^5+2*q^4+2*q^3+q^2+q+1)/q^5,_
  (12*q^5+13*q^4+8*q^3+6*q^2+4*q+1)/q^5,_
  (-21*q^5+2*q^4-14*q^3+17*q^2-7*q+1)/q^5,_
  (-6*q^5-q^4-2*q^3-q^2+2*q+1)/q^6,_
  (8*q^6+2*q^4-4*q^2+1)/q^7,_
  (-10*q^7-q^6+2*q^4+6*q^3-3*q^2-2*q+1)/q^8,_
  (9*q^8+8*q^7-10*q^6-4*q^5-5*q^4+8*q^3+2*q^2-4*q+1)/q^9];
assertEquals([rationalFunction(x, 0) for x in eqgensZ], rfeqgensZ)

eqgens := [zcmap x for x in eqgensZ];
assertEquals([qetaGrade x for x in eqgens], [5, 5, 5, 6, 7, 8, 9])

msyms := indexedSymbols("M", #eqgens)
assertEquals(msyms, [M1, M2, M3, M4, M5, M6, M7])

xeqgens := [toX1(C, x, s) for x in eqgens for s in msyms]; -- List X

-- We take a minimal (in terms the order in q) element from the
-- generators and make it special. Then we compute an algebra basis
-- for $C[M_1,\ldots,M_7]$.
-- 19-Feb-2020: Interestingly, it does not work for the element
-- corresponding to [- 1, 1, 11, - 11].
-- We choose the vector corresponding to [- 4, 8, 4, - 8].
tex := xeqgens.2  -- X
abex := samba(tex, xeqgens)$QXTOPSAMBA1(C)

-- Due to another critical element selection strategy, the resulting
-- polynomial is a bit different.

bas := sort(smallerGrade?, basis abex);
abexExpectedArticle := [1/128*M7-5/128*M6+21/128*M5_
  +(-1/1024*M1+11*213/1024)*M4-85/128*M3+1/1024*M1^2+505/1024*M1,_
  1/8*M4-1/8*M1, M3, M5, M6] -- LPol C
abexExpected := [3/8*M3+M2+11*(-1/8)*M1, 1/3*M2-1/3*M1, M4, M5, M6] --: LPol C
assertEquals([second(x)::PolC for x in bas], abexExpected)
assertEquals(# bas, 5)

-- ## Relation for divisibility of $p(11k+6)$ by 11

-- Let's first define $F$ as on top of page 30 of
-- \cite{Radu:RamanujanKolberg:2015}, i.e.
-- $$
-- F(\tau) = q^{\frac{13}{24}}
--           \frac{\eta(\tau)^{10}\eta(2\tau)^2\eta(11\tau)^{11}}
--                {\eta(22\tau)^{22}}
--           \sum_{k=0}^{\infty}p(11k+6)q^k
-- $$
--
-- First we define the partion series.

ps := partitionSeries(1)$QFunctions(Z, LZ) --: LZ

-- Then we select the sub-series given by the (11n+6)-th term.

r11 := choose(11, 6, ps, 0) --: LZ
r11Expected := _
  1188908248*q^10+342325709*q^9+92669720*q^8+23338469*q^7+5392783*q^6_
  +1121505*q^5+204226*q^4+31185*q^3+3718*q^2+297*q+11 --: Fraction PolZ
assertEquals(rationalFunction(r11, 10), r11Expected)

pfZ := (monomial(1,13)$QLS(Z)*expansion(etaQuotient(divs, [10,2,11,-22])$ETAZ))::LZ
pfZExpected: PolZ := _
  -1079*q^14+715*q^13-2465*q^12-375*q^11+1211*q^10+352*q^9+11*q^8-726*q^7_
  +177*q^6+308*q^5-176*q^4-10*q^3+33*q^2-10*q+1
assertEquals(numer rationalFunction(pfZ, 0), pfZExpected)
assertEquals(qetaGrade pfZ, 14)

fe := pfZ * r11
feExpected := _
  1272909*q^14+944328*q^13+689282*q^12+485254*q^11+327415*q^10+214500*q^9+_
  135608*q^8+79211*q^7+44396*q^6+22286*q^5+10164*q^4+3696*q^3+1111*q^2+187*q+11
  --: PolZ
assertEquals(numer rationalFunction(fe, 0), feExpected)
assertEquals(qetaGrade fe, 14)

fex := toX1(C, zcmap fe, F) --: X


-- Now we can reduce this element `fex` by the respective algebra
-- basis, taking into account that `tex` is our special $t$ that we
-- used in the construction of the algebra basis of the
-- $\eta$-quotients of level 22.
--
-- As can be seen from the sorted monomials of the representaion, $F$
-- can be expressed as $F=11p(M_1,\ldots, M_7)$ where $p$ is a
-- polynomial having coefficient denominators not divisible by 11.

rex := reduce(fex, abex)$QXTOPRED1(C)
assertTrue(zero? rex)

-- Due to a different reduction strategy in a newer implementation of
-- Samba, we don't get exactly the same result as in
-- \cite{Hemmecke:DancingSambaRamanujan:2018}.

mrex := [(inv(unitPart leadingCoefficient x)::C)*x _
         for x in monomials(second(rex)::PolC)]
mrexExpectedArticle := _
  [F, 11*M1^2, 11*M1^3, 11*M1*M3, 11*M1^2*M4, 11*M1*M5, 11^2*M1, 11^2*M3,_
  11^2*M1*M4, 11^2*M5, 11^2*M1*M6, 11^2*M6, 11^2*M7, 11^3, 11^3*M4]$LPol(C)
mrexExpected := _
  [F, 11*M2^2, 11*M2^3, 11*M1*M2^2, 11*M2*M3, 11*M2^2*M3, 11*M2*M4, _
  11*M2*M5, 11^2*M2, 11^2*M1*M2, 11^3*M3, 11^3*M4, 11^3*M5, 11^4, 11^5*M1]$LPol(C)
assertEquals(sort(smaller11, mrex), mrexExpected)

-- Let's normalize the leading coefficient of $F$ to 1.

rexq := (second rex)::PolC;
cfxq := coefficient(rexq, 'F, 1)
lcfxq := leadingCoefficient cfxq
lcfxqArticle := -1/1360
assertEquals(lcfxq, 1)
ilcfxq := (inv lcfxq)::C
rxq := ilcfxq * rexq
rxqExpectedArticle := _
  (11^2*(-3068)*M7+(11^2*(-3)*M1+11^2*(-4236))*M6+(11*(-285)*M1_
  +11^2*(-5972))*M5+(11*(-1/8)*M1^2+11^2*(-4497/8)*M1+11^3*(-789/2))*M4_
  +(11*(-1867)*M1+11^2*(-2476))*M3+11*1/8*M1^3+11*1011/8*M1^2_
  +11^2*1647/2*M1+F+11^3*1360)$PolC
rxqExpected := (11*(
  (M2+11^2*7)*M5_
  +(3*M2+11^2*5)*M4_
  +((-1/4)*M2^2+(-245/8)*M2+11^2*(-1/8))*M3_
  +(-1)*M2^3+(5/4*M1+3)*M2^2_
  +(11*(-49/8)*M1+11*(-13))*M2_
  +11^4*1/8*M1+11^3*(-1))_
  +F)$PolC
assertEquals(rxq, rxqExpected)

-- Let's plug in the original $\eta$-quotients into `rxq`.


sers := cons(first fex,eqgens) --: List L
-- Note that after some changes to the sorting order in the
-- implementation, the old Mi correspond to other new Mi.
sersArticle:=cons(first fex, [eqgens.i for i in [3,2,4,1,5,6,7]])
ezArticle := eval(rxqExpectedArticle, c+->c::L, cons('F,msyms), sersArticle)_
      $ PolynomialEvaluation(C, L)
assertTrue(zero? ezArticle)
ez := eval(rxq, c+->c::L, cons('F,msyms), sers)_
      $ PolynomialEvaluation(C, L)
assertTrue(zero? ez)


-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -


-- # Checking modularity

-- +
-------------------------------------------------------------------
--test:modularity
-------------------------------------------------------------------
-- -

-- We are looking for an expression of
-- $S=\sum_{k=0}^{\infty}p(11k+6)q^k$ in terms of Dedekind
-- $\eta$-functions.
--
-- Note that the partition series is (up to a factor of $q^{1/24}$)
-- given by $\eta(\tau)^{-1}$. Thus for the divisors $\{1,11\}$ of
-- $M=11$, that gives us the vector $s=(-1, 0)$. The cofactor exponent
-- from the $F$ given at the beginning of the previous section is
-- $r=(10,2,11,-22)$ and corresponds to the divisors $\{1,2,11,22\}$
-- of $N=22$.
--
-- The following function, checks whether all the data corresponding
-- to $F$ from above indeed leads to a modular function.

assertTrue(modularConditions?(22,[10,2,11,-22],11,[-1,0],11,6))

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # Express in Klein's J-invariant

-- +
-----------------------------------------------------------------
--NONWORKING test:Klein
-----------------------------------------------------------------
-- -

-- The Test is currently disabled, because for some reason it takes
-- too long in newer versions (2.0) of QEta.

-- Let
-- $$
-- U(v):=t^v q \prod_{n=1}^{\infty}(1-q^{11n})\sum_{k=0}^{\infty}p(11k+6)q^k.
-- $$

-- Below, we show that $U(7)=11 t^2 h(t, j, j_p)$ where $j$, $j_p$,
-- and $t$ are Laurent series with integer coefficients and $h$ is a
-- polynomial with rational coefficients having no denominator that is
-- divisible by 11. In other words, we can give a relation $z U(7) =
-- 11 t^2 k(t, j, j_p)$ with an integer polynomial $k$ and an integer
-- $z$ such that $\gcd(z,11)=1$.
--
-- We choose Klein's $j$-invariant
-- (https://en.wikipedia.org/wiki/J-invariant#The_q-expansion_and_moonshine)
-- expressed as a Laurent series in terms of $q = \exp(2\pi i \tau)$
-- as the series $j$ above.
--
-- The series $j_p$ is given by replacing $q$ by $q^{11}$ in $j$.
--
-- The series $t$ is given by
-- $$
-- t =
-- \left(\frac{\eta(\tau)}{\eta(11\tau)}\right)^{12}
-- $$
-- (again in terms of $q = \exp(2\pi i \tau)$) where $\eta$ is Dedekind's
-- $\eta$-function
-- (https://en.wikipedia.org/wiki/Dedekind_eta_function#Definition).
--
--
-- ## Construction of Klein's $j$-invariant

-- Klein's $j$-invariant can be given by the expression
-- $$j(\tau) = E_4^3/\Delta$$ where
-- $$\Delta=\frac{E_4^3-E_6^2}{1728}=\eta(\tau)^{24}$$ and the
-- normalize Eisenstein series $E_4$ and $E_6$ are given by
-- $$E_4=1+240 \sum_{n=1}^\infty \sigma_3(n)q^n$$ and $$E_6=1-504
-- \sum_{n=1}^\infty \sigma_5(n)q^n$$ where $q = \exp(2\pi i\tau)$ and
-- $\sigma_k(n)$ denotes the sum of the $k$-th power of the positive
-- divisors of $n$.
--
-- All the above are series with integers coefficients.
-- For our computation, however, we compute with integers localized at
-- the prime 11, i.e. these are rational numbers with denominators not
-- divisible by 11.
--
-- The following function simply maps the integer coefficients of the
-- series into the localized coefficient domain and eventually creates
-- an object that attaches the indeterminate $J$ to the series.

i := kleinJInvariant()$QFunctions(C, L) --: L
j := toX1(C, i, J); --: X
first j, second j

ip := multiplyExponents(i, 11)$L;
jp := embed(ip, Jp)$X;
first jp, second jp

-- ### The series $t$

-- The value $t$ is given by $$t =
-- \left(\frac{\eta(\tau)}{\eta(11\tau)}\right)^{12}$$ in terms of $q
-- = \exp(2\pi i \tau)$.

tz := expansion(etaQuotient([1,11],[12,-12])$ETAZ); --: LZ
tt := zcmap tz; --: L
t := toX1(C, tt, T); --: X
first t, second t

-- ## Construct elements suitable for the samba algorithm

m1 := t; --: X

-- We need some auxiliary values $m_2$ and $m_3$.

m2 := j*t^3; --: X
m3 := jp*t;  --: X

-- We want yet another value to end up in an algebra basis of orders
-- -2 and -3.
--
-- We ignore this for now, because with the data that produces a
-- relation among $F$, $J$, $J_p$, and $T$, the computation did not
-- finish.

m := jp; mp := j;
while (d := qetaGrade(m)$X) > 0 repeat (_
    c := qetaLeadingCoefficient(m)$X;_
    e := d::N;_
    m := m-c*j^e;_
    mp := mp-c*jp^e)

-- mp
--
--     [-q^(-121)+O(q^(-110)),_
--     -Jp^11_
--     +11*744*Jp^10_
--     +11(-2570796)*Jp^9_
--     +11*4880620256*Jp^8_
--     +11*(-5550817150590)*Jp^7_
--     +11^2*351821430873072*Jp^6_
--     +11^2*(-147929969197380488)*Jp^5_
--     +11^2*35519316022933293120*Jp^4_
--     +11^2*(-4373015221856525734395)*Jp^3_
--     +11^2*224874476512864841450600*Jp^2_
--     +11^2*(-3096214928567780111700972)*Jp_
--     +J]

mgens := [m1,m2,m3]

-- ### The algebra basis computation

-- Then we compute the algebra basis for the elements $t=m_1$, $m_2$,
-- and $m_3$.
--
-- Since we compute in an extended algebra. The second component of
-- each entry in the basis gives its expression in terms of the
-- original generators.

--approx. 75 seconds
ab := samba(t, mgens)$QXTOPSAMBA1(C);

assertEquals(# basis ab, 12)

-- Show the powers of 11 in the coefficients of the representation (in
-- terms of J, Jp, T) of the algebra basis elements.

pows := [[exponent(x) for x in coefficients second ab.n] for n in 2..#ab]
powsExpected := [_
  [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,_
   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 0, 0, 0, 0, 1, 0, 0, 0,_
   1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 2, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 1,_
   1, 0, 1, 0, 0, 0, 1, 0, 0, 1, 2, 2, 0, 0, 0, 0, 0, 0, 0, 2, 2, 3, 0, 0,_
   1, 1, 1, 0, 3, 3, 0, 0, 0, 3, 1, 3, 4, 4, 4, 5, 4, 6, 6, 6, 8, 8],_
  [0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0,_
   0, 0, 0, 0, 1, 0, 0, 2, 0, 0, 0, 0, 0, 0, 2, 0, 0, 0, 0, 0, 0, 0, 1, 0,_
   0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 0, 0, 0, 1, 0, 1, 1, 3, 2, 0, 1, 0, 1,_
   0, 2, 2, 3, 0, 1, 0, 1, 0, 3, 3, 1, 2, 3, 1, 3, 4, 1, 1, 1, 4, 4, 2, 2,_
   4, 4],_
  [0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0,_
   1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 0, 0, 0, 0, 0, 1, 1, 2, 0, 0,_
   0, 0, 0, 2, 2, 0, 0, 2, 0, 2, 3, 0, 0, 0, 3, 4, 3, 4, 3],_
  [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 0, 1,_
   0, 2, 1, 1, 1, 1, 1, 1, 2, 3, 0, 2, 0, 3, 3, 1, 3, 1, 3, 3, 1, 1, 3, 3,_
   2, 2, 3],_
  [0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,_
   0, 0, 0, 0, 0, 0, 0, 2, 0, 0, 1, 0, 1, 2, 0, 0, 0, 0, 0, 2, 2, 2, 0, 1,_
   0, 1, 0, 2, 3, 0, 0, 3, 0, 3, 4, 1, 1, 1, 3, 3, 2, 2, 3],_
  [0, 0, 0],_
  [0],_
  [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 0, 1, 0, 1, 3, 2, 0, 2,_
   0, 2, 2, 0, 2, 0, 2, 2, 2, 2, 1, 1],_
  [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 0, 1, 0, 2, 1, 0, 1, 0,_
   1, 1, 0, 0, 1, 1],_
  [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0],_
  [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]]$List(List N)
assertEquals(pows, powsExpected)

-- The (absolute value of the) order and the power of 11 in the
-- leading coefficient of the elements in the algebra basis.

gl := [[qetaGrade i, unitCanonical qetaLeadingCoefficient i] for i in basis ab]
glExpected := [[7, 11^18], [8, 11^8], [9, 11^4], [11, 11^3],_
  [12, 11^3], [13, 11^3], [14, 1], [16, 1], [17, 11^2], [18, 11],_
  [11*2, 1], [23, 1]]$List(List C)
assertEquals(gl, glExpected)

-- We define
-- $$
-- U(v):=t^v q \prod_{n=1}^{\infty}(1-q^{11n})\sum_{k=0}^{\infty}p(11k+6)q^k.
-- $$
--
-- Let's do this step by step. We take the partition series.

ps := partitionSeries(1)$QFunctions(Z, LZ) --: LZ

-- We also take the sub-series given by the (11n+6)-th term of ps as
-- defined earlier. The coefficients of the series `r11` we want to
-- check for divisibility by 11.

r11 := choose(11, 6, ps, 0) --: LZ

-- Putting all together (except the factor $t^v$) gives the following expression.


q := monomial(1, 1)$LZ
fz := q*eulerFunction(11)$QFunctions(Z, LZ)*r11; --: LZ
f := embed(zcmap fz, F)$X


-- Now let's check what the smallest $v>0$ is such that $U(v)=t^v f$
-- is in the $\mathbb{Q}[t]$-module generated by $\{1, z_1, \ldots,
-- z_{12}\}$ where the $z_i$ are given by the list `ab`.
--
-- Although for the coefficient domain $\mathbb{Q}$ we find $v=3$,
-- with the coefficients being in $C=\mathbb{Z}_{(11)}$, the reduction
-- to zero only starts to work with $v=7$.

qab := [cqmap first x for x in basis ab];
qt := cqmap first t
qreds := [zero? reduce(cqmap(first(t^v * f)), qt, qab)$QEtaTopReduction(Q, LQ)_
          for v in 1..7]
qredsExpected := [false, false, true, true, true, true, true]
assertEquals(qreds, qredsExpected)

-- With $v=3$ the reduction does not lead to 0 in case the
-- coefficients are non-rational, but rather lie in
-- $\mathbb{Z}_{(11)}$.

assertFalse(zero? reduce(t^3*f, t, ab)$QTOPRED1(C))

assertEquals(_
  [zero? reduce(t^v * f, t, ab)$QEtaTopReduction(C, X) for v in 1..10],_
  [false, false, false, false, false, false, true, true, true, true])

-- Since all involved series in the algebra basis `ab` have their
-- representation in terms of $J$, $Jp$, and $T$ attached, we have
-- found a relation in $F$, $J$, $Jp$, and $T$.

result := reduce(t^7 * f, t, ab)$QEtaTopReduction(C, X);
assertTrue(zero? result)
res := second result;
assertEquals(variables res, [T, Jp, J, F])

-- The leading coefficient is a rational number whose numerator and
-- denominator are not divisible by 11, i.e., the `exponent` (of 11)
-- of the coefficient is zero.

cf := coefficient(res, 'F, 1);
lcf := leadingCoefficient cf
assertTrue(zero? exponent lcf)

-- Let's normalize the coefficient in front of the variable $F$ such
-- that it becomes 1, i.e., we multiply by the inverse of the
-- coefficient.

r := (inv(unitPart lcf)::C)*res;

-- Let us investigate the coefficients of all monomials of $r$.

mr := [(inv(unitPart leadingCoefficient x)::C)*x for x in monomials r];
sort(smaller11, mr)
sort(smallerT, mr)

-- The above shows that we have found a relation
-- $$
-- U(7):=t^7 q \prod_{n=1}^{\infty}(1-q^{11n})
--             \sum_{k=0}^{\infty}p(11k+6)q^k
--      = 11 t^2 h(t,j,j_p)
-- $$
-- where $h$ is a polynomial in $\mathbb{Z}_{(11)}[t,j,j_p]$.

-- Let us create output that can directly pasted into a journal paper.

tups := [(d:=degree(x, [T,J,Jp]); [d.1-2,d.2,d.3]) for x in rest sort(smaller11, mr)];
assertEquals(#tups, 161)

tupsExpected: List List Z := [_
  [0, 0, 0], [0, 0, 1], [0, 0, 2],_
  [1, 0, 0], [1, 0, 1], [1, 0, 2], [1, 0, 3],_
  [2, 0, 0], [2, 0, 1], [2, 0, 2], [2, 0, 3], [2, 0, 4],_
  [2, 1, 0], [2, 1, 1],_
  [3, 0, 0], [3, 0, 1], [3, 0, 2], [3, 0, 3], [3, 0, 4],_
  [3, 1, 0], [3, 1, 1], [3, 1, 2],_
  [4, 0, 0], [4, 0, 1], [4, 0, 2], [4, 0, 3], [4, 0, 4],_
  [4, 1, 0], [4, 1, 1], [4, 1, 2], [4, 1, 3],_
  [4, 2, 0],_
  [5, 0, 0], [5, 0, 1], [5, 0, 2], [5, 0, 3], [5, 0, 4],_
  [5, 1, 0], [5, 1, 1], [5, 1, 2], [5, 1, 3],_
  [5, 2, 0], [5, 2, 1],_
  [6, 0, 0], [6, 0, 1], [6, 0, 2], [6, 0, 3], [6, 0, 4],_
  [6, 1, 0], [6, 1, 1], [6, 1, 2], [6, 1, 3],_
  [6, 2, 0], [6, 2, 1], [6, 2, 2],_
  [7, 0, 0], [7, 0, 1], [7, 0, 2], [7, 0, 3], [7, 0, 4],_
  [7, 1, 0], [7, 1, 1], [7, 1, 2], [7, 1, 3],_
  [7, 2, 0], [7, 2, 1], [7, 2, 2],_
  [7, 3, 0],_
  [8, 0, 0], [8, 0, 1], [8, 0, 2], [8, 0, 3], [8, 0, 4],_
  [8, 1, 0], [8, 1, 1], [8, 1, 2], [8, 1, 3],_
  [8, 2, 0], [8, 2, 1], [8, 2, 2],_
  [8, 3, 0], [8, 3, 1],_
  [9, 0, 0], [9, 0, 1], [9, 0, 2], [9, 0, 3],_
  [9, 1, 0], [9, 1, 1], [9, 1, 2], [9, 1, 3],_
  [9, 2, 0], [9, 2, 1], [9, 2, 2],_
  [9, 3, 0], [9, 3, 1],_
  [10, 0, 0], [10, 0, 1], [10, 0, 2], [10, 0, 3],_
  [10, 1, 0], [10, 1, 1], [10, 1, 2], [10, 1, 3],_
  [10, 2, 0], [10, 2, 1], [10, 2, 2],_
  [10, 3, 0], [10, 3, 1],_
  [10, 4, 0],_
  [11, 0, 0], [11, 0, 1], [11, 0, 2],_
  [11, 1, 0], [11, 1, 1], [11, 1, 2],_
  [11, 2, 0], [11, 2, 1], [11, 2, 2],_
  [11, 3, 0], [11, 3, 1],_
  [11, 4, 0],_
  [12, 0, 0], [12, 0, 1], [12, 0, 2],_
  [12, 1, 0], [12, 1, 1], [12, 1, 2],_
  [12, 2, 0], [12, 2, 1], [12, 2, 2],_
  [12, 3, 0], [12, 3, 1],_
  [12, 4, 0],_
  [13, 0, 0], [13, 0, 1],_
  [13, 1, 0], [13, 1, 1],_
  [13, 2, 0], [13, 2, 1],_
  [13, 3, 0], [13, 3, 1],_
  [13, 4, 0],_
  [14, 0, 0], [14, 0, 1],_
  [14, 1, 0], [14, 1, 1],_
  [14, 2, 0], [14, 2, 1],_
  [14, 3, 0], [14, 3, 1],_
  [14, 4, 0],_
  [15, 0, 0], [15, 1, 0], [15, 2, 0], [15, 3, 0], [15, 4, 0],_
  [16, 0, 0], [16, 1, 0], [16, 2, 0], [16, 3, 0], [16, 4, 0]];

assertEquals(sort tups, tupsExpected)

-- Size of numerator and denominators of the coefficients of the
-- linear combination of the powerproducts.

sz(x) ==> floor((log(abs(numer(x::Q)::Float))/log(10.0))::Float)::Z
sizes := remove(0, sort [sz x for x in coefficients r])
assertEquals(min sizes, 2324)
assertEquals(max sizes, 2395)
degs := sort [degree(x, T) for x in mr]
degsExpected: List N := _
  [2, 2, 2, 3, 3, 3, 3, 4, 4, 4, 4, 4, 4, 4, 5, 5, 5, 5, 5, 5, 5, 5, 6, 6,_
   6, 6, 6, 6, 6, 6, 6, 6, 7, 7, 7, 7, 7, 7, 7, 7, 7, 7, 7, 7, 8, 8, 8, 8,_
   8, 8, 8, 8, 8, 8, 8, 8, 9, 9, 9, 9, 9, 9, 9, 9, 9, 9, 9, 9, 9, 10, 10,_
   10, 10, 10, 10, 10, 10, 10, 10, 10, 10, 10, 10, 11, 11, 11, 11, 11, 11,_
   11, 11, 11, 11, 11, 11, 11, 12, 12, 12, 12, 12, 12, 12, 12, 12, 12, 12,_
   12, 12, 12, 13, 13, 13, 13, 13, 13, 13, 13, 13, 13, 13, 13, 14, 14, 14,_
   14, 14, 14, 14, 14, 14, 14, 14, 14, 15, 15, 15, 15, 15, 15, 15, 15, 15,_
   16, 16, 16, 16, 16, 16, 16, 16, 16, 17, 17, 17, 17, 17, 18, 18, 18, 18, 18]
assertEquals(degs, degsExpected)

-- ## Check the relation

-- Plugging in the series $f$, $j$, $j_p$, $t$, for the indeterminates
-- $F$, $J$, $J_p$, $T$ in $r$ should result in the zero series.

-- approx. 45 seconds
fjjpt: List Symbol := [F, J, Jp, T];
z := eval(r, c+->c::A1, fjjpt, [first f, first j, first jp, first t])_
     $ PolynomialEvaluation(C, A1)
assertTrue(zero? z)

-- Let's check whether the relation interpreted with rational
-- coefficients also yields zero. This checks against bugs in
-- `IntegerLocalizedAtPrime`.

-- approx. 30 seconds
rq := r::Polynomial(Fraction Integer);
zq := eval(rq, c+->c::LQ, fjjpt, [cqmap first f, cqmap i, cqmap ip, zqmap tz])_
      $ PolynomialEvaluation(Q, LQ)
assertTrue(zero? zq)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------

-- +
-------------------------------------------------------------------
--test:Klein_rational
-------------------------------------------------------------------
-- -

-- # The same algorithm can be used over the rational numbers $\mathbb{Q}$

-- However, that computation leads to only 5 elements in the algebra
-- basis.

LQ ==> A1 Q
XQ ==> X1 Q
i := kleinJInvariant()$QFunctions(Q, LQ); --: LQ
ip := multiplyExponents(i, 11); --: LQ
jq := toX1(Q, i,  J); --: XQ
jpq := toX1(Q, ip, Jp); --: XQ
tz := expansion(etaQuotient([1,11],[12,-12])$ETAZ); --: LZ
tq := toX1(Q, zqmap tz,  T); --: XQ
mqgens := [tq, jq*tq^3, jpq*tq]; --: List XQ
abq := samba(tq, mqgens)$QXTOPSAMBA1(Q);
assertEquals(# basis abq, 4)

bas := sort(smallerGrade?, basis abq);
degs := [[qetaGrade i, unitCanonical qetaLeadingCoefficient i] for i in bas]
degsExpected := [[7, 1], [8, 1], [9, 1], [11, 1]]$List(List Q)
assertEquals(degs, degsExpected)


ps := partitionSeries(1)$QFunctions(Q, LQ) --: LQ
r11 := choose(11, 6, ps, 0) --: LQ
q := monomial(1,1)$LQ
fq := embed(q*eulerFunction(11)$QFunctions(Q, LQ)*r11, F)$XQ

-- As we have seen above, $U(3)$ lies in the $\mathbb{Q}[t]$-module
-- generated by 1 and the elements of the algebra basis.

qreds := [zero? reduce(tq^v * fq, abq)$QXTOPRED1(Q) for v in 1..7]
qredsExpected := [false, false, true, true, true, true, true]
assertEquals(qreds, qredsExpected)

resultq := reduce(tq^3*fq, abq)$QXTOPRED1(Q);
assertTrue(zero? resultq)

resq := (second resultq)::PolQ;
cfq := coefficient(resq, 'F, 1);
lcfq := leadingCoefficient cfq;
assertEquals(numer lcfq, -1252111172757724101655226123277382591240584821954085843318203093446033379433146400428686578188183211)
assertEquals(denom lcfq, 3011581780596422507767778499914449384926148363732693753192663881848855429918948961031907889283682210836)

rq := inv(lcfq)*resq;

-- Reduction with respect to the rational basis is not useful to show
-- divisibility of $fq$ by 11, since it leads to denominators of some
-- coefficients of monomials that do not involve the variable $F$.

denoms := [unitCanonical(denom(x)::C) for x in monomials rq]
terms := [((inv(leadingCoefficient(x)))*x)::PolQ for x in monomials rq]
monoms := [d*t for d in denoms for t in terms]
monomsExpected := [_
  11^40*J^2*T^17, 11^40*J*T^17, 11^40*T^17, 11^40*J^2*T^16, 11^40*J*T^16,_
  11^40*T^16, 11^40*J*Jp*T^15, 11^40*Jp*T^15, 11^40*J^2*T^15, 11^40*J*T^15,_
  11^40*T^15, 11^40*J*Jp*T^14, 11^40*Jp*T^14, 11^40*J^2*T^14, 11^39*J*T^14,_
  11^39*T^14, 11^40*Jp^2*T^13, 11^39*J*Jp*T^13, 11^40*Jp*T^13,_
  11^39*J^2*T^13, 11^39*J*T^13, 11^39*T^13, 11^40*Jp^2*T^12,_
  11^39*J*Jp*T^12, 11^40*Jp*T^12, 11^39*J^2*T^12, 11^38*J*T^12, 11^38*T^12,_
  11^40*Jp^2*T^11, 11^37*J*Jp*T^11, 11^40*Jp*T^11, 11^38*J^2*T^11,_
  11^38*J*T^11, 11^38*T^11, 11^38*Jp^2*T^10, 11^38*J*Jp*T^10,_
  11^36*Jp*T^10, 11^38*J^2*T^10, 11^37*J*T^10, 11^35*T^10, 11^35*Jp^2*T^9,_
  11^35*J*Jp*T^9, 11^34*Jp*T^9, 11^35*J^2*T^9, 11^33*J*T^9, 11^32*T^9,_
  11^28*Jp^2*T^8, 11^29*J*Jp*T^8, 11^29*Jp*T^8, 11^31*J^2*T^8,_
  11^31*J*T^8, 11^29*T^8, 11^24*Jp^2*T^7, 11^24*J*Jp*T^7, 11^23*Jp*T^7,_
  11^25*J^2*T^7, 11^25*J*T^7, 11^23*T^7, 11^18*Jp^2*T^6, 11^18*J*Jp*T^6,_
  11^18*Jp*T^6, 11^19*J^2*T^6, 11^19*J*T^6, 11^18*T^6, 11^13*Jp^2*T^5,_
  11^13*J*Jp*T^5, 11^12*Jp*T^5, 11^13*J*T^5, 11^12*T^5, 11^7*Jp^2*T^4,_
  11^6*J*Jp*T^4, 11^7*Jp*T^4, 11^7*J*T^4, 11^7*T^4, 11*Jp^2*T^3,_
  11*Jp*T^3, F*T^3, 11*T^3, Jp^2*T^2, Jp*T^2, T^2]$List(PolC)
assertEquals(monoms, monomsExpected)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # The relation in $M_i$'s with integer coefficients

-- +
-----------------------------------------------------------------
--test:integer
-----------------------------------------------------------------
-- -

-- Here $\mathbb{Z}$ is considered as a Euclidean domain wrt the
-- absolute value as the Euclidean size function.

LZ ==> A1 Z
XZ ==> X1 Z
divs := divisors(level)$IntegerNumberTheoryFunctions;
QEQEV ==> QEtaQuotientExponentVectors4ti2
eqmev ==> etaQuotientMonoidExponentVectors $ QEQEV
rgens := eqmev level;
etaquotgensZ := [etaQuotient(divs, r)$ETAZ for r in rgens]; --: List ETAZ
eqgensZ := [expansion x for x in etaquotgensZ]; --: List LZ
msyms := indexedSymbols("M", #rgens)
xeqgensZ := [toX1(Z, x, s) for x in eqgensZ for s in msyms] --: List XZ
tez := xeqgensZ.3
abez := samba(tez, xeqgensZ)$QXTOPSAMBA1(Z)
assertEquals(# basis abez, 5)

pfZ := (monomial(1,13)$QLS(Z) * expansion(etaQuotient(divs, [10,2,11,-22])$ETAZ))::LZ;
ps := partitionSeries(1)$QFunctions(Z, LZ) --: LZ
r11 := choose(11, 6, ps, 0) --: LZ
fe := pfZ * r11
fez := toX1(Z, fe, F) --: XZ

-- Interestingly, we have to multiply by 4, because otherwise the
-- reduction would not return 0.

rez := reduce(4*fez, abez)$QXTOPRED1(Z)
assertTrue(zero? rez)

rz := (second rez)::PolZ;
rzExpected := (11*(_
  ((-1800)*M3+11^2*(-14336))*M5_
  + ((-4696)*M3+11^2*53760)*M4_
  + (-4)*M3^3_
  + ((-12)*M2+16*M1+11*(-1139))*M3^2_
  + ((-74080)*M2+11*4603*M1+11*(-135680))*M3_
  + 11^2*84992*M2+11^3*(-28224)*M1+11^3*(-35840))_
  + 4*F)$PolZ
assertEquals(rz, rzExpected)

lcrez := [leadingCoefficient x for x in monomials(rz-4*F)];
divisibility11 := reduce(_+, [positiveRemainder(x, 11) for x in lcrez], 0)
assertEquals(divisibility11, 0)

ez := eval(rz, c+->c::LZ, cons('F,msyms), cons(first fez,eqgensZ))_
      $ PolynomialEvaluation(Z, LZ)
assertTrue(zero? ez)

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # Paule's proof via computation in $C[t,F]$

-- +
-----------------------------------------------------------------
--test:Paule
-----------------------------------------------------------------
-- -

LZ ==> A1 Z
X ==> X1 C
ps := partitionSeries(1)$QFunctions(Z, LZ) --: LZ
r11: LZ := choose(11, 6, ps, 0)
q := monomial(1,1)$LZ
fz := q*eulerFunction(11)$QFunctions(Z, LZ)*r11;--: LZ
tz := expansion(etaQuotient([1,11],[12,-12])$ETAZ); --: LZ
fep := toX1(C, zcmap(tz*fz), Y) --: X
t := toX1(C, zcmap tz, T); --: X
abp := samba(t, [fep])$QXTOPSAMBA1(C);
assertEquals(# basis abp, 4)
pols := sort((x,y)+->degree x < degree y, [(second x)::PolC for x in basis abp])
polsExpected := [Y, Y^2, Y^3, Y^4]$List(PolC)
assertEquals(pols, polsExpected)

rep := reduce((fep)^5, abp)$QXTOPRED1(C)
assertTrue(zero? rep)


pol := - (second rep)::PolC::PolQ::PolZ
polExpected := (-Y^5_
  + 5*11^4*Y^4_
  + 11^4*(251*T-2*5*11^4)*Y^3_
  + 11^3*(4093*T^2+2*3*5*11^5*31*T+2*5*11^9)*Y^2_
  + 11^4*(3*41*T^3-2^2*3*11^3*1289*T^2+2*5*11^8*17*T-5*11^12)*Y_
  + 11^5*(T+11^4)*(T^3+11^2*1321*T^2-3*7*11^7*T+11^11))$PolZ

assertEquals(pol, polExpected)

-- The above polynomial agrees with the relation that Peter Paule has
-- on his Slide 32 of hia talk at "The 2016 International Number
-- Theory Conference in honor of Krishna Alladi's 60th birthday"
-- (Gainesville, March 17-21, 2016).
-- Compare with formula (8) in \cite{Hemmecke::DancingSambaRamanujan:2018}.


-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
-- -

-- # With $\mathbb{Z}_{(11)}$ and division by 11

-- +
-----------------------------------------------------------------
--test:Hemmecke2
-----------------------------------------------------------------
-- -

-- **TODO:**
-- Introduce vectors in the second component so that we can track
-- whether the just reduced element is divisible by 11.

nn := 22; -- level
m := 11::P; mm := m; s := [-1, 0]$List(Z);
mmdivs := DIVISORS(mm) --: List P
shat := sigmaInfinity(mmdivs, s) --: Z
orbs := [orb for tt in 0..m-1 | (orb := orbit(shat,m,tt); one? # orb)]
t := first(first orbs) :: N

-- Let's compute the expansion (at $\infty$) of
-- $$
-- F(\tau) = g_r(\tau)
--           q^{\frac{e}{24}}
--           \sum_{k=0}^{\infty}p(m k + t)q^k.
-- $$

-- ## From input/convenience.input

-- generatingSeries for p(m*n+t)

siftedPartitionSeries(m: P, t: N): A1(Z) == _
  choose(m, t, partitionSeries(1)$QFunctions(Z, A1 Z), 0)

-- generating series for p(m*n+t) with cofactor eta quotient and
-- the fractional q power.

modularSiftedPartitionSeries(nn: P, m: P, t: N): A1 Z == (_
  mmdivs: List P := DIVISORS m;_
  s: List Z := cons(-1, rest [0 for i in mmdivs]);_
  r: List Z := etaCoFactorInfinity(nn, m, s, m, t)$QEtaModularFunctionTools;_
  vPrint("-- cofactor", r);_
  shat: Z := sigmaInfinity(mmdivs, s);_
  orbs := [orb for tt in 0..m-1 | (orb := orbit(shat, m, tt::N); one? # orb)];_
  if not(one? # orbs and one? # first orbs and t = first first orbs) then _
    error "not a single element orbit";_
  expectedGrade := qetaGrades(nn, r, m, s, m, t).(1/nn);_
  cofact := etaQuotient(DIVISORS nn, r)$ETA(Z);_
  fcf := monomial(1,-expectedGrade)$QLS(C)*eulerExpansion cofact;_
  fzf := fcf*siftedPartitionSeries(m, t));

-- ## Create modular series for p(11n+6)

fzf := modularSiftedPartitionSeries(nn, m, t);
ff := zcmap fzf; -- modular f with coefficients in C, i.e. living in A1
xf := toX1(C, ff, F)

-- The generators of the $\eta$-quotients of level 22 are given by
-- these exponents for the $\eta$ functions (with arguments being the
-- divisors of 22). The variable `rgens` corresponds to the generators
-- of $R^\infty(22)$.

nndivs := DIVISORS(nn);
QEQEV ==> QEtaQuotientExponentVectors4ti2
rgens := etaQuotientMonoidExponentVectors(nn)$QEQEV
eqgens := [expansion etaQuotient(nndivs, r)$ETAZ for r in rgens]
msyms := indexedSymbols("M", #rgens)
mxgens := [toX1(C, x, sym) for x in eqgens for sym in msyms];
xgens := cons(toX1(C, eqgens.1, 'T), rest mxgens);

xt: X := xgens.1
xab := samba(xt, xgens)$QXTOPSAMBA1(C)

-- Due to another critical element selection strategy, the resulting
-- polynomial is a bit different.

be := basisElements xab
bas := sort(smallerGrade?, basis xab);

-- We show that the basis element of degree 3 has coefficients that are
-- all divisible by 11. So we can divide it by 11.
-- The method is to multiply by t and track the cofactors of the
-- basis elements used in this reduction.
-- That this reduction should give 0 is clear, since xab is an
-- algebra basis.

x3 := (xt+1$X)*be.3.1-11*(be.3.2+3*be.2.1+5*be.1.1-xt+9*be.4.1-4*1$X)
(xt+1$X)
--x3 := xt*bas.1-11*(bas.5-5*bas.4+21*bas.3-89*xt+32*bas.1-11*(53*bas.2+12*1$X))
assertTrue(zero? x3)

-- It's still not a representation in terms of the Mi.
p := second(x3)::Pol(C);
[[e, x] for x in monomials(p)| zero?(e:=exponent leadingCoefficient x)]

-- +
-------------------------------------------------------------------
--endtest
-------------------------------------------------------------------
